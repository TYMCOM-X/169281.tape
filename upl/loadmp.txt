PROCEDURE LOADMP.GEN         %DENNIS L. ALLEN%
BEGIN
FIELDS
        BEG.POS                 AS '2N',
        END.POS                 AS '2N',
        FIELD.NAME              AS '30C',
        FIELD.PICTURE           AS '20C',
        FIELD.SEQ               AS '3N',
        FILE.RECORD             AS '80C',
        HASH                    AS '6C',
        INSTANCE.COUNT          AS '4ZN',
        L.L                     AS '15C',
        MAGNUM.TYPE             AS '9C',
        REL.NAME                AS '30C',
        YES.OR.NO               AS '1C'


SOURCE DIRALL.FILE FROM 'DIRALL.TMP' FIXED FORM
  INPUT FILE.RECORD, @CR

SOURCE RELFLD.FILE FROM 'RELFLD.TMP' FIXED FORM
  INPUT FILE.RECORD, @CR

SOURCE RELSIZ.FILE FROM 'RELSIZ.TMP' FIXED FORM
  INPUT FILE.RECORD, @CR

RELATION REL.INDEX IS
    KEY
        REL.NAME
    DATA
        HASH

RELATION RELFLD.INFO IS
    KEY
        REL.NAME,
        FIELD.SEQ
    DATA
        FIELD.NAME,
        FIELD.PICTURE

REPORT L TO 'LOAD.TXT-A' PAGE.SIZE 0

REPORT D TO 'DUMP.TXT-A' PAGE.SIZE 0
%  PHASE 1 - LOAD RELATIONAL DATA %

LET L.L = 'LINE.LENGTH 500'
TYPE TO TERMINAL 'BEGIN DATA LOAD',@CR,@CR
FOR EACH DIRALL.FILE
 IF (SUBSTR(FILE.RECORD,1,1) NE '"') AND
    (SUBSTR(FILE.RECORD,1,4) NE ' Use' AND 'ANY ' AND 'ALL ') THEN
  IF SUBSTR(FILE.RECORD,1,1) NE ' ' THEN
   BEGIN   % FIRST LINE OF THE PAIR  SAVE NAME AND TYPE %
    LET REL.NAME = SUBSTR(FILE.RECORD,1,POSITION(FILE.RECORD,' ',1))
    LET MAGNUM.TYPE = SUBSTR(FILE.RECORD,POSITION(FILE.RECORD,'-',1)+1)
   END
  ELSE
   IF SUBSTR(FILE.RECORD,1,12) NE '   Controls:' THEN
    BEGIN   % SECOND LINE OF THE PAIR INSERT TIME %
     LET HASH = SUBSTR(FILE.RECORD,4,6)
     IF MAGNUM.TYPE EQ 'RELDECL' THEN
      INSERT INTO REL.INDEX
    END

FOR EACH RELFLD.FILE
 IF SUBSTR(FILE.RECORD,1,1) NE ' ' THEN
  BEGIN
   LET REL.NAME = TRIM(FILE.RECORD)
   LET FIELD.SEQ = 0
  END
 ELSE
  BEGIN
   LET FIELD.NAME = SUBSTR(FILE.RECORD,7,POSITION(FILE.RECORD,' ',7)-7)
   LET FIELD.PICTURE = SUBSTR(FILE.RECORD,POSITION(FILE.RECORD,' ',7)+2)
   ADD 1 TO FIELD.SEQ
   INSERT INTO RELFLD.INFO
  END

FOR EACH RELSIZ.FILE
 BEGIN
  LET REL.NAME = SUBSTR(FILE.RECORD,1,POSITION(FILE.RECORD,' '))
  IF POSITION(FILE.RECORD,'-Declared') EQ 0 THEN
   BEGIN
    LET BEG.POS = POSITION(FILE.RECORD,':') + 2
    LET END.POS = POSITION(FILE.RECORD,' ',BEG.POS) - BEG.POS
    LET INSTANCE.COUNT = SUBSTR(FILE.RECORD,BEG.POS,END.POS) AS '4ZN'
   END
  ELSE
   LET INSTANCE.COUNT = 0
  IF INSTANCE.COUNT EQ 0 THEN
   DELETE FROM REL.INDEX VIA KEY
 END
%  PHASE 2 - GET LIST OF RELATIONS TO BE INCLUDED %

TYPE TO TERMINAL 'DO YOU WISH TO INCLUDE ALL POPULATED RELATIONS ? '
GET.N.Y:
 BEGIN
  ACCEPT FROM TERMINAL YES.OR.NO
  LET YES.OR.NO = UPPER.CASE(YES.OR.NO)
  IF YES.OR.NO NE 'Y' AND 'N' THEN
   BEGIN
    TYPE TO TERMINAL 'Y or N only please '
    REPEAT GET.N.Y
   END
 END
IF YES.OR.NO EQ 'N' THEN
 BEGIN
  TYPE TO TERMINAL 'REPLY TO RELATION NAME WITH Y TO INCLUDE OR N TO BYPASS',@CR
  FOR EACH REL.INDEX
   BEGIN
    TYPE TO TERMINAL TRIM(REL.NAME),' ? '
    GET.Y.N:
     BEGIN
      ACCEPT FROM TERMINAL YES.OR.NO
      LET YES.OR.NO = UPPER.CASE(YES.OR.NO)
      IF YES.OR.NO NE 'Y' AND 'N' THEN
       BEGIN
        TYPE TO TERMINAL 'Y or N only please '
        REPEAT GET.Y.N
       END
     END
    IF YES.OR.NO EQ 'N' THEN
     DELETE FROM REL.INDEX VIA KEY
   END
 END
%  PHASE 3 - GENERATE TEXT FOR DUMP PROCEDURE %

TYPE TO TERMINAL 'GENERATING TEXT FOR DUMP ROUTINE TO FILE DUMP.TXT',@CR

WRITE REPORT D
 BEGIN
  PRINT TO D 'PROCEDURE DUMP.RELATION.DATA',@CR,
        'BEGIN',@CR,@CR
  FOR EACH REL.INDEX
   BEGIN
    PRINT TO D '%  ',TRIM(REL.NAME),'  %',@CR,
          'REPORT ',HASH,'.RPT TO "',HASH,'.DTA-A" PAGE.SIZE 0 ',L.L,@CR,
          'FORMAT ',HASH,'.FIELDS',@CR
    FOR EACH RELFLD.INFO VIA (REL.NAME)
     IF POSITION(FIELD.PICTURE,'(COMMAS)') EQ 0 THEN
      PRINT TO D @TAB 4,TRIM(FIELD.NAME),',',@CR
     ELSE
      BEGIN
       LET SUBSTR(FIELD.PICTURE,POSITION(FIELD.PICTURE,'(COMMAS)')) = '"'
       ALTER RELFLD.INFO
       PRINT TO D @TAB 4,TRIM(FIELD.NAME),' ',TRIM(FIELD.PICTURE),',',@CR
      END
    PRINT TO D @TAB 4,'@CR',@CR,@CR
   END
  FOR EACH REL.INDEX
   PRINT TO D 'TYPE TO TERMINAL "DUMPING RELATION: ',TRIM(REL.NAME),'",@CR',@CR,
         'FOR EACH ',TRIM(REL.NAME),' WRITE REPORT ',HASH,'.RPT',@CR,
         ' PRINT TO ',HASH,'.RPT ',HASH,'.FIELDS',@CR,@CR
  PRINT TO D 'END',@CR
 END
%  PHASE 4 - GENERATE TEXT FOR LOAD PROCEDURE %

TYPE TO TERMINAL 'GENERATING TEXT FOR LOAD ROUTINE TO FILE LOAD.TXT',@CR

WRITE REPORT L
 BEGIN
  PRINT TO L 'PROCEDURE LOAD.RELATION.DATA',@CR,
        'BEGIN',@CR,@CR
  FOR EACH REL.INDEX
   BEGIN
    PRINT TO L '%  ',TRIM(REL.NAME),'  %',@CR,
          'SOURCE ',HASH,'.SRC FROM "',HASH,'.DTA" FIXED FORM ',L.L,@CR,
          '  INPUT',@CR
    FOR EACH RELFLD.INFO VIA (REL.NAME)
     PRINT TO L @TAB 4,TRIM(FIELD.NAME),' ',TRIM(FIELD.PICTURE),',',@CR
    PRINT TO L @TAB 4,'@CR',@CR,@CR
   END
  FOR EACH REL.INDEX
   PRINT TO L 'TYPE TO TERMINAL "LOADING RELATION: ',TRIM(REL.NAME),'",@CR',@CR,
         'FOR EACH ',HASH,'.SRC',@CR,
         ' BEGIN',@CR,
         '  INSERT INTO ',TRIM(REL.NAME),@CR,
         ' END',@CR,@CR
  PRINT TO L 'END',@CR
 END

END
