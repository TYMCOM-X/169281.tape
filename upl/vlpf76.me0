PROCEDURE ADD.DATA
BEGIN

FIELDS  SNAME   SAME AS SLSMAN.NAME

SCREEN FOR "TYM425"
FIELD.FILLER "_"

FRAME.FORMAT F1.CUST
"                              CUSTOMER INFORMATION"
" "
"     CUSTOMER NUMBER  %%%%%          SALESMAN NUMBER %%%%%"

FRAME.FORMAT F2.CUST
" "
"          SALESMAN NAME  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
" "
" "
"                              BILLING ADDRESS"
" "
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  %%%%%%%%%%%%%%%  %%%%%"
" "
"                              SHIPPING ADDRESS"
" "
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%"
"          %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%  %%%%%%%%%%%%%%%  %%%%%"

FRAME F1 FROM F1.CUST
FRAME.AREA 1 TO 3
DATA.NAME CUST.NO, SLSMAN.NO

FRAME F2 FROM F2.CUST
FRAME.AREA 4 TO 18
DATA.NAME SLSMAN.NAME, CUST.NAME, CUST.ADDR, CUST.CITY, CUST.STATE, CUST.ZIP,
          SHIP.NAME, SHIP.ADDR, SHIP.CITY, SHIP.STATE, SHIP.ZIP

FUNCTION.KEY TERMINATE IS "/" + @CR
FINISH LIST.SCREEN

LIST.SCREEN:  DISPLAY F1 USING TERMINATE
DIS.F1:  BEGIN
INPUT CUST.NO, SLSMAN.NO
SELECT CUSTOMER VIA KEY THEN
  BEGIN
  TYPE TO MESSAGE.AREA "THIS CUSTOMER NUMBER ALREADY USED FOR ", CUST.NAME
  PAUSE 5
  TYPE TO MESSAGE.AREA " "
  REPEAT DIS.F1
  END
ELSE
BEGIN
SELECT SALESMAN VIA KEY THEN
  MOVE SLSMAN.NAME TO SNAME
  ELSE
  BEGIN
  TYPE TO MESSAGE.AREA "SALESMAN NUMBER NOT VALID."
  PAUSE 3
  TYPE TO MESSAGE.AREA " "
  REPEAT DIS.F1
  END
DISPLAY F2
  BEGIN
  MOVE SNAME TO SLSMAN.NAME 
  INPUT CUST.NAME,CUST.ADDR,CUST.CITY,CUST.STATE,CUST.ZIP,
      SHIP.NAME,SHIP.ADDR,SHIP.CITY,SHIP.STATE,SHIP.ZIP
  INSERT INTO CUSTOMER
  REPEAT DIS.F1
  END

END
 
END

END
