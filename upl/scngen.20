PROCEDURE SCREEN.DIS.INQ (SCREEN.NAME)
BEGIN
  FIELDS
  LAST.SCREEN.NAME SAME AS SCREEN.NAME,
  CY AS 'C',
  CX AS 'C',
  CURSOR AS 'C'

  CONTROLS FOR RELATIONS
  CONCURRENT UPDATE

  INCLUDE TEXT "CRT.CONTROLS"
  PROCEDURE PROCESS.SCREEN.NAME
  BEGIN
    IF SCREEN.NAME NE LAST.SCREEN.NAME THEN
    SELECT SCREENS VIA KEY THEN
    BEGIN
      MOVE Y.COR TO CY
      MOVE X.COR TO CX
      MOVE SCREEN.NAME TO LAST.SCREEN.NAME
    END
    ELSE
    BEGIN
      MOVE 'Y' TO ERROR.
      MOVE 9923 TO ERR.NUM
    END
  END
  INCLUDE TEXT "SCREEN.DIS.LIB"

  DISP.MASK:
  DISPLAY SCREEN.DIS USING DISPLAY.
  BEGIN
    IF FUNCT.KEY EQ DEFAULT THEN
    MOVE SCREEN.NAME OF SCREEN.DIS.INQ TO SCREEN.NAME ECHO
    TYPE TO TERMINAL SET.BLOCK.MODE,SET.PROTECT.MODE.ON
    MOVE DEFAULT TO FUNCT.KEY

    FUNCT.KEY.LOOP:
    BEGIN
      IF FUNCT.KEY EQ FUNCT.T THEN
      GET.FLD:
      BEGIN
        APPLY PROCESS.SCREEN.DIS
        IF EDIT.CANCELED NE 'N' THEN
        FINISH GET.FLD
        SELECT SCREEN.FLDS VIA KEY THEN
        BEGIN
          IF X.COR EQ CX AND Y.COR EQ CY THEN
          MOVE 'Y' TO CURSOR
          ELSE
          MOVE 'N' TO CURSOR
          SELECT SCREEN.KEYS VIA KEY THEN
          BEGIN
            MOVE SINK.FLD TO SINK.FLD OF SCREEN.DIS
            MOVE SINK.QFR TO SINK.QFR OF SCREEN.DIS
          END
          ELSE
          IF FIELD.TYPE EQ 'K' THEN
          MOVE 'NOT FOUND' TO SINK.FLD,SINK.QFR
          ELSE
          MOVE DEFAULT TO SINK.FLD,SINK.QFR
          SELECT PRE.PROCESS VIA KEY THEN
          BEGIN
            MOVE PRE.PROCESS.STMT1 TO PRE.PROCESS.STMT1 OF SCREEN.DIS
            MOVE PRE.PROCESS.STMT2 TO PRE.PROCESS.STMT2 OF SCREEN.DIS
          END
          ELSE
          MOVE DEFAULT TO PRE.PROCESS.STMT1,PRE.PROCESS.STMT2
          SELECT LOOKUP VIA KEY THEN
          BEGIN
            MOVE PRE.SELECT.STMT TO PRE.SELECT.STMT OF SCREEN.DIS
            MOVE RELATION.NAME TO RELATION.NAME OF SCREEN.DIS
            MOVE VIA.CLAUSE TO VIA.CLAUSE OF SCREEN.DIS
            MOVE THEN.ERR.NUM TO THEN.ERR.NUM OF SCREEN.DIS
            MOVE THEN.STMT TO THEN.STMT OF SCREEN.DIS
            MOVE ELSE.ERR.NUM TO ELSE.ERR.NUM OF SCREEN.DIS
            MOVE ELSE.STMT TO ELSE.STMT OF SCREEN.DIS
          END
          ELSE
          MOVE DEFAULT TO PRE.SELECT.STMT,RELATION.NAME,VIA.CLAUSE,
          THEN.ERR.NUM,THEN.STMT,ELSE.ERR.NUM,ELSE.STMT
          SELECT POST.PROCESS VIA KEY THEN
          MOVE POST.PROCESS.STMT TO POST.PROCESS.STMT OF SCREEN.DIS
          ELSE
          MOVE DEFAULT TO POST.PROCESS.STMT

          APPLY EXPAND.SCREEN.DIS
        END
      END
      %  GET.FLD  %

      ELSE IF FUNCT.KEY EQ FUNCT.Q THEN
      FINISH DISP.MASK

      ELSE IF FUNCT.KEY EQ FUNCT.D THEN
      REPEAT DISP.MASK

      ELSE IF FUNCT.KEY NE DEFAULT THEN
      TYPE TO TERMINAL BELL

      INPUT FUNCTION.KEY
      REPEAT FUNCT.KEY.LOOP
    END
    %  FUNCT.KEY.LOOP  %
  END
  %  DISP.MASK  %
END
