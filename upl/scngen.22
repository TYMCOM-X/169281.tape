PROCEDURE SCREEN.DOC.RPT (SCREEN.NAME,PRINT.SW AS 'C',FILE.IN AS '10C',
PN. AS '5Z')
RETURNING (PN.)
BEGIN


  FIELDS
  TAB. AS '2N',
  TIME. AS '8C',
  HRS. AS 'ZN',
  MIN. AS 'NN',
  TMP. AS '5N',
  CURSOR AS 'C',
  TYPE.FIELD AS '15C',
  LINE1 SAME AS LINE.,
  END.SCREEN.NAME SAME AS SCREEN.NAME,
  INQUIRY.SW AS 'N',
  PG.SW AS 'N',
  FILE.NAME AS '14C'

  CONTROLS FOR RELATIONS
  CONCURRENT UPDATE

  REPORT APPEND TO FILE.NAME
  PAGE.NUM PN.
  PAGE.TOP @127
  BOTTOM.MARGIN 3
  HEADING.SIZE 3
  PAGE.HEADING
  BEGIN
    PRINT @FF,@CR
    IF PG.SW EQ 0 THEN
    PRINT TODAY AS DATE 'ZM/DD/YY',' ',TIME.
    PRINT @TAB TO TAB.,'SCREEN NAME: ',SCREEN.NAME
    IF PG.SW EQ 0 THEN
    PRINT @TAB TO 65,'PAGE ',PAGE.NUM,@CR
    ELSE
    PRINT @CR
    LET PN. = PAGE.NUM + 1
  END

  INCLUDE TEXT "CRT.SETUP"

  IF PN. EQ 0 THEN
  LET PN. = 1

  IF SCREEN.NAME EQ DEFAULT THEN
  MOVE 15*'Z' TO END.SCREEN.NAME
  ELSE IF SCREEN.NAME EQ 'ALL' THEN
  BEGIN
    MOVE ' ' TO SCREEN.NAME
    MOVE 15 * 'Z' TO END.SCREEN.NAME
  END
  ELSE IF POSITION(SCREEN.NAME,',') NE 0 THEN
  BEGIN
    LET END.SCREEN.NAME = SUBSTR(SCREEN.NAME,POSITION(SCREEN.NAME,',')+1)
    LET END.SCREEN.NAME = TRIM(END.SCREEN.NAME) + 15 * 'Z'
    LET SCREEN.NAME = SUBSTR(SCREEN.NAME,1,POSITION(SCREEN.NAME,',')-1)
  END
  ELSE MOVE SCREEN.NAME TO END.SCREEN.NAME

  IF FILE.IN EQ ' ' THEN
  BEGIN
    LET FILE.IN = 'SCREEN.DMP'
    LET FILE.NAME = 'SCREEN.DMP-ANY'
  END
  ELSE
  IF UPPER.CASE(FILE.IN) EQ 'TER' OR 'TERM' OR 'TERMI' OR 'TERMIN' OR
  'TERMINA' OR 'TERMINAL' THEN
  BEGIN
    LET FILE.NAME = 'SCREEN.DMP-ANY'
    LET FILE.IN = 'SCREEN.DMP'
  END
  ELSE
  LET FILE.NAME = TRIM(FILE.IN) + '-ANY'

  IF FILE.IN EQ 'SCREEN.DMP' THEN
  LET PG.SW = 1

  MOVE DEFAULT TO Y.COR

  PROCESS.REPORT:
  FOR EACH SCREENS VIA SCREEN.NAME GE SCREEN.NAME OF SCREEN.DOC.RPT
  WRITE REPORT
  BEGIN
    IF SCREEN.NAME GT END.SCREEN.NAME THEN FINISH PROCESS.REPORT
    LET TAB. = 28 + (15 - LENGTH(SCREEN.NAME)) / 2
    IF PG.SW EQ 0 THEN
    BEGIN
      LET TMP. = TIME
      LET HRS. = TMP. / 3600
      LET MIN. = (TMP. MOD 3600) / 60
      IF HRS. GT 11 THEN
      BEGIN
        IF HRS. NE 12 THEN
        SUBTRACT 12 FROM HRS.
        LET TIME. = HRS. AS '2C' + ':' + MIN. AS '2C' + ' PM'
      END
      ELSE
      BEGIN
        IF HRS. EQ 0 THEN
        LET HRS. = 12
        LET TIME. = HRS. AS '2C' + ':' + MIN. AS '2C' + ' AM'
      END
    END
    IF PRINT.SW EQ 'B' OR 'M' THEN
    BEGIN
      LET INQUIRY.SW = 0
      PRINT PAGE.TOP,@SKIP 8
      IF SCREEN.TYPE EQ 'I/O' THEN
      PRINT @TAB TO 32,'TYPE: INPUT/OUTPUT',@CR
      ELSE
      IF SCREEN.TYPE EQ 'FRM' THEN
      PRINT @TAB TO 29,'TYPE: FRAME INPUT/OUTPUT',@CR,
      @TAB TO 27,'FRAME AREA: LINES ',STARTING.LINE.NO.,' THRU ',
      ENDING.LINE.NO.,@CR
      ELSE
      BEGIN
        LET INQUIRY.SW = 1
        IF SPACE.X NE DEFAULT THEN
        IF SCREEN.TYPE EQ 'INQ' THEN
        LET SCREEN.TYPE = 'SI'
        ELSE
        IF SCREEN.TYPE EQ 'FI' THEN
        LET SCREEN.TYPE = 'FSI'
        CONDITIONAL ON SCREEN.TYPE
        BEGIN
          'INQ': PRINT @TAB TO 34,'TYPE: INQUIRY',@CR
          'SI': PRINT @TAB TO 29,'TYPE: SELECTABLE INQUIRY',@CR
          'FI': PRINT @TAB TO 31,'TYPE: FRAME INQUIRY',@CR
          'FSI': PRINT @TAB TO 26,'TYPE: FRAME SELECTABLE INQUIRY',@CR
          'MSI': PRINT @TAB TO 26,'TYPE: MULTI-SELECTABLE INQUIRY',@CR
          'FMI': PRINT @TAB TO 23,'TYPE: FRAME MULTI-SELECTABLE INQUIRY',@CR
        END
        IF SPACE.X NE DEFAULT THEN
        PRINT @TAB TO 31,'SELECTION COLUMN: ',POSITION(X.Y.POS,SPACE.X)
        AS 'NN',@CR
        PRINT @TAB TO 26,'DISPLAY AREA: LINES ',STARTING.LINE.NO.,' THRU ',
        ENDING.LINE.NO.,@CR,@TAB TO 33,'SPACING: '
        IF SPACING. EQ 1 THEN
        PRINT 'SINGLE',@CR
        ELSE IF SPACING. EQ 2 THEN
        PRINT 'DOUBLE',@CR
        ELSE IF SPACING. EQ 3 THEN
        PRINT 'TRIPLE',@CR
        ELSE PRINT 'UNKNOWN',@CR
      END
      PRINT @TAB TO 31,'CREATED ON: ',CRE.DATE,@CR,
      @TAB TO 28,'LAST MODIFIED ON: ',MOD.DATE,@CR,
      @TAB TO 25,'LAST CODE GENERATION ON: ',CODE.DATE,@CR
      CONDITIONAL ON STATUS.
      BEGIN
        'CATTR': PRINT @TAB TO 28,'STATUS: ATTRIBUTES CREATED'
        'CCODE': PRINT @TAB TO 27,'STATUS: CODE GENERATION DONE'
        'CMASK': PRINT @TAB TO 31,'STATUS: MASK CREATED'
        'MSCRN': PRINT @TAB TO 30,'STATUS: SCREEN MODIFIED'
      END
      PRINT @SKIP 5

      LET FIELD.NO = 0
      LET LINE.NO. = 1
      DO
      BEGIN
        SELECT SCREEN.MASK VIA KEY THEN
        LET LINE1 = LINE.
        ELSE LET LINE1 = DEFAULT
        MOVE SUBSTR(X.Y.POS,LINE.NO.,1) TO Y.COR OF SCREEN.DOC.RPT

        GET.FIELDS:
        FOR EACH SCREEN.FLDS VIA (SCREEN.NAME,FLD.NO GT FIELD.NO)
        IF Y.COR = Y.COR OF SCREEN.DOC.RPT THEN
        BEGIN
          LET FIELD.NO = FLD.NO
          IF FLD.LENGTH EQ 1 AND (FIELD.TYPE NE 'E' AND 'I') THEN
          MOVE '@' TO SUBSTR(LINE1,POSITION(X.Y.POS,X.COR),1)
          ELSE
          BEGIN
            IF FIELD.TYPE EQ 'E' AND REQUIRED. EQ 'Y' THEN
            BEGIN
              MOVE '[' TO LEFT.D
              MOVE ']' TO RIGHT.D
            END
            ELSE
            IF FIELD.TYPE EQ 'E' OR 'I' THEN
            BEGIN
              MOVE '(' TO LEFT.D
              MOVE ')' TO RIGHT.D
            END
            ELSE
            BEGIN
              MOVE '<' TO LEFT.D
              MOVE '>' TO RIGHT.D
            END
            MOVE LEFT.D TO SUBSTR(LINE1,POSITION(X.Y.POS,X.COR)-1,1)
            MOVE RIGHT.D TO SUBSTR(LINE1,POSITION(X.Y.POS,X.COR)+FLD.LENGTH,1)
          END
        END
        ELSE
        FINISH GET.FIELDS

        IF INQUIRY.SW EQ 1 AND LINE.NO. EQ STARTING.LINE.NO. THEN
        BEGIN
          IF SPACE.X NE DEFAULT THEN
          LET SUBSTR(LINE1,POSITION(X.Y.POS,SPACE.X),1) = '^'
        END

        IF LINE.NO. NE 23 THEN
        IF LINE.NO. EQ ENDING.LINE.NO. THEN
        IF SCREEN.TYPE EQ 'FI' OR 'FSI' OR 'FMI' THEN
        IF LINE1 EQ DEFAULT THEN
        LET SUBSTR(LINE1,80,1) = '|'

        PRINT TRIM(LINE1), @CR

        ADD 1 TO LINE.NO.
      END
      WHILE LINE.NO. LE 23
      PRINT @TAB TO 35,'MESSAGE AREA',@CR
    END

    IF PRINT.SW NE 'M' THEN
    PRINT PAGE.TOP

    IF PRINT.SW EQ 'A' OR 'B' THEN
    FOR EACH SCREEN.FLDS VIA SCREEN.NAME
    BEGIN

      CONDITIONAL ON FIELD.TYPE
      BEGIN
        'C': LET TYPE.FIELD = 'CHANGING OUTPUT'
        'D': LET TYPE.FIELD = 'INQUIRY DATA'
        'E': LET TYPE.FIELD = 'EDIT'
        'I': LET TYPE.FIELD = 'INPUT'
        'K': LET TYPE.FIELD = 'INQUIRY KEY'
        'O': LET TYPE.FIELD = 'OUTPUT'
      END
      ELSE LET TYPE.FIELD = 'UNKNOWN'
      IF X.COR OF SCREENS EQ X.COR AND Y.COR OF SCREENS EQ Y.COR THEN
      LET CURSOR = 'Y'
      ELSE LET CURSOR = 'N'
      PRINT 'FLD.NO: ', FLD.NO, '  Y.COR: ', POSITION(X.Y.POS,Y.COR) AS '2N',
      '  X.COR: ', POSITION(X.Y.POS,X.COR) AS '2N', '  FLD.LENGTH: ',
      FLD.LENGTH, @CR, 'TYPE: ', TRIM(TYPE.FIELD),
      '     POSITION CURSOR TO THIS FIELD? ',CURSOR,@CR,
      'FIELD NAME: ',TRIM(ACTUAL.FLD)
      IF ACTUAL.QFR NE DEFAULT THEN
      PRINT ' OF ',TRIM(ACTUAL.QFR),@CR
      ELSE PRINT @CR

      IF FIELD.TYPE EQ 'K' THEN
      SELECT SCREEN.KEYS VIA KEY THEN
      BEGIN
        PRINT 'RETURN TO: ',TRIM(SINK.FLD)
        IF SINK.QFR NE DEFAULT THEN
        PRINT ' OF ',TRIM(SINK.QFR),@CR
        ELSE PRINT @CR
      END
      ELSE PRINT 'RETURN TO NAME MISSING',@CR
      IF DEFAULT.EXPRESSION NE DEFAULT THEN
      PRINT 'DEFAULT EXPRESSION: ', TRIM(DEFAULT.EXPRESSION), @CR
      IF REQUIRED. EQ 'Y' OR REQ.ERR.NUM NE DEFAULT THEN
      BEGIN
        PRINT 'REQUIRED: ',REQUIRED.,'  REQUIRED ERR NUMBER: ',REQ.ERR.NUM,@CR
        SELECT ERR.MSG VIA ERR.NUM=REQ.ERR.NUM THEN
        BEGIN
          PRINT 'ERROR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT 'NO REQUIRED ERROR MESSAGE EXISTS',@CR
      END
      IF HELP. EQ 'Y' THEN
      PRINT 'HELP: Y  '
      IF VERIFY.ERR.NUM NE DEFAULT THEN
      BEGIN
        PRINT 'VERIFY ERR NUMBER: ',VERIFY.ERR.NUM,@CR
        SELECT ERR.MSG VIA ERR.NUM=VERIFY.ERR.NUM THEN
        BEGIN
          PRINT 'ERROR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT 'NO VERIFY ERROR MESSAGE EXISTS',@CR
      END
      ELSE
      IF HELP. EQ 'Y' THEN
      PRINT @CR
      IF (MIN.LENGTH NE 0) OR (MIN.LEN.ERR.NUM NE 0) THEN
      BEGIN
        PRINT 'MINIMUM LENGTH: ', MIN.LENGTH,
        '  MINIMUM LENGTH ERR NUMBER: ', MIN.LEN.ERR.NUM, @CR
        SELECT ERR.MSG VIA ERR.NUM=MIN.LEN.ERR.NUM THEN
        BEGIN
          PRINT 'ERROR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT 'NO MINIMUM LENGTH ERROR MESSAGE EXISTS',@CR
      END
      IF IS.ERR.NUM NE 0 THEN
      BEGIN
        PRINT 'FORMAT CONVERSION ERR NUMBER: ', IS.ERR.NUM,@CR
        SELECT ERR.MSG VIA ERR.NUM=IS.ERR.NUM THEN
        BEGIN
          PRINT 'ERROR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT 'NO FORMAT CONVERSION ERROR MESSAGE EXISTS',@CR
      END

      SELECT PRE.PROCESS VIA KEY THEN
      BEGIN
        PRINT 'PRE PROCESS STMTS: '
        IF PRE.PROCESS.STMT1 NE DEFAULT THEN
        PRINT TRIM(PRE.PROCESS.STMT1),@CR
        IF PRE.PROCESS.STMT2 NE DEFAULT THEN
        PRINT @TAB TO 20,TRIM(PRE.PROCESS.STMT2),@CR
      END

      SELECT LOOKUP VIA KEY THEN
      BEGIN
        IF VIA.CLAUSE EQ DEFAULT THEN
        LET VIA.CLAUSE = 'KEY'
        PRINT 'PRE SELECT:    ', TRIM(PRE.SELECT.STMT),@CR,
        'SELECT:        ', TRIM(RELATION.NAME),' VIA'
        IF VIA.CLAUSE EQ 'KEY' THEN
        PRINT ' KEY',@CR
        ELSE IF LENGTH(VIA.CLAUSE) GT 30 THEN
        PRINT @CR,'(',TRIM(VIA.CLAUSE),')',@CR
        ELSE
        PRINT ' (',TRIM(VIA.CLAUSE),')',@CR
        PRINT ' THEN',@CR,
        '  ERR NUMBER:  ', THEN.ERR.NUM,@CR
        IF THEN.ERR.NUM NE DEFAULT THEN
        SELECT ERR.MSG VIA ERR.NUM=THEN.ERR.NUM THEN
        BEGIN
          PRINT '  ERR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT '  NO THEN ERROR MESSAGE EXISTS',@CR
        PRINT '  OR STMT:     ', TRIM(THEN.STMT),@CR,
        ' ELSE',@CR,
        '  ERR NUMBER:  ', ELSE.ERR.NUM,@CR
        IF ELSE.ERR.NUM NE DEFAULT THEN
        SELECT ERR.MSG VIA ERR.NUM=ELSE.ERR.NUM THEN
        BEGIN
          PRINT '  ERR MESSAGE: '
          IF LENGTH(ERROR.MESSAGE) GT 65 THEN
          PRINT @CR
          PRINT TRIM(ERROR.MESSAGE),@CR
        END
        ELSE PRINT '  NO ELSE ERROR MESSAGE EXISTS',@CR
        PRINT '  OR STMT:     ', TRIM(ELSE.STMT), @CR
      END

      SELECT POST.PROCESS VIA KEY
      THEN PRINT 'POST PROCESS STMT: ', TRIM(POST.PROCESS.STMT), @CR
      IF SCREEN.NAME NOT CHANGING THEN
      IF LINE.NUM GT 4 AND LT 63 THEN
      PRINT @SKIP 2
      ELSE
      IF LINE.NUM EQ 63 THEN
      PRINT @SKIP 1

    END

  END

  TYPE TO TERMINAL ERROR.LINE,'PRINTING COMPLETED - OUTPUT APPENDED TO FILE ',
  FILE.IN,@CR
  PAUSE 3
END
  