100 REM
110 REM
120 REM            STAR WARS GAME
130 REM
140 REM  ADAPTED BY MARK BOYLES  (VERSION #6.2 03MAR78)
150 REM  TYMSHARE INTERGALACTIC GAMES (UK) LTD
160 REM
170 REM
180 VAR = ZERO
190 STRING V4,V7,A6,C1,B3,R,N
200 FOR I=1 TO 5
210 PRINT
220 NEXT I
230 PRINT TAB(1),"DEATHSTAR ATTACK"
240 PRINT TAB(1),"********* ******"
250 PRINT
260 PRINT "A STAR WARS COMPUTER GAME"
270 PRINT
280 N1=.49
290 N2=.19
300 N3=.18
310 V4="HEADING "
320 V7="SHIP ATTITUDE (PITCH) "
330 A6="THROTTLE SETTING "
340 C1="CHANGE"
350 PRINT "DO YOU WANT INFORMATION ABOUT THE GAME  ":
360 INPUT R
370 IF R = "N"THEN 810
380 IF R # "Y"THEN 350
390 PRINT
400 PRINT "YOU ARE THE PILOT OF A REBEL X- WING FIGHTER, CARRYING"
410 PRINT "A PROTON TORPEDO WITH WHICH TO ATTACK THE GIANT ARTIFICIAL"
420 PRINT "PLANET, THE DEATHSTAR, THAT THE TYRANT, GRAND MOFF TARKIN"
430 PRINT "IS USING TO TERRORISE THE GALAXY. YOUR OBJECTIVE IS TO FLY"
440 PRINT "TO A TRENCH IN THE SURFACE OF THE STAR, FLY ALONG IT AND"
450 PRINT "LAUNCH THE TORPEDO THAT WILL DESTROY THE DEATHSTAR."
460 PRINT
470 PRINT "AT THE NEAR END OF THE TRENCH, WHICH IS 2000 METRES LONG"
480 PRINT "BY 35 METRES WIDE, IS YOUR FIRST TARGET POINT, A RECTANGLE"
490 PRINT "30 METRES WIDE AND 100 METRES LONG CENTERED ON THE CO-ORDINATES"
500 PRINT "X=0, Y=0.IN ORDER TO CALIBRATE YOUR SHIPS MISSILE LAUNCH"
510 PRINT "SYSTEMS, YOU MUST FINISH A MANOEUVRE WITHIN THIS AREA BEFORE"
520 PRINT "ENTERING THE TRENCH."
530 PRINT
540 PRINT "YOUR FIGHTER HAS A WING SPAN OF 5 METRES, THEREFORE"
550 PRINT "YOU MUST FLY WITHIN 15 METRES OF THE TRENCH CENTRE-LINE"
560 PRINT "WHEN YOU REACH THE END OF THE TRENCH, YOU ARE ON COURSE AND "
570 PRINT "TARGET, AND BELOW THE CRITICAL ALTITUDE, THE COMBAT COMPUTER"
580 PRINT "WILL RELEASE YOUR TORPEDO FOR YOU."
590 PRINT
600 PRINT "NOTE:- THE LONGER YOU STAY IN THE TRENCH, THE LOWER"
610 PRINT "THE CRITICAL ALTITUDE BECOMES. THIS STARTS AT 200"
620 PRINT "METRES, SO GET LOW FAST! TO ASSIST YOU, YOUR POSITION"
630 PRINT "ON THE X-Y GRID, HEADING, AIRSPEED, RANGE AND BEARING TO "
640 PRINT "TARGET (TO THE CENTRE OF THE CALIBRATION RECTANGLE) ARE GIVEN."
650 PRINT "AT THIS POINT YOU MUST BE BELOW 200 METRES, HEADING ON A COURSE"
660 PRINT "OF NOT MORE THAN + OR - 5 DEGREES."
670 PRINT
680 PRINT "YOU CONTROL YAW AND PITCH RATES AND THROTTLE SETTING."
690 PRINT "THROTTLE GREATER THAN .5 WILL ACCELLERATE YOUR SHIP."
700 PRINT "LESS THAN .5 , AND THE DEATH STAR DEFENCE SCREENS"
710 PRINT "WILL SLOW YOU DOWN."
720 PRINT
730 PRINT "YOU ARE FLYING ON A HEADING OF 0 DEGREES AT POSITION"
740 PRINT "X=200, Y=15000. YOUR VELOCITY IS 1000 AND YOUR ALITIUDE IS"
750 PRINT "10000 METRES."
760 PRINT
770 PRINT "***** WARNING: THE DEATH STAR WILL LAUNCH TIE FIGHTERS AGAINST"
780 PRINT "YOU. YOUR ONBOARD HIT PREDICTOR WILL WARN YOU OF LASER"
790 PRINT "STRIKES AGAINST YOUR SHIP."
800 PRINT FOR J9=1 TO 3
810 PRINT
820 PRINT "ARE YOU AN EXPERIENCED PILOT  ":
830 INPUT R
840 IF R = "N"THEN 950
850 IF R # "Y"THEN 820
860 N9=1
870 PRINT
880 PRINT "YOU HAVE BEEN SPOTTED AS A SKILLED PILOT. THE DEFENDING"
890 PRINT "TIE FIGHTERS WILL HUNT YOU OUT AND LAUNCH UNEXPECTED ATTACKS"
900 PRINT "AGAINST YOU. YOUR SHIP CAN SURVIVE ONLY FIVE SUCCESSFUL"
910 PRINT "HITS TO ITS HULL OR ENGINES. EVERY HIT WILL REDUCE MANOEUVERABILITY"
920 PRINT "AND FORWARD VELOCITY."
930 PRINT
940 PRINT
950 PRINT "PLEASE TYPE IN YOUR NAME ":
960 INPUT N
970 PRINT FOR J9=1 TO 5
980 PRINT "MAY THE FORCE BE WITH YOU."
990 PRINT CHAR(7): FOR J9=1 TO 5
1000 PRINT FOR J9=1 TO 3
1010 X=200
1020 Y=-15000
1030 K4=1
1040 S=1000
1050 K2=2
1060 H=10000
1070 PRINT
1080 PRINT "COURSE AND HEIGHT CHANGES + OR - 0 TO 50, + = STBD, OR CLIMB, ":
1090 PRINT "- = PORT, OR DIVE."
1100 PRINT "THROTTLE SETTING 0 TO 1.9, WHERE 1 = 100%"
1110 PRINT
1120 PRINT
1130 PRINT
1140 FOR I=1 TO 30
1150 PRINT
1160 PRINT "MANOUVER ":I
1170 PRINT V4:C1:"":
1180 INPUT D2
1190 D2 = -D2
1200 IF D2>=45 THEN D2=45
1210 IF D2<=-45 THEN D2=-45
1220 PRINT V7:
1230 INPUT D3
1240 IF D3>=60 THEN D3=60
1250 IF D3<=-60 THEN D3=-60
1260 PRINT A6:
1270 INPUT S2
1280 IF S2>=2 THEN 2190
1290 S3=S2-.5
1300 S=S+(1000*S3)
1310 IF S<=10 THEN S=10
1320 A2=A2+K2*D2
1330 A=A2
1340 GOSUB 2090
1350 A2=A
1360 D3=(D3*PI/180)
1370 H=H+(S*SIN(D3))
1380 IF H<=0 THEN 2210
1390 D3=ABS(D3)
1400 A5=(A2*PI/180)
1410 S1=S*COS(D3)
1420 X=X+S1*SIN(A5)
1430 Y=Y+S1*COS(A5)
1440 IF Y <=0 THEN Z2=0
1450 R9=((X*X)+(Y*Y))^.5
1460 IF Z2=1 THEN 1690
1470 IF Y >50 THEN Z2=1
1480 IF ABS(X) <=15 THEN IF ABS(Y)<= 50 THEN IF ABS(A2)<=5 THEN 2310
1490 IF X#0 THEN IF Y#0 THEN 1550
1500 IF X=0 THEN IF Y>0 THEN LET A1=180
1510 IF X=0 THEN IF Y<0 THEN LET A1=0
1520 IF Y=0 THEN IF X>0 THEN LET A1=270
1530 IF Y=0 THEN IF X<0 THEN LET A1=90
1540 GOTO 1610
1550 X2=ABS(X)
1560 Y2=ABS(Y)
1570 IF X>0 THEN IF Y>0 THEN LET A1=180+(ATN(X2/Y2)*180/PI)
1580 IF X >0 THEN IF Y<0 THEN LET A1=270+(ATN(Y2/X2)*(180/PI))
1590 IF X<0 THEN IF Y<0 THEN LET A1=(ATN(X2/Y2)*180/PI)
1600 IF X<0 THEN IF Y>0 THEN LET A1=90+(ATN(Y2/X2)*180/PI)
1610 A3=A1
1620 A2=A1
1630 A=A3
1640 GOSUB 2090
1650 A3=A
1660 IF Y>=0 THEN IF Z2=1 THEN 1710
1670 IF Y<0 THEN 1710
1680 IF X2<=15 THEN 1710
1690 PRINT "YOUVE OVERSHOT THE TARGET, GO ROUND AND TRY AGAIN."
1700 Z2=1
1710 IF A3 <0 THEN IF A3 > -180 THEN 1770
1720 IF A3 < -180 THEN 1800
1730 IF A3 >0 THEN IF A3 < 180 THEN 1830
1740 A4 = ABS(A3-360)
1750 B3 = "DEGREES PORT "
1760 GOTO 1860
1770 A4 = ABS(A3)
1780 B3="DEGREES PORT "
1790 GOTO 1860
1800 A4 = ABS(A3 + 360)
1810 B3="DEGREES STBD "
1820 GOTO 1860
1830 A4 = ABS(A3)
1840 B3 ="DEGREES STBD "
1850 GOTO 1860
1860 PRINT
1870 PRINT "VELOCITY       = ":S,"POSITION X   = ":X
1880 PRINT "POSITION Y     = ":Y,
1890 PRINT CHAR(7): FOR J9 = 1 TO 3
1900 A=A4
1910 GOSUB 2090
1920 A4=A
1930 IF A4 =0 THEN B3=""
1940 PRINT "HEADING      = ":A2
1950 PRINT "ALTITUDE       = ":INT(H),"TARGET RANGE = ":INT(R9)
1960 PRINT "TARGET BEARING = ":A4:" ":B3
1970 IF ABS(A3) <= .25 THEN PRINT "TARGET DEAD AHEAD"
1980 IF Z1=1 THEN 2480
1990 IF N9=1 THEN GOSUB 2860
2000 PRINT
2010 NEXT I
2020 PRINT CHAR(7): FOR J9=1 TO 3
2030 PRINT "YOUR SHIP IS LOW ON FUEL. COMBAT COMPUTER HAS BROKEN OFF"
2040 PRINT "ATTACK AND IS RETURNING YOU TO THE REBEL BASE."
2050 PRINT "ATTACK NOW TERMINATED."
2060 PRINT "**** BETTER LUCK NEXT TIME !!! ******"
2070 PRINT FOR J9=1 TO 3
2080 GOTO 2280
2090 REM ANGLE SUBROUTINE
2100 IF A>=360 THEN 2120
2110 GOTO 2140
2120 A=A-360
2130 GOTO 2100
2140 IF A <= -360 THEN 2160
2150 RETURN
2160 A=A+360
2170 GOTO 2140
2180 RETURN
2190 PRINT "YOU JUST BLEW UP YOUR ENGINES AND ARE DIVING OUT OF CONTROL!!"
2200 PRINT FOR J9=1 TO 2
2210 PRINT "**** KER.....POW !!!!!!!!!! ****"
2220 PRINT "YOU JUST RAN OUT OF HEIGHT AND PILED INTO THE DEATHSTAR!"
2230 PRINT "KAMI KAZI TACTICS WENT OUT WITH PISTON ENGINED FIGHTERS !"
2240 PRINT "TRY NOT TO BE SO ENTHUSUSIASTIC NEXT TIME, AND"
2250 PRINT "WATCH YOUR ALTITUDE."
2260 PRINT FOR J9=1 TO 2
2270 GOTO 3260
2280 PRINT "MAY THE FORCE BE WITH YOU. "
2290 PRINT FOR I=1 TO 5
2300 STOP
2310 PRINT "OVER TARGET !!"
2320 R1 = 2000-R9
2330 PRINT "DISTANCE TO TORPEDO RELEASE POINT = ":R1:"METRES "
2340 IF A2 = 0 THEN B3 = "DEGREES "
2350 IF A2 <0 THEN B3 ="DEGREES STBD "
2360 IF A2 > 0 THEN B3 = "DEGREES PORT "
2370 IF A4 <=5 THEN 2420
2380 PRINT "COMPUTER REPORTS FIGHTER OFF COURSE "
2390 IF H <= 200 THEN 2210
2400 PRINT "ABORT ATTACK AND MAKE ANOTHER RUN "
2410 GOTO 1860
2420 IF H <= 200 THEN 2450
2430  PRINT "TOO HIGH ! TOO HIGH ! ABORT ATTACK AND MAKE ANOTHER RUN"
2440 GOTO 1860
2450 PRINT "COMBAT COMPUTER REPORTS ON COURSE AND TRACK FOR TORPEDO RUN"
2460 Z1=1
2470 IF Y<=2000 THEN 1860
2480 REM VENT TRENCH LIMITS SUBROUTINE
2490 IF X1>15 THEN 2590
2500 H1=200*K4
2510 IF H>=H1 THEN 2630
2520 K4 = K4 - .1
2530 IF Y >= 2000 THEN 2570
2540 R1 = 2000 - R9
2550 PRINT "DISTANCE TO TORPEDO RELEASE POINT = ":R1:"METRES"
2560 GOTO 1990
2570 IF A4 <= 180 THEN IF A4 >= 178 THEN 2680
2580 GOTO 1990
2590 PRINT "YOUVE CLIPPED THE SIDE OF THE VENT TRENCH ! YOUR"
2600 PRINT "FIGHTER IS BREA K  I    N    G    UP !"
2610 PRINT CHAR(7) FOR J9 = 1 TO 2
2620 GOTO 2210
2630 PRINT "THE TIE FIGHTERS HAVE FLUSHED YOU OUT. ABORT ATTACK AND"
2640 PRINT "TRY ANOTHER RUN."
2650 Z1=0
2660 K4=1
2670 GOTO 1990
2680 PRINT "ON TARGET ! COMBAT COMPUTER HAS RELEASED THE TORPEDO."
2690 PRINT "5 SECONDS TO IMPACT !"
2700 PRINT "4 SECONDS TO IMPACT !"
2710 PRINT "3 SECONDS TO IMPACT !"
2720 PRINT "2 SECONDS TO IMPACT !"
2730 PRINT "1 SECOND TO IMPACT !"
2740 PRINT "KERBOOMMMMMMMMM":CHAR(7):"BAMOOMMMMMM"
2750 PRINT CHAR(7): FOR J9=1 TO 5
2760 PRINT "CONGRATULATIONS! YOU HAVE SUCCESSFULLY ANNIHILATED THE"
2770 PRINT "DEATHSTAR.":CHAR(7)
2780 PRINT "YOU HAVE SAVED THE ENTIRE UNIVERSE FROM THE EVIL CLUTCHES"
2790 PRINT "OF DARTH VADER AND HIS MINIONS. "
2800 PRINT "YOU WIN A WOOKIE, TWO WOMP RATS AND"
2810 PRINT "THE HAND OF PRINCESS LEIA ORGANA IN MARRIAGE !"
2820 PRINT FOR J9=1 TO 2
2830 PRINT "WHO SAYS SCI-FI ISNT FUN ANYMORE !!"
2840 PRINT
2850 GOTO 2280
2860 REM FIGHTER SUBROUTINE
2870 R9=RND(-1)
2880 IF R9 = .5 THEN 3060
2890 N1= N1-.02
2900 IF R9 <= N1 THEN RETURN
2910 N2=N2+.01
2920 N3=N3 - .01
2930 IF N2 > .45 THEN N2 = .45
2940 IF N3 < .55 THEN N3 = .55
2950 PRINT "TIE FIGHTER ATTACK":CHAR(7):CHAR(7)
2960 R9 = RND(-1)
2970 IF R9 >= N2 THEN 3010
2980 D9 = D9 + 1
2990 K2 = K2 - .1
3000 PRINT "LASER STRIKE ON SHIP ",R9
3010 IF R9 <= N3 THEN 3110
3020 M9=M9 + 1
3030 K2 = K2 - .1
3040 PRINT "LASER STRIKE ON ENGINES",R9
3050 GOTO 3110
3060 PRINT 
3070 PRINT "A LUCKY HIT.... TIE FIGHTER HAS GOT UNDER YOUR SHIELDS"
3080 PRINT "AND WRECKED YOUR SHIP WITH A SINGLE BURST"
3090 PRINT
3100 GOTO 3220
3110 REM DAMAGE SUBROUTINE
3120 IF K>0 THEN 3160
3130 PRINT
3140 PRINT "TOTAL LOSS OF MANOEUVERING...."
3150 GOTO 3220
3160 IF D9 <= 5 THEN 3190
3170 PRINT "YOUR FIGHTER HAS SUFFERED MAJOR DAMAGE..."
3180 GOTO 3220
3190 IF M9 <= 5 THEN RETURN
3200 PRINT "THAT LAST ATTACK TOOK OUT YOUR ENGINES..."
3210 PRINT "TOTAL POWER LOSS!"
3220 PRINT 
3230 PRINT "KERPLUMMMMMMMK !!!!!!"
3240 PRINT CHAR(7): FOR J9= 1 TO 9
3250 PRINT
3260 PRINT N:", ASTRONAUT. A MAN MORE DEAD THAN ALIVE."
3270 PRINT
3280 PRINT "GENTLEMEN, WE CAN REBUILD HIM, WE HAVE THE TECHNOLOGY."
3290 PRINT "WE HAVE THE CAPABILITY OF BUILDING THE WORLDS FIST"
3300 PRINT "BIONIC IDIOT !!"
3310 PRINT FOR J9 = 1 TO 4
3320 PRINT "COMIN SOON TO THIS COMPUTER ...."
3330 PRINT "THE BUILD YOUR OWN SIX MILLION DOLLAR MAN GAME"
3340 PRINT "!!!!! DONT MISS IT !!!!! "
3350 PRINT FOR J9 = 1 TO 3
3360 GOTO 2280
    