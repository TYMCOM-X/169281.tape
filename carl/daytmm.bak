DAMESS:	ASCIZ	/-JAN-/

STZONE:	ASCIZ	/Y/	;  4
	ASCIZ	/BST/	;  5 BERING STANDARD TIME
	ASCIZ	/HST/	;  6 HAWAII-ALASKA STANDARD TIME
	ASCIZ	/YST/	;  7 YUKON STANDARD TIME
	ASCIZ	/PST/	; 10 PACIFIC STANDARD TIME
	ASCIZ	/MST/	; 11 MOUNTAIN STANDARD TIME
	ASCIZ	/CST/	; 12 CENTRAL STANDARD TIME
	ASCIZ	/EST/	; 13 EASTERN STANDARD TIME
	ASCIZ	/AST/	; 14 ATLANTIC STANDARD TIME
	ASCIZ	/P/	; 15
	ASCIZ	/O/	; 16
	ASCIZ	/N/	; 17
	ASCIZ	/GMT/	; 20 GREENWICH MEAN TIME (Western European)
	ASCIZ	/CET/	; 21 CENTRAL EUROPEAN TIME
	ASCIZ	/EET/	; 22 EASTERN EUROPEAN TIME
	ASCIZ	/C/	; 23
	ASCIZ	/D/	; 24
	ASCIZ	/E/	; 25
	ASCIZ	/F/	; 26
	ASCIZ	/G/	; 27
	ASCIZ	/H/	; 30
	ASCIZ	/JST/	; 31 TOKYO
	ASCIZ	/K/	; 32 BRISBANE
	ASCIZ	/L/	; 33
	ASCIZ	/M/	; 34

DSZONE:	ASCIZ	/Y +/	; 44
	ASCIZ	/BDT/	; 45 BERING DAYLIGHT TIME
	ASCIZ	/HDT/	; 46 HAWAII-ALASKA DAYLIGHT TIME
	ASCIZ	/YDT/	; 47 YUKON DAYLIGHT TIME
	ASCIZ	/PDT/	; 50 PACIFIC DAYLIGHT TIME
	ASCIZ	/MDT/	; 51 MOUNTAIN DAYLIGHT TIME
	ASCIZ	/CDT/	; 52 CENTRAL DAYLIGHT TIME
	ASCIZ	/EDT/	; 53 EASTERN DAYLIGHT TIME
	ASCIZ	/ADT/	; 54 ATLANTIC DAYLIGHT TIME
	ASCIZ	/P +/	; 55
	ASCIZ	/O +/	; 56
	ASCIZ	/N +/	; 57
	ASCIZ	/WEST/	; 60 WESTERN EUROPEAN SUMMER TIME
	ASCIZ	/CEST/	; 61 CENTRAL EUROPEAN SUMMER TIME
	ASCIZ	/EEST/	; 62 EASTERN EUROPEAN SUMMER TIME
	ASCIZ	/C +/	; 63
	ASCIZ	/D +/	; 64
	ASCIZ	/E +/	; 65
	ASCIZ	/F +/	; 66
	ASCIZ	/G +/	; 67
	ASCIZ	/H +/	; 70
	ASCIZ	/JST+/	; 71 TOKYO + DAYLIGHT  (Not normally used)
	ASCIZ	/K +/	; 72 BRISBANE + DAYLIGHT
	ASCIZ	/L +/	; 73
	ASCIZ	/M +/	; 74

;DAYTIME command
DAYTIM::PUSHJ	P,GETGMT	; GET T1=TIME T2=DATE (GMT)
	MOVSI	P4,400020	; SET DEFAULT OF TYM-GMT TO DEC-CURRENT
	LDB	T4,PVYTIM	; GET OUR CURRENT TIME ZONE
	TRNN	T4,40		; DAYLIGHT SAVINGS EFFECTIVE FOR ME?
	 JRST	DAYTI0		; NO! DON'T BOTHER, ONLY CONVERT ONCE
	MOVE P4,[400020,,400000]; CONVERT FROM TYMSHARE GMT
	PUSHJ	P,DATCOM	;    TO TYMSHARE CURRENT USER
	  JFCL			;Date not set yet
	PUSH	P,T2		;SAVE DATE (TYM-FORMAT) AND
	PUSH	P,T1		;SAVE TIME (TYM-FORMAT) FOR DST CHECK
	MOVSI	P4,400000	; CONVERT FROM TYMSHARE CURRENT
DAYTI0:	PUSHJ	P,DATCOM	;    TO DEC CURRENT
	  TLZ	T2,400000	;Date not set yet
	PUSH	P,T1		;SAVE TIME
	MOVE	T1,T2
	IDIVI	T1,^D31
	EXCH	T1,T2		;YEAR AND MONTH IN T2
	PUSHJ	P,DECP1		;ADD 1 TO DAY(IN T1) AND PRINT DECIMAL
	MOVEI	T1,0
	DIVI	T1,^D12
	EXCH	T1,T2
	MOVE	T1,MONTAB(T1)	;MONTH
	DPB	T1,[POINT 21,DAMESS,27]
	MOVEI	T1,DAMESS
	PUSHJ	P,CONMES	;PRINT DAY
	MOVEI	T1,^D1964(T2)
	PUSHJ	P,PRTDEC	;PRINT YEAR
	PUSHJ	P,PRSPC
	POP	P,T1		;RESTORE DAYTIME
	TLZ	T1,400000	;Sign bit set if DATE ok but TIME is not
	PUSHJ	P,PRTIME	;PRINT TIME AS HH:MM:SS
	PUSHJ	P,PRSPC		;PRINT A SPACE
	LDB	T4,PVYTIM	;GET OUR CURRENT TIME ZONE
	TRNN	T4,40		;DAYLIGHT SAVINGS EFFECTIVE FOR ME?
	 JRST	DAYTI1		;NO! DON'T BOTHER WITH CHECKING. (STACK OK!)
	POP	P,T1		;YES! RESTORE TIME (STACK HAS 2 ITEMS ON IT)
	IDIV	T1,JFYHR	;DIVIDE TO GET HOURS
	POP	P,T2		;RESTORE DATE SINCE JAN 1 1964 (STACK OK)
	IMULI	T2,^D24		;MULTIPLY TO MAKE HOURS
	ADD	T1,T2		;ADD TO GET HOURS SINCE JAN 1 1964
	PUSHJ	P,DSTCHK	;DO WE FALL WITHIN DAYLIGHT SAVINGS?
	  TDZA	T3,T3		;YES--CLEAR FLAG
DAYTI1:	SETO	T3,		;NO---SET FLAG
	TRZ	T4,40		; T4 STILL OK, CLEAR DST IF WAS ON
	MOVEI	T1,STZONE-4(T4)	;GET ASCIZ STRING TO TYPE
	SKIPN	T3		; SKIP IF WE'RE OK
	 MOVEI	T1,DSZONE-4(T4)	;  ELSE USE DAYLIGHT STRING
	SKIPG	THSDAT##	;If date is not valid
	 MOVEI	T1,[ASCIZ / Date not set!/]
	PUSHJ	P,CONMES	; TYPE IT.
	SKIPN	KSYS##		;Is the KSYS timer set?
	 JRST	DAYTI2		;No
	MOVEI	T1,[ASCIZ / Timesharing ceases in /]
	PUSHJ	P,CONMES
	MOVE	T1,KSYS		;Minutes left
	PUSHJ	P,PRTDEC
	MOVEI	T1,[ASCIZ / minutes/]
	PUSHJ	P,CONMES
DAYTI2:	PJRST	CRLF		;AND ADD CRLF
