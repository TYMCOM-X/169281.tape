
STZONE:	"Y"	;  4
	"BST"	;  5 BERING STANDARD TIME
	"HST"	;  6 HAWAII-ALASKA STANDARD TIME
	"YST"	;  7 YUKON STANDARD TIME
	"PST"	; 10 PACIFIC STANDARD TIME
	"MST"	; 11 MOUNTAIN STANDARD TIME
	"CST"	; 12 CENTRAL STANDARD TIME
	"EST"	; 13 EASTERN STANDARD TIME
	"AST"	; 14 ATLANTIC STANDARD TIME
	"P"	; 15
	"O"	; 16
	"N"	; 17
	"GMT"	; 20 GREENWICH MEAN TIME (Western European)
	"CET"	; 21 CENTRAL EUROPEAN TIME
	"EET"	; 22 EASTERN EUROPEAN TIME
	"C"	; 23
	"D"	; 24
	"E"	; 25
	"F"	; 26
	"G"	; 27
	"H"	; 30
	"JST"	; 31 TOKYO
	"K"	; 32 BRISBANE
	"L"	; 33
	"M"	; 34

DSZONE:	"Y +"	; 44
	"BDT"	; 45 BERING DAYLIGHT TIME
	"HDT"	; 46 HAWAII-ALASKA DAYLIGHT TIME
	"YDT"	; 47 YUKON DAYLIGHT TIME
	"PDT"	; 50 PACIFIC DAYLIGHT TIME
	"MDT"	; 51 MOUNTAIN DAYLIGHT TIME
	"CDT"	; 52 CENTRAL DAYLIGHT TIME
	"EDT"	; 53 EASTERN DAYLIGHT TIME
	"ADT"	; 54 ATLANTIC DAYLIGHT TIME
	"P +"	; 55
	"O +"	; 56
	"N +"	; 57
	"WEST"	; 60 WESTERN EUROPEAN SUMMER TIME
	"CEST"	; 61 CENTRAL EUROPEAN SUMMER TIME
	"EEST"	; 62 EASTERN EUROPEAN SUMMER TIME
	"C +"	; 63
	"D +"	; 64
	"E +"	; 65
	"F +"	; 66
	"G +"	; 67
	"H +"	; 70
	"JST+"	; 71 TOKYO + DAYLIGHT  (Not normally used)
	"K +"	; 72 BRISBANE + DAYLIGHT
	"L +"	; 73
	"M +"	; 74


;DAYTIME command
DAYTIM::PUSHJ	P,GETGMT	; GET T1=TIME T2=DATE (GMT)
	MOVSI	P4,400020	; SET DEFAULT OF TYM-GMT TO DEC-CURRENT
	LDB	T4,PVYTIM	; GET OUR CURRENT TIME ZONE
	TRNN	T4,40		; DAYLIGHT SAVINGS EFFECTIVE FOR ME?
	 JRST	DAYTI0		; NO! DON'T BOTHER, ONLY CONVERT ONCE
	MOVE P4,[400020,,400000]; CONVERT FROM TYMSHARE GMT
	PUSHJ	P,DATCOM	;    TO TYMSHARE CURRENT USER
	  JFCL			;Date not set yet
	PUSH	P,T2		;SAVE DATE (TYM-FORMAT) AND
	PUSH	P,T1		;SAVE TIME (TYM-FORMAT) FOR DST CHECK
	MOVSI	P4,400000	; CONVERT FROM TYMSHARE CURRENT
DAYTI0:	PUSHJ	P,DATCOM	;    TO DEC CURRENT
	  TLZ	T2,400000	;Date not set yet
	PUSH	P,T1		;SAVE TIME
	MOVE	T1,T2
	IDIVI	T1,^D31
	EXCH	T1,T2		;YEAR AND MONTH IN T2
	PUSHJ	P,DECP1		;ADD 1 TO DAY(IN T1) AND PRINT DECIMAL
	MOVEI	T1,0
	DIVI	T1,^D12
	EXCH	T1,T2
	MOVE	T1,MONTAB(T1)	;MONTH
	DPB	T1,[POINT 21,DAMESS,27]
	MOVEI	T1,DAMESS
	PUSHJ	P,CONMES	;PRINT DAY
	MOVEI	T1,^D1964(T2)
	PUSHJ	P,PRTDEC	;PRINT YEAR
	PUSHJ	P,PRSPC
	POP	P,T1		;RESTORE DAYTIME
	TLZ	T1,400000	;Sign bit set if DATE ok but TIME is not
	PUSHJ	P,PRTIME	;PRINT TIME AS HH:MM:SS
	PUSHJ	P,PRSPC		;PRINT A SPACE
	LDB	T4,PVYTIM	;GET OUR CURRENT TIME ZONE
	TRNN	T4,40		;DAYLIGHT SAVINGS EFFECTIVE FOR ME?
	 JRST	DAYTI1		;NO! DON'T BOTHER WITH CHECKING. (STACK OK!)
	POP	P,T1		;YES! RESTORE TIME (STACK HAS 2 ITEMS ON IT)
	IDIV	T1,JFYHR	;DIVIDE TO GET HOURS
	POP	P,T2		;RESTORE DATE SINCE JAN 1 1964 (STACK OK)
	IMULI	T2,^D24		;MULTIPLY TO MAKE HOURS
	ADD	T1,T2		;ADD TO GET HOURS SINCE JAN 1 1964
	PUSHJ	P,DSTCHK	;DO WE FALL WITHIN DAYLIGHT SAVINGS?
	  TDZA	T3,T3		;YES--CLEAR FLAG
DAYTI1:	SETO	T3,		;NO---SET FLAG
	TRZ	T4,40		; T4 STILL OK, CLEAR DST IF WAS ON
	MOVEI	T1,STZONE-4(T4)	;GET ASCIZ STRING TO TYPE
	SKIPN	T3		; SKIP IF WE'RE OK
	 MOVEI	T1,DSZONE-4(T4)	;  ELSE USE DAYLIGHT STRING
	SKIPG	THSDAT##	;If date is not valid
	 MOVEI	T1,[ASCIZ " Date not set!"]
	PUSHJ	P,CONMES	; TYPE IT.
	SKIPN	KSYS##		;Is the KSYS timer set?
	 JRST	DAYTI2		;No
	MOVEI	T1,[ASCIZ " Timesharing ceases in "]
	PUSHJ	P,CONMES
	MOVE	T1,KSYS		;Minutes left
	PUSHJ	P,PRTDEC
	MOVEI	T1,[ASCIZ " minutes"]
	PUSHJ	P,CONMES
DAYTI2:	PJRST	CRLF		;AND ADD CRLF
