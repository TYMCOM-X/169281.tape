90 ! *** 9/23/88
91 ! THE DISCOUNT WAS MOVED FROM A LINE BY LINE ENTRY TO THE BOTTOM
92 ! OF THE SUMMARY TO PREVENT IRCS FROM USING THE BOTTOM LINE AND THEN
93 ! APPLYING A DISCOUNT WHICH RESULTS IN TWO DISCOUNTS
94 ! THIS IS LINES  1720 1985 920 2113 TO 2119
100!  2060!PRINT IN IMAGE "%%%% %%%%%%%%%%%%%%% %%.%%% %% %%.%%% %% %%.%%% %% %%.%%%" :  T.DNIC;A;X1;CUR1;T.HRS.C;TRA.DIM;X;CUR2,T.TRA.C
110 PRINT "PROGRAM VERSION 4.0 LAST UPDATED 14 SEP 88"  ! *** CHANGED 9/14/88
120 PRINT "THIS UPDATE USES NON FOREIGN SERVICES AS THEY ARE GENERATED BY THE TIB PROGRAM"
130 PRINT "NON FOREIGN SERVICES SHOULD NOT EXIST IN THE SDR.BAS  or SDR.VAR TABLES"
140 PRT.FLG=1 ! PRINT FLAG TO PRINT INPUT TABLE
150 real nbill$,bill$
160 DBUG=1
170 LN=1
180 ! PROGRAM FILE IRCSDR.TBA  17 DEC 87 
190!PROGRAM TO CORRECT IRCRPT
200 STRING A,B,BLANKS,MTH,C(50),SVC,BC,BCM(50),S1,S2,MTH,CUR1,CUR2,CUR(300),TRA.DIM
210 STRING F.NAME,IRC.STRING
220 DIM THRS(20),TSEG(20),N(20),N(300),KS.COST(300)
230 DIM V(50),DNIC(300),HR.COST(300),M(300),TSES(300),KS.COSTT(300),KC.COST(300)
240 INTEGER UDNIC(300)
250 DIM TKC(16)
260 INTEGER AX(10),AL(10)
270 ON ERROR GOTO 3050
280  S1="  %%%  %%%%  %%%%  %%%%%%%%%%%%%%%%%%%% %%%%%%%%%%%%  %%%%%%%%.%%%    %%%%%%%.%%%  %%%%%.%%%  %%%%%%%%%%.%%  %%%%%%%%%%.%%"
290 S2="                   %%%%%%%%%%%%%%%%%%%% %%%%%%%%%%%%  %%%%%%%%.%%% %%%%%%%%%%%.%% %%%%%%%.%%  %%%%%%%%%%.%%  %%%%%%%%%%.%%"
300!   ROUTINE TO SET UP LIMITS OF FIELDS
310 SDR=1.36
320 PRINT "CURRENT SDR EXCHANGE RATE OR CARRIAGE RETURN IF ":SDR:
330 GOLD=2.5374  !GOLD FRANCS PER DOLLAR
340 INPUT IN FORM "R":A
350 IF A="" THEN 360 ELSE SDR=VAL(A)
360!PRINT "CURRENT GOLD FRANC RATE OR CARRIAGE RETURN IF ":GOLD:
370!INPUT IN FORM "R":A
380!IF A#""  THEN GOLD=VAL(A)
390 read nfields
400 FOR I=1 TO nfields
410 READ AX(I),AL(I)
420 NEXT I
430 BLANKS=SPACE(40)
440 FOR I= 1 TO 14
450 READ A
460 BCM(I)= LEFT(A+BLANKS,20)! MAKE ALL THE SAME 20 CHARACTERS LONG
470 NEXT I
480 PRINT "MONTH"
490 INPUT I
500 MTH=STR(1000+I)+"3"
510 MTH=RIGHT(MTH,3)
520 F.NAME=   "IRCRPT."+MTH
530 PRINT F.NAME      +" NOW BEING READ"
540 ON ERROR GOTO 570
550 OPEN F.NAME ,INPUT,3
560 GOTO 590
570 PRINT "ERROR   THE FILE DOES NOT EXIST "
580 GOTO 3050
590 ON ERROR GOTO 3050
600 ON ENDFILE(3) GOTO 3020
610 A="IRCRP2."+MTH+"-A"
620 OPEN A,OUTPUT,4
630 PRINT "OUTPUT FILE IS : ":A
640 A="IRCEXC."+MTH+"-A"
650 print
660 PRINT "The file : ":A:" will be generated by this program and will contain any service"
670 PRINT "that was not found in the SDR.BAS table or the SDR.VAR table.  The value that"
680 PRINT "was invoiced by the TIB output will be used as the amount to be invoiced"
690 PRINT "note that this is correct for non foreign  services such as telex or local access."
700 PRINT
710 OPEN A,OUTPUT,7
715 !
716 ! get the values from the tables SDR.BAS and SDR.VAR
717!
720 GOSUB 2400
730 !
740 ! LOOP TO BEGIN LOOKING FOR PTT/IRC
750 INPUT FROM 3 IN FORM "R":A
760 IF INDEX(A,IRC.STRING )=0 THEN 790 ! CHECK FOR IRC/PTT
770 PRINT "FOUND ":IRC.STRING
780 GOTO 930
790 WRITE ON 4:A
795 if index(a,"PAGE    1")=0 THEN 810                      !*** CHANGED 9/14/88
796 PRINT " THE FOLLOWING WAS NOT FOUND IN THE NEXT ENTRY ON SDR.VAR "                  ! ***  ADDED 9/14/88
797 PRINT A                                                                             ! ***  ADDED 9/14/88
800 WRITE ON 7:A                                        ! ***  CHANGED 9/14/88
810 GOTO 750
820 !
830 ! PTT/IRC FOUND LOOK FOR DATA
840 !
850 INPUT FROM 3 IN FORM "R":A
860 NB.COST=0
870 IF SUBSTR(A,3 ,8)="TOTALS :"  THEN 1960
880 IF SUBSTR(A,63,1)="." THEN 960
890!
900 IF INDEX(A,"==============================")#0 THEN 910 ELSE 930
910! PLACE VALUE OF SDR AT THE TOP OF THE REPORT AND INBOUND DISCOUNT
920  A=SUBSTR(A,1,80)+ "   SDR VALUE = "+STR(SDR) ! *** CHANGED 9/23/88
930 WRITE ON 4:A
940 GOTO 850
950 !
960 ! PTT/IRC BILLS FOUND
970 IF INDEX(A,"SUBTOTAL")#0 THEN 2160
980!PRINT 
990!PRINT A
1000 FOR I=1 TO nfields
1010 C(I)=SUBSTR(A,AX(I),AL(I))
1020 B=C(I)
1030 A1=AX(I)
1040 A2=AL(I)
1050 GOSUB 2280
1060 C(I)=B
1070 V(I)=VAL(B)
1080 NEXT I
1090 SESSIONS=V(3)
1100 HOURS=V(4)
1110 VKC=V(5)
1120 KILOSEGMENTS =V(6)
1130 bill$=v(7)
1140 nbill$=v(8)
1150 SVC=SUBSTR(A,3,3)
1160 BC = SUBSTR (A,020,20)
1170!  CHECK THE BILLING CODES
1180 FOR BCX=1 TO 14
1190 IF BC=BCM(BCX) THEN 1250
1200 NEXT BCX
1210 PRINT "NON-EXISTANT BILLING CODE":BC
1220 GOTO 850
1230 !
1240 ! CHECK FOR FIXED COST
1250!
1260 VDNIC = V (1)
1270 VTDNC = V( 2)
1280 IF VTDNC >79 AND VTDNC <100 THEN 1290 ELSE 1320
1290 BCX=1
1300 VTDNC=3106
1310 GOTO 1350
1320 IF VDNIC >79 AND VDNIC <100 THEN 1330 ELSE 1350
1330 BCX=8
1340 VDNIC=3106
1350 BC=BCM(BCX)
1360 IF BCX>10 THEN 1370 ELSE 1420
1370 COST=SESSIONS*OFF.NET
1380 IF VDNIC#9 THEN      1390 ! *** CHANGED 9/7/88
1382 COST=0                    ! *** CHANGED 9/7/88
1384 BCX=14                    ! *** CHANGED 9/7/88
1386 BC=BCM(BCX)               ! *** CHANGED 9/7/88
1390 GOTO 1670
1400!  SEARCH THE TABLE OF DNICS
1410 !
1420!
1430 IF INDEX(BC,"OUT")#0 THEN  XDNIC=VTDNC  ELSE XDNIC=VDNIC
1440 FOR J=1 TO LAST
1450 IF DNIC(J)=XDNIC THEN 1640
1460 NEXT J
1515 IF XDNIC<1000 THEN 1570                            ! *** ADDED   9/14/88
1520 FOR J=1 TO LAST
1530! check the country code if not a  dnic.
1540 IF IP(DNIC(J)/10)=IP(XDNIC /10) THEN 1640
1550 NEXT J
1551 FOR K=1 TO LASTK   ! **** CHANGED THIS THROUGH NEXT 5 ON 9/16/88
1552 IF XDNIC=UDNIC(K) THEN 1560!
1553 NEXT K
1554 LASTK=LASTK+1
1555 UDNIC(LASTK)=XDNIC
1560!
1570 ! this entry was not found leave it as the tib produced it
1580!
1590 write on 7:a
1600 nb.cost=nbill$
1610 cost= bill$
1620 go to 1770
1630!
1640 COST=HR.COST(J)*HOURS+KS.COST( J)*KILOSEGMENTS+KC.COST(J)*VKC
1650 IF CUR(J)="S" THEN COST=COST*SDR
1660 IF CUR(J)="G" THEN COST=COST/GOLD
1670 IF INDEX(BC,"-CREDIT")=0 THEN 1700
1680 COST=-COST*OUT.FACTOR ! OUTBOUND WE PAY 3/4 OF THE ACCTG RATE
1690!
1700 ! TO 1720  DISCOUNT BY COUNTRY FOR INBOUND REMOVED  ***** SECTION REMOVED 9/23
1710!
1730!
1740 IF INDEX(BC,"-NB")=0 THEN 1770
1750 NB.COST=COST
1760 COST=0
1770 NBST=NBST+NB.COST
1780 ST=ST+COST
1790 M(BCX)=M(BCX)+COST
1800 TSEG(BCX)=TSEG(BCX)+KILOSEGMENTS
1810 TKC(BCX)=TKC(BCX)+VKC
1820 N(BCX)=N(BCX)+NB.COST
1830 TSES(BCX)=TSES(BCX)+SESSIONS
1840 THRS(BCX)=THRS(BCX)+HOURS
1850 WRITE ON 4 IN IMAGE S1:SVC,VDNIC,VTDNC,BC,SESSIONS,HOURS,VKC,KILOSEGMENTS,COST,NB.COST
1860!PRINT HR.COST(BCX);KS.COST(BCX );KC.COST(BCX )
1870!PRINT  IN IMAGE S1:SVC,VDNIC,VTDNC,BC,SESSIONS,HOURS,VKC,KILOSEGMENTS,COST,NB.COST
1880 HRS1=HRS1+HOURS
1890 SES1=SES1+SESSIONS
1900 KC1=KC1+VKC
1910 KS1=KS1+KILOSEGMENTS
1920 COST=0
1930 NB.COST=0
1940 GO TO 850
1950 !
1960! PRINT TOTALS
1970 !
1980 PRINT ON 4: "  TOTALS :"
1985 ! BYPASS OLD TOTALS !              *** ADDED 9/23/88
1990 FOR J = 1 TO 15
2000 INPUT FROM 3 IN FORM "R" :A
2010 IF INDEX(A,"  TOTALS:")#0 THEN 2030
2020 NEXT J
2030 TOT.DISCOUNT=(M(1)+M(4))*DIS.COUNT ! ADDED *** 9/23/88
2035 FOR J = 1 TO 14            !*** MOVED 9/23/88
2040 WRITE ON 4 IN IMAGE S2:BCM(J),TSES(J),THRS(J),TKC(J),TSEG(J),M(J),N(J)
2050 TTKC=TTKC+TKC( J) , TTHRS=TTHRS+THRS(J), TTSEG=TTSEG+TSEG(J) ,TTM=TTM+M(J), TTN=TTN+N(J)
2060 TTSES=TTSES+TSES(J)
2070 TKC( J) = 0, THRS(J) = 0, TSEG(J) = 0, M(J) =0, N(J) = 0
2080 TSES(J)=0
2090 NEXT J
2100 PRINT "TOTALS:",TTSES,TTHRS,TTKC, TTSEG,TTM,TTN
2110 WRITE ON 4 IN IMAGE S2:"TOTALS:",TTSES,TTHRS,TTKC, TTSEG,TTM,TTN 
2111 WRITE ON 4:
2112 ! IF THE TOTAL DISCOUNT =0 THEN DO NOT MENTION IT *** 9/23/88
2113 IF TOT.DISCOUNT=O THEN 2120 ! *** 9/23/88
2114 WRITE ON 4 :
2115 WRITE ON 4 :
2116 WRITE ON 4 : "              DISCOUNT FOR PROMPT PAYMENT IS ":DIS.COUNT*100:" PERCENT  APPLIED TO INBOUND TRAFFIC ONLY." ! *** ADDED 9/23/88
2117 WRITE ON 4 :  "              THIS IS A  DISCOUNT OF $":TOT.DISCOUNT : "  IF PAYMENT IS MADE WITHIN 45 DAYS" ! *** ADDED 9/23/88
2118 PRINT "            INBOUND DISCOUNT FOR THIS IRC IS: ":TOT.DISCOUNT ! *** ADDED 9/23/88
2119 TOT.DISCOUNT =0
2120 TTSES=0,TTHRS=0, TTKC=0, TTSEG=0,TTM=0,TTN=0
2130 GO SUB 2430 ! READ IN THE NEXT IRC.
2140 GOTO 750
2141!
2142!
2150! PRINT SUBTOTALS
2160 WRITE ON 4 IN IMAGE S2:"SUBTOTAL":SES1,HRS1,KC1,KS1,ST,NBST
2170!IF DBUG=1 THEN PRINT A
2180 KS1=0
2190 KC1=0
2200 HRS1=0
2210 SES1=0
2220 ST=0
2230 NBST=0
2240 GOTO 850
2250!
2260!   REMOVE COMMAS
2270!
2280!
2290 IF B=LEFT(  BLANKS,A2) THEN 2340
2300 X3=INDEX(B,",")
2310 IF X3=0 THEN 2350
2320 B=LEFT(B,X3-1)+SUBSTR(B,X3+1)
2330 GOTO 2300
2340 B="0"
2350 RETURN
2360 !
2370!
2380 ! READ IN THE  ACCOUNTING RATE FOR THIS  IRC
2390!
2400 OPEN    "SdR.VAR",INPUT,5
2410 ON ENDFILE(5) GOTO 2900
2420!
2430! AFTER THE FIRST CALL USE THIS ENTRY POINT
2440!
2450 J=0
2460 INPUT FROM 5 IN FORM "R":B ! COMMENT LINE
2470 GOSUB 3070
2480 INPUT FROM 5 IN FORM "R":IRC.STRING
2490 GOSUB 3070
2500 print        "LOOKING FOR   : ":IRC.STRING
2510 INPUT FROM 5:irc.nbr
2520 GOSUB 3070
2530 INPUT FROM 5:OFF.NET
2540 GOSUB 3070
2550 J=0
2560 INPUT FROM 5: OUT.FACTOR
2570 GOSUB 3070
2580 INPUT FROM 5:  in.FACTOR
2590 GOSUB 3070
2600 INPUT FROM 5: DIS.COUNT
2610 GOSUB 3070
2620 GOSUB 3090 ! GET THE BASE PRICES IN THE SYSTEM
2630 LAST=J
2634!
2635 !
2636! *** BEGINNING OF THE LOOP TO INPUT THE  EXCEPTIONS TO THE BASE PRICES
2637!
2640 INPUT FROM 5: T.DNIC
2645 J=0 ! ZERO THE INDEX SO THAT THE ENTIRE ARRAY IS SCANNED FOR EACH NEW ENTRY
2650 GOSUB 3070 ! SAVES THE LINE NUMBER ON THE INPUT FILE FOR DEBUGGING
2660 IF T.DNIC<0 THEN 2900
2670 INPUT FROM 5: A,X1,CUR1,T.HRS.C,TRA.DIM,X,CUR2,T.TRA.C
2680 IF PRT.FLG=0 THEN 2700
2690!PRINT IN IMAGE "%%%% %%%%%%%%%%%%%%% %%.%%% %% %%.%%% %% %%.%%% %% %%.%%%" :  T.DNIC;A;X1;CUR1;T.HRS.C;TRA.DIM;X;CUR2,T.TRA.C
2700 IF J=LAST   THEN 2720 ELSE J=J+1
2710 IF DNIC(J)=T.DNIC THEN 2780 ELSE  2700
2720 J=J+1
2730 LAST=J
2740 if j<300 then 2770
2750 print "Too many entries for this IRC/PTT please correct tables or call the programmer"
2760 pause
2770 DNIC(J)=T.DNIC
2780 HR.COST(J)=T.HRS.C*in.factor
2790 KS.COST(J)=0
2800 KC.COST(J)=0
2810 IF INDEX("KCKS", TRA.DIM  )=0   THEN PRINT "ERROR IN LINE ":LN:" NOT KILOSEGS OR KILOCHARACTERS"
2820 IF INDEX("GDS",CUR2)=00   THEN PRINT "ERROR IN LINE ":LN:" CURRENCY NOT DOLLARS, GOLDFRANCS  OR SDR"
2830 IF INDEX( TRA.DIM,"KC")#0   THEN 2860
2840 KS.COST(J)=T.TRA.C*in.factor
2850 GOTO 2870
2860 KC.COST(J)=T.TRA.C*in.factor
2870 CUR(J)=CUR2
2880!PRINT IN IMAGE  "%% %%%% %%%%%.%% %%%%.%% %%%%.%% %%%% %%%%%%%%%%%%%%%%%%%%%" : J;  DNIC(J); HR.COST(J);KC.COST(J);KS.COST(J); CUR(J);A
2890 GO TO 2640
2891 ! *** END OF THE LOOP THAT INPUTS EXCEPTIONS
2900! END OF THE LOOP
2910 print "price entries =":last
2920 J=0
2930 RETURN
2940!
2950! number of data fields in a line describing traffic and the location and length of the fields
2960!
2970 DATA 8
2980 DATA 8,4,14,4,41,12,54,13,69,13,82,11,94,14,108,14
2990  DATA "INBOUND","INBOUND-NB","INBOUND-CREDIT","INBOUND-TNIC","INBOUND-CREDIT-TNIC"
3000  DATA "OUTBOUND","OUTBOUND-NB"
3010  DATA "OUTBOUND-CREDIT","OUTBOUND-TNIC","OUTBOUND-CREDIT-TNIC","OFFNET","OFFNET-NB","INTL-INTL","INTL-INTL-NB"
3020 CLOSE (3)
3030 CLOSE (4)
3031 PRINT "THESE DNICS WERE NOT FOUND IN CURRENT TABLES AND SO ENTRIES CONTAINING THEM" ! *** ADDED 9/14/88
3032 PRINT "WERE USED UNCHANGED"    ! ****  ADDED    /14/88
3033 FOR I= 1 TO LASTK                                  ! *** ADDED   9/14/88
3034 PRINT UDNIC(I)                                      ! *** ADDED 9/14/88
3035 NEXT I
3040 STOP
3050 PRINT "ERROR IN STEP":ERRLN
3060 STOP
3070 LN=LN+1
3080 RETURN
3090!
3100! READ IN THE BASE RATES
3110!
3120 CLOSE(6)
3130 OPEN    "SDR.BAS" , INPUT,6
3140 ON ENDFILE(6) GOTO  3410
3150 J=0
3160 ! INPUT THE COMMENT LINE
3170 INPUT FROM 6 IN FORM "R":A
3180!
3190!
3200! INPUT THE DATA
3210 INPUT FROM 6:T.DNIC
3220 LN=LN+1
3230 IF T.DNIC<0 THEN 3410
3240 INPUT FROM 6: A,X1,CUR1,T.HRS.C,TRA.DIM,X,CUR2,T.TRA.C
3250 IF PRT.FLG=0 THEN 3270
3260!PRINT IN IMAGE "%%%% %%%%%%%%%%%%%%% %%.%%% %% %%.%%% %% %%.%%% %% %%.%%%" :  T.DNIC;A;X1;CUR1;T.HRS.C;TRA.DIM;X;CUR2,T.TRA.C
3270 J=J+1
3280 DNIC(J)=T.DNIC
3290 HR.COST(J)=T.HRS.C*in.factor
3300 KS.COST(J)=0
3310 KC.COST(J)=0
3320 IF INDEX("KCKS", TRA.DIM  )=0   THEN PRINT "ERROR IN LINE ":J+1:" NOT KILOSEGS OR KILOCHARACTERS FILE : RATES.SDR"
3330 IF INDEX("GDS",CUR2)=00   THEN PRINT "ERROR IN LINE ":J+1:" CURRENCY NOT DOLLARS, GOLDFRANCS  OR SDR FILE : RATES.SDR"
3340 IF INDEX( TRA.DIM,"KC")#0   THEN 3370
3350 KS.COST(J)=T.TRA.C*in.factor
3360 GOTO 3380
3370 KC.COST(J)=T.TRA.C*in.factor
3380 CUR(J)=CUR2
3390!PRINT IN IMAGE  "%% %%%% %%%%%.%% %%%%.%% %%%%.%% %%%% %%%%%%%%%%%%%%%%%%%%%" : J;  DNIC(J); HR.COST(J);KC.COST(J);KS.COST(J); CUR(J);A
3400 GO TO 3210
3410 CLOSE(6)
3420 RETURN
  