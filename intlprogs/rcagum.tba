45 Print "    RCA Guam Settlement and Billing"
50! 
51 print "The R3Tmm.GUM and the R2Tmm.GUM files contain the sessions that have already been"
52 print "billed under the domestic billing program"
100 ON ERROR GOTO 1270
110! CORRECTED 4 JAN 1984 TO MERGE SPANISH AND ENGLISH VERSIONS
120 !    R2R3 PRINTS 6 REPORTS: R2 AND R3 ADMIN, TERM AND BILLABLE
130 ! MODIFIED FOR FTC
140 INTEGER B$(15),I99,M9,M9X,Z9,SPAIN
150 SPAIN=0 ! SPAIN=0 USE ENGLISH  SPAIN =1 USE SPANISH
160 STRING M8,BCDE,HD1,HD2,HD3,HD4,HD5,THOST,S5,CONN.S
170  STRING A$,OLDUN,UN, F1, F2, F3, GT2,GT3, T$(6),PTT,Z$,XDTE
180 DIM LINE(6),HEAD(6)
190 DIM TOTCT(6),TOTKCHAR(6),TOTSEGS(6),TOTCOST(6),TOTSESS(6)
200 STRING W1,W$
210 DIM SUMCT(6),SUMKCHAR(6),SUMSEGS(6),SUMCOST(6),SUMSESS(6)
220! THE FOLLOWING ARE BILLING CODES
230!  BC   1,  2,  3,  4,  5,  6,  7,  8,  9,  10, 11, 12, 13, 14, 15
240 DATA   6,  6,  5,  6,  4,  5,  4,  4,  4,   4,  4,  5,  5,  5,  5
250 ! AFTER OCTOBER 1985 USE THESE CODES
260 DATA   6,  4,  4,  6,  4,  5,  4,  5,  5,  5,    6, 4,   6,  4,  4
270 ! BILLING CODE 6=BILLABLE, 5=TERMINATION, 4=NONBILLABLE
280 READ B$(I) FOR I=1 TO 15
290 OPEN "GATHIN"    ,RANDOM INPUT,7
300 ON ENDFILE(7) GOTO 460
310  INPUT FROM 7 IN FORM "R":A$
320  MTHDD=VAL(SUBSTR(A$,13,4))
330 M9X=VAL(SUBSTR(A$,15,2))
340 M9=M9X
350 PTT="GUM"
360 GOSUB 3280 ! SET UP HEADINGS
370 FOR I=1 TO 20
380  INPUT FROM 7 IN FORM "R":A$
390  YYMTH=VAL(SUBSTR(A$,13,4))
400 IF YYMTH1>=YYMTH THEN 430
410  M9X=M9
420 YYMTH1=YYMTH
430 NEXT I
440 IF YYMTH1<8510 THEN 460
450READ B$(I) FOR I=1 TO 15 ! READ A NEW SET OF NUMBERS IF THE DATE > 8509
460 LOCATE 1 ON 7
470 M8=RIGHT(STR(M9X+1000),2)
480 T$(1)="ADMINISTRATIVE TRAFFIC"
490 T$(2)="Traffic Billed In Domestic Program"
500 T$(3)="BILLABLE TRAFFIC"
510 T$(4)="ADMINISTRATIVE TRAFFIC"
520 T$(5)="Traffic Billed In Domestic Program"
530 T$(6)="BILLABLE TRAFFIC"
540 ON ENDFILE(7) GOTO 1180
550 OPEN "R2A"+M8+"." + PTT + "-A"   ,OUTPUT,1
560 PRINT T$(1)+"FILE R2A"+M8+"." + PTT 
570 OPEN "R2T"+M8+"." + PTT + "-A",OUTPUT,2
580 PRINT T$(2)+ "FILE R2T"+M8+"." + PTT 
590 OPEN "R2B"+M8+"." + PTT + "-A", OUTPUT, 3
600 PRINT T$(3)+ "FILE R2B"+M8+"." + PTT 
610 OPEN "R3A"+M8+"." + PTT + "-A",OUTPUT,4
620 PRINT T$(1)+" FILE R3A"+M8+"." + PTT 
630 OPEN "R3T"+M8+"." + PTT + "-A",OUTPUT,5
640 PRINT T$(2)+" FILE R3T"+M8+"." + PTT 
650 OPEN "R3B"+M8+"." + PTT + "-A",OUTPUT,6
660 PRINT T$(3)+" FILE R3B"+M8+"." + PTT 
670  F1="6% 1B 5% 1B 4% 1B 3% 1B 6% 1B 8% 1B 9% 1B 9% 1B 7%.3% 1B 14% /"
680  F2="8% ' SESSIONS           ' 1B 8% 1B 9% 1B 9% 1B 7%.3% /"
690 F3="18% 1B 4(8%)    7%.3% /"
700 GT2="                   -------- ------- ------- ------- ----------"
710 GT3="                             -------- --------- --------- ------------"
720 LINE(I)=66 FOR I=1 TO 6              !LINE COUNTER
730 OLDUN = "ZZZZZZZZZZ"        !OLD USER NAME
740 OLDUUN=0            !OLD USER NUMBER
750 FOR I=1 TO 6
760 SUMCT(I)=0
770 SUMKCHAR(I)=0
780!
790 SUMSEGS(I)=0
800 SUMCOST(I)=0
810 SUMSESS(I)=0
820 TOTSESS(I)=0
830 HEAD(I)=0           ! =1 IF HEADING HAS BEEN PRINTED FOR THIS CODE
840 NEXT I
850 !
860 ON ERROR GOTO 1260
870 !
880 !                   BEGIN MAIN LOOP
890 !
900 Z$="",Z9=0
910 FOR I=1 TO 99999
920 INPUT FROM 7 IN FORM "R":A$
930 Z$=A$
940 IF LENGTH(A$)=0 THEN 1160
950 I99=I99+1
960 IF I99#100 THEN 990
970 I99=0
980 PRINT ".":
990 GOSUB 1400          !TAKE APART THE INPUT LINE
1000 !                   CALCULATE CORRECT FILE CODE TO PUT OUTPUT ON
1010 FILECODE=6
1020 IF BCODE=0 THEN BCODE=2
1030 FILECODE=B$(BCODE)
1040 !
1050 IF UUN=OLDUUN THEN 1090                      !USER NUMBER MATCHES OLD ONE
1060 IF OLDUUN=0 THEN 1090
1070 !
1080 GOSUB 2460 FOR FCODE=4 TO 6          !PRINT TOTALS
1090 !
1100 FCODE=FILECODE
1110 GOSUB 2090         !PRINT A LINE
1120 GOSUB 1940          !CALC TOTALS
1130 OLDUN=UN
1140 OLDUUN=UUN
1150 !
1160 NEXT I             ! END OF MAIN LOOP
1170 !
1180 !
1190 !                  END OF FILE
1200 !
1210 IF Z9=0 THEN PRINT "NO DUPLICATE SESSIONS HAVE BEEN FOUND"
1220 IF Z9#0 THEN PRINT Z9,"DUPLICATE SESSIONS HAVE BEEN FOUND"
1230 GOSUB 2460  FOR FCODE=4 TO 6        !PRINT TOTALS
1240 GOSUB 2670 FOR FCODE=4 TO 6         !GRAND TOTALS
1241 QUIT
1250 END
1260 ! ERROR END
1270 PRINT "ERROR IN LINE ":ERRLN
1280 PAUSE
1290 END
1300 !
1310 !                  !ZERO OUT SUMMERS
1320 !
1330 TOTSESS(FCODE)=0
1340 TOTCT(FCODE)=0
1350 TOTKCHAR(FCODE)=0
1360!
1370 TOTSEGS(FCODE)=0
1380 TOTCOST(FCODE)=0
1390 RETURN
1400 !
1410 !                  SUBROUTINE TO TAKE APART THE INPUT LINE
1420 !
1430 IRC=VAL(SUBSTR(A$,1,2))
1440 UUN=VAL(SUBSTR(A$,7,6))            !USER NUMBER
1450 XDTE=SUBSTR(A$,152,16)
1460 IF LEFT(XDTE,1)#"0" THEN 1490 ! REMOVE LEADING ZEROS
1470 XDTE=SUBSTR(XDTE,2)
1480 GOTO 1460
1490 IF LEFT(XDTE,1)#" " THEN 1500 ELSE 1470 ! REMOVE SPACES
1500 DAT =VAL(SUBSTR(A$,13,6))
1510!
1520 CONN.S  =    SUBSTR(A$,92,9)
1530  CONNECT=VAL(CONN.S)
1540  CONN   =CONNECT
1550 IF CONNECT=0 THEN 920
1560 IF INDEX(CONN.S,".")#0 THEN CONNECT=CONNECT*60 ! IF . THEN VAL IS IN HARS
1570 CHIN=VAL(SUBSTR(A$,61,9))          !CHAR IN
1580 CHOUT=VAL(SUBSTR(A$,70,9))         !CHAR OUT
1590   KCHTOT=(CHIN+CHOUT)/1000         !
1600   KCHAR=INT(KCHTOT)                !ROUND KILOCHARACTERS UP
1610   KCHTOT=KCHTOT-KCHAR              !TO NEAREST 1000 CHARS
1620   IF KCHTOT#0 THEN KCHAR=KCHAR+1   !
1630!
1640!
1650 GOSUB 3020
1660 SEGS=ORSEGIN+ORSEGOUT
1670 IF SEGS=0 THEN SEGS=DESTSIN+DESTSOUT
1680 IF SEGS=0 THEN SEGS=(CHIN+CHOUT)/40
1690 IF SEGS=0 THEN SEGS=1
1700 OHOST=VAL( SUBSTR(A$,44,5))                    !ORIG HOST
1710 ONODE=VAL( SUBSTR(A$,3,4))                     !ORIG NODE
1720 NHOST=VAL(SUBSTR(A$,56,5))
1730 ST=VAL(SUBSTR(A$,19,7))            !START TIME  HH.HHHH
1740 SH=INT(ST)
1750 SM=(ST-SH)*60
1760 ST=SH*100+SM                       !START TIME  HHMM
1770 UN=SUBSTR(A$,106,5)+" "+   SUBSTR(A$,111,25)                    !USER NAME
1780     LOC1=INDEX(UN,";")                     !USER NAME (TAKE OFF CHAR )
1790     IF LOC1#0 THEN UN=SUBSTR(UN,1,LOC1-1)  !USER NAME (AFTER ;       )
1800 BCDE =SUBSTR(A$,243,2)
1810 IF BCDE="  " THEN BCDE="02"
1820 BCODE=VAL(BCDE)
1830 IF NHOST=3607 OR NHOST=6141 OR NHOST=395 OR NHOST=8131 OR NHOST=513 THEN 1920
1840 IF NHOST=3829 THEN 1920
1850 IF NHOST=1255 THEN 1880
1860 COST=KCHAR*.12+ CONNECT*11.45/60
1861  if b$(bcode)#4 then BCODE=6
1870 GO TO 1930
1880 COST=SEG*12/1000+ CONNECT*10.00/60
1890 GO TO 1930
1900 COST=KCHAR*.12+ CONNECT*11.45/60
1910 GO TO 1930
1920 COST=KCHAR*.24+ CONNECT*13.90/60
1930 RETURN
1940 !
1950 !                  CALCULATE TOTALS
1960 TOTCT(FCODE)=TOTCT(FCODE)+CONNECT
1970 TOTKCHAR(FCODE)=TOTKCHAR(FCODE)+KCHAR
1980!
1990 TOTSEGS(FCODE)=TOTSEGS(FCODE)+SEGS
2000 TOTCOST(FCODE)=TOTCOST(FCODE)+COST
2010 TOTSESS(FCODE)=TOTSESS(FCODE)+1
2020 SUMCT(FCODE)=SUMCT(FCODE)+CONNECT
2030 SUMKCHAR(FCODE)=SUMKCHAR(FCODE)+KCHAR
2040!
2050 SUMSEGS(FCODE)=SUMSEGS(FCODE)+SEGS
2060 SUMCOST(FCODE)=SUMCOST(FCODE)+COST
2070 SUMSESS(FCODE)=SUMSESS(FCODE)+1
2080 RETURN
2090 !
2100 !                  PRINT LINE SUBROUTINE
2110 !
2120 IF HEAD(FCODE)=0 THEN 2140
2130 IF LINE(FCODE)<=60 THEN 2150
2140 GOSUB 2240         !TOP OF PAGE
2150 LINE(FCODE)=LINE(FCODE)+1
2160 IF XDTE#"" THEN 2210
2170 IF XDTE="" THEN XDTE="310600"+SUBSTR(STR(NHOST),2)
2180 IF NHOST=395 THEN 920
2190 IF NHOST=1255 THEN  920
2200 IF NHOST=8131 THEN  920
2210!
2220 PRINT ON FCODE IN  FORM F1:DAT,OHOST,NHOST,IRC,ST,CONNECT,KCHAR,SEGS,COST,XDTE
2230 RETURN
2240 !
2250 !                  R3 TOP OF PAGE SUBROUTINE
2260 !
2270 PRINT ON FCODE:CHAR(12)             !FORM FEED
2280 PRINT ON FCODE
2290 PRINT ON FCODE:"DETAIL BY USER"
2300 PRINT ON FCODE:T$(FCODE)
2310 PRINT ON FCODE:DATE
2320 PRINT ON FCODE
2330 PRINT ON FCODE:HD1:UN
2340 PRINT ON FCODE
2350 !
2360 !                  R3 HEADINGS SUBROUTINE
2370 !
2380 PRINT ON FCODE:HD2
2390 PRINT ON FCODE:HD3
2400 PRINT ON FCODE:HD4
2410 PRINT ON FCODE: "====== ===== ==== === ====== ======== ========= ========= =========== ==========="
2420 PRINT ON FCODE
2430 LINE(FCODE)=10
2440 HEAD(FCODE)=1              !HEADING HAS BEEN PRINTED FOR THIS FCODE
2450 RETURN
2460 !
2470 !                  TOTALS SUBROUTINE
2480 !
2490 !          R3 REPORTS     (DETAIL BY USER)
2500 !
2510 IF TOTCT(FCODE)=0 THEN 2660
2520 PRINT ON FCODE:GT3
2530 PRINT ON FCODE IN FORM F2:TOTSESS(FCODE),TOTCT(FCODE),TOTKCHAR(FCODE),TOTSEGS(FCODE),TOTCOST(FCODE)
2540 LINE(FCODE)=LINE(FCODE)+2
2550 HEAD(FCODE)=0              !RESET HEADING TO NOT PRINTED FOR THIS CODE
2560 !
2570 !          R2 REPORTS     (SUMMARY BY USER)
2580 !
2590 R2CODE=FCODE-3
2600 IF HEAD(R2CODE)=0 THEN 2620
2610 IF LINE(R2CODE)<=60 THEN 2630
2620 GOSUB 2870         ! R2 TOP OF PAGE 
2630 PRINT ON R2CODE IN FORM F3:OLDUN,TOTSESS(FCODE),TOTCT(FCODE),TOTKCHAR(FCODE),TOTSEGS(FCODE), TOTCOST(FCODE)
2640 LINE(R2CODE)=LINE(R2CODE)+1
2650 GOSUB 1300                 !ZERO ALL SUMMERS
2660 RETURN
2670 !
2680 !                  GRAND TOTALS SUBROUTINE 
2690 !
2700 !          R3 GRAND TOTALS
2710 !
2720 UN="GRAND TOTALS"
2730 GOSUB 2240         !TOP OF FORM
2740 PRINT ON FCODE
2750 PRINT ON FCODE IN FORM F2:SUMSESS(FCODE),SUMCT(FCODE),SUMKCHAR(FCODE),SUMSEGS(FCODE),SUMCOST(FCODE)
2760 PRINT ON FCODE:CHAR(12)                !FORM FEED
2770 !
2780!           R2 GRAND TOTALS
2790 !
2800 R2CODE=FCODE-3
2810 PRINT ON R2CODE:GT2
2820 PRINT ON R2CODE IN FORM F3:'GRAND TOTALS',SUMSESS(FCODE),SUMCT(FCODE),SUMKCHAR(FCODE),SUMSEGS(FCODE), SUMCOST(FCODE)
2830 PRINT ON R2CODE: CHAR(12)
2840 PRINT
2850 PRINT " FILE ":T$(R2CODE): " SESSIONS= ":SUMSESS(FCODE)
2860 RETURN
2870 !
2880 !          R2 TOP OF PAGE SUBROUTINE
2890 !
2900 PRINT ON R2CODE:CHAR(12)
2910 PRINT ON R2CODE
2920 PRINT ON R2CODE:"SUMMARY BY USER"
2930 PRINT ON R2CODE:T$(R2CODE)
2940 PRINT ON R2CODE:DATE
2950 PRINT ON R2CODE:"                             TOTAL CONNECT                      "
2960 PRINT ON R2CODE:HD5
2970 PRINT ON R2CODE:"================== ======== ======= ======= ======= ============="
2980 PRINT ON R2CODE
2990 LINE(R2CODE)=10
3000 HEAD(R2CODE)=1
3010 RETURN
3020 W1="        "
3030 W$=SUBSTR(A$,178,8)
3040 IF W1=W$ THEN W$="0"
3050 ORPAKIN =VAL(W$)
3060 W$=    SUBSTR(A$,186,8)
3070 IF W1=W$ THEN W$="0"
3080 ORPAKOUT=VAL(W$)
3090 W$=SUBSTR(A$,194,8)
3100 IF W1=W$ THEN W$="0"
3110 ORSEGIN =VAL(W$)
3120 W$=SUBSTR(A$,202,8)
3130 IF W1=W$ THEN W$="0"
3140 ORSEGOUT=VAL(W$)
3150 W$=SUBSTR(A$,210,8)
3160 IF W1=W$ THEN W$="0"
3170 DESTPIN =VAL(W$)
3180 W$=SUBSTR(A$,218,8)
3190 IF W1=W$ THEN W$="0"
3200 DESTPOUT=VAL(W$)
3210 W$=SUBSTR(A$,226,8)
3220 IF W1=W$ THEN W$="0"
3230 DESTSIN =VAL(W$)
3240 W$=SUBSTR(A$,234,8)
3250 IF W1=W$ THEN W$="0"
3260 DESTSOUT=VAL(W$)
3270 RETURN
3280! SET HEADINGS TO SPANISH OR ENGLISH
3290 IF SPAIN=1 THEN 3360 
3300 HD1="USER NAME:"
3310 HD2=""
3320 HD3="        ORIG TERM      START  CONNECT                         SESSION      DTE"
3330 HD4="  DATE  HOST HOST IRC   TIME  MINUTES    K-CHAR  SEGMENTS        COST    CALLED"
3340 HD5="USER NAME          SESSIONS MINUTES  K-CHAR SEGMENT      COST"
3350 GOTO 3420
3360! USE SPANISH HEADINGS
3370 HD1="NOMBRE DE USARIO: "
3380 HD2="        HOST NODO             MINUTOS                         COSTO"
3390 HD3="         DE   DE                 DE                            DE       TERNINAL"
3400 HD4="  FECHA ORIG ORIG      HORA  CONEXION    K-CARAC SEGMENTOS    SESION     LLAMADO"
3410 HD5="NOMBRE DE USARIO SESSIONS ESMINUTOS  K-CARAC SEGMONTOS   COSTO"
3420!
3430!
3440 S5=PTT+".HDR"
3450 ON ERROR GO TO 3560
3460!
3470! BEGIN PROCESSING THE FILE OF CHARGES
3480!
3490 OPEN S5,INPUT,3
3500 ON ENDFILE (3) GOTO 3560
3510  INPUT FROM 3 IN FORM "R":HD1
3520  INPUT FROM 3 IN FORM "R":HD2
3530  INPUT FROM 3 IN FORM "R":HD3
3540  INPUT FROM 3 IN FORM "R":HD4
3550  INPUT FROM 3 IN FORM "R":HD5
3560 CLOSE(3)
3570 RETURN
     