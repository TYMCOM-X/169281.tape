:	QUICK TEST FOR SUPER CLOCKS
:
:	THIS PROGRAM CONTINUOUSLY READS THE SUPER CLOCK DEVICE AND DISPLAYS
:	THE RESULTS ON THE CONSOLE.  IN ADDTION THE PROGRAM VERIFIES THAT
:	THE SUPER CLOCK VALUE INCREMENTS.
:
:	*** THERE IS NO VERIFICATION DONE ON THE ACCURACY OF THE SUPER CLOCK.
:
:	THE SWITCH REGISTER VALUE DETERMINES THE DISPLAY TYPE.  THE USER
:	SHOULD SELECT A SETTING BECAUSE THE PROGRAM CAN NOT RESET THIS
:	REGISTER.  TO SELECT A SETTING PUSH THE "DTA" BUTTON, THE DESIRED
:	OPTION, AND THEN "RUN".
:
:		0 - 32 BIT HEX VALUE
:		1 - YEAR AND JULIAN DAY  ( 0 YYYY 0DDD )
:		2 - HOUR, MUNITE, SECOND ( 0 00HH MMSS )
:
:		A - SET ABSOLUTE VAULE   (REQUIRES RESISTER JUMPER BETWEEN
:					  "RJA" AND "RJB" TO BE INSTALLED.
:					  USE A 33K OHM RESISTER.)
:
:		B - SET CLOCK BACK 0-2046 SECONDS
:		F - SET CLOCK FORWARD 0-2047 SECONDS
:
:
:	WHEN IN ONE OF THE ABOVE SET MODES, DISPLAYS WILL PROMPT THE USER
:	FOR INPUT.  WHEN ONE OF THE FOLLOWING DISPLAYS OCCURS, PUSH THE
:	"DTA" BUTTON, THE REQUESTED VALUE, THE "DTA" BUTTON, AND THEN
:	PRESS "RUN".
:
:		(0 000A 1111) - YEAR IN DECIMAL
:		(0 0000 2222) - JULIAN DAY IN DECIMAL
:		(0 0000 3333) - HOUR IN DECIMAL
:		(0 0000 4444) - MINUTE IN DECIMAL
:		(0 0000 5555) - SECOND IN DECIMAL
:
:		(0 000B BBBB) - COUNT OF SECONDS TO BACK UP CLOCK (0-2046) IN DECIMAL
:		(0 000F FFFF) - COUNT OF SECONDS TO ADVANCE CLOCK (0-2047) IN DECIMAL
:
:	IF A HALT OCCURS THE ERROR CODE CAN BE FOUND IN REGISTER 15.
:
R0	EQ	0
R1	EQ	1
R2	EQ	2
R3	EQ	3
R4	EQ	4
R5	EQ	5
R6	EQ	6
R7	EQ	7
R8	EQ	8
R9	EQ	9
R10	EQ	0A
R11	EQ	0B
R12	EQ	0C
R13	EQ	0D
R14	EQ	0E
R15	EQ	0F

	ORG	0FE00
DSTART	EQ	.
SPSW	WC	0000,10006
FHALT	WC	800F,0
TIMTBL	WC	00000000	:1974
	WC	01E13380	:1975
	WC	03C26700	:1976
	WC	05A4EC00	:1977
	WC	07861F80	:1978
	WC	09675300	:1979
	WC	0B488680	:1980
	WC	0D2B0B80	:1981
	WC	0F0C3F00	:1982
	WC	10ED7280	:1983
	WC	12CEA600	:1984
	WC	14B12B00	:1985
	WC	16925E80	:1986
	WC	18739200	:1987
	WC	1A54C580	:1988
	WC	1C374A80	:1989
	WC	1E187E00	:1990
	WC	1FF9B180	:1991
	WC	21DAE500	:1992
	WC	23BD6A00	:1993
	WC	259E9D80	:1994
	WC	277FD100	:1995
	WC	29610480	:1996
	WC	2B438980	:1997
	WC	2D24BD00	:1998
	WC	2F05F080	:1999
	WC	30E72400	:2000
	WC	32C85780	:2001
	WC	34A98B00	:2002
	WC	368ABE80	:2003
	WC	386BF200	:2004
	WC	3A4E7700	:2005
	WC	3C2FAA80	:2006
	WC	3E10DE00	:2007
	WC	3FF21180	:2008
	WC	41D49680	:2009
	WC	43B5CA00	:2010
	WC	4596FD80	:2011
	WC	47783100	:2012
	WC	495AB600	:2013
	WC	4B3BE980	:2014
	WC	4D1D1D00	:2015
	WC	4EFE5080	:2016
	WC	50E0D580	:2017
	WC	52C20900	:2018
	WC	54A33C80	:2019
	WC	56847000	:2020
	WC	5866F500	:2021
	WC	5A482880	:2022
	WC	5C295C00	:2023
	WC	5E0A8F80	:2024
	WC	5FED1480	:2025
	WC	61CE4800	:2026
	WC	63AF7B80	:2027
	WC	6590AF00	:2028
	WC	67733400	:2029
	WC	69546780	:2030
	WC	6B359B00	:2031
	WC	6D16CE80	:2032
	WC	6EF95380	:2033
	WC	70DA8700	:2034
	WC	72BBBA80	:2035
	WC	749CEE00	:2036
	WC	767F7300	:2037
	WC	7860A680	:2038
	WC	7A41DA00	:2039
	WC	7C230D80	:2040
	WC	7E059280	:2041
	WC	7FE6C600	:2042
TIMSIZ  EQ	.-TIMTBL
YEARS	EQ	TIMSIZ/4
	ORG	10000
START	LPSW	SPSW,,
	LCS	R14,1
	LIS	R8,0
MAIN	LIS	R3,9
	LIS	R15,1		:***** ERROR CODE 1 *****
	RDR	R3,R9		:Read high 16 bits
	JEFS	GREAD
BREAD	LPSW	BRHALT,,
BRHALT	WC	800B,0
:
GREAD	LR	R10,R9
	EXHR	R11,R9
	LHI	R3,109
	LIS	R15,2		:***** ERROR CODE 2 *****
	RDR	R3,R9		:Read low 16 bits
	JNBS	BREAD
	LR	R4,R9		:Save in R4
	OR	R9,R11
	LIS	R3,9
	LIS	R15,3		:***** ERROR CODE 3 *****
	RDR	R3,R12		:Read high 16 bits
	JN	BREAD
	EXHR	R12,R12
	CR	R11,R12
	JN	GR2
	LR	R14,R14
	JG	FBDIS
	LR	R1,R1
	JN	DDIS
HDIS	CIO	R9,R1
COMPAR	LR	R8,R8
	JEFS	GR3
	CR	R8,R9
	JLE	MAIN
	J	FAIL
GR3	LR	R8,R9
	J	MAIN
GR1	AIS	R8,1
	CR	R8,R9
	JE	MAIN
	SIS	R8,1
FAIL	LPSW	FHALT,,
GR2	AIS	R12,1
	CR	R11,R12
	JNBS	FAIL
	J	MAIN
DDIS	LHI	R15,TIMSIZ	:First find out what year we have
DDIS1	LR	R13,R9
	S	R13,TIMTBL-4,R15
	JGEFS	DDIS2		:We found it, now jump
	SIS	R15,4
	JGBS	DDIS1
	LIS	R14,0
	JFS	DDIS3
DDIS2	SRLS	R15,2		:Shift it down so that the index is right
	LHI	R14,$A1973,$00F	:Now make it understandable
DDIS3	LIS	R2,0
	LR	R3,R13		:Copy the left over time
	LI	R4,$A86400
	DR	R2,R4
	LR	R7,R2
	LR	R13,R3		:Put the day into R13
	AIS	R13,1
	LIS	R6,0
	LI	R2,$A3600
	DR	R6,R2		:Put the hour into R7
	LIS	R5,0
	LI	R2,$A60
	DR	R5,R2		:Put the minute into R6, and the second into R5
	LR	R0,R14
	JAL	R2,HTOD		:Make the year decimal
	LR	R15,R0
	LR	R0,R13
	JAL	R2,HTOD		:Make the day decimal
	LR	R13,R0
	LR	R0,R7
	JAL	R2,HTOD		:Make the hour decimal
	LR	R7,R0
	LR	R0,R6
	JAL	R2,HTOD		:Make the minute decimal
	LR	R6,R0
	LR	R0,R5
	JAL	R2,HTOD		:Make the second decimal
	LR	R5,R0
	EXBR	R6,R6
	OR	R6,R5
	LCS	R14,1
	SIS	R1,1
	JNFS	TDIS
YDIS	LR	R14,R15		:Display the year and the day
	CIO	R13,R1
	LCS	R14,1
	J	COMPAR
TDIS	SIS	R1,1		:Display the hour,minute,secound
	JN	SETCHK
	CIO	R6,R1
	J	COMPAR
HTOD	LR	R12,R0
	NHI	R12,0FFFF
	LIS	R3,0
	LIS	R0,0
HTOD1	LR	R11,R12
	JER	R2
	LIS	R10,0A
	DHR	R11,R10
	SLHL	R11,0,R3
	OR	R0,R11
	AIS	R3,4
	J	HTOD1
SETCHK	AIS	R1,2
	CHI	R1,0A
	JE	ABSET
	CHI	R1,0B
	JEFS	BKSET
	CHI	R1,0F
	JEFS	FWSET
	J	HDIS
BKSET	LHI	R3,0BBBB	:Come here to move the clock back in time
	JAL	R15,GETNUM
	NHI	R14,7FF
	J	MAIN
FWSET	LHI	R3,0FFFF	:Come here to move the clock forward in time
	JAL	R15,GETNUM
	NHI	R14,7FF
	LR	R15,R14
	LIS	R14,0
	SR	R14,R15
	NHI	R14,07FF
	J	MAIN
GETNUM	LIS	R4,0		:Routine to get a value from the console
GTNUM1	CIO	R3,R5		:YES,,,,IT IS AS FLAKEY AS IT LOOKS...
	LR	R5,R5
	JNBS	GTNUM1		:Wait for the DTA button to be pushed.
GTNUM2	CIO	R3,R14
	LR	R14,R14
	JEBS	GTNUM2		:Wait for the value to go non-zero
	LR	R5,R14
GTNUM3	LR	R14,R5
	CIO	R3,R5
	LR	R5,R5
	JNBS	GTNUM3		:Wait for the DTA button to be pushed again
	LR	R4,R14		:Now convert to decimal
	LIS	R14,0
	LIS	R3,3
GTNUM4	SLLS	R4,4
	NI	R4,0FFFF0
	LR	R5,R14
	SLLS	R5,3
	AR	R14,R14
	AR	R14,R5
	EXHR	R5,R4
	LBR	R5,R5
	AR	R14,5
	SIS	R3,1
	JGE	GTNUM4
	JR	R15	
FBDIS	NHI	R4,07FF
	LR	R3,R14
	SR	R3,R4
	JGFS	FBDIS1
	NHI	R3,7FF
	JEFS	FBSET
	AHI	R3,800
FBDIS1	LIS	R4,0
	CIO	R3,R1
	J	MAIN
FBSET	LHI	R3,209
	LIS	R15,4
	RDR	R3,R4		:***** ERROR CODE 4 *****
	JN	BREAD
	LCS	R14,1
	LIS	R8,0
	J	MAIN
ABSET	LHI	R3,1111
	JAL	R15,GETNUM
	LR	R6,R14		:SAVE YEAR
	SHI	R6,$A1974
	JLBS	ABSET
	SLLS	R6,2
	L	R9,TIMTBL,R6
	SHI	R6,YEARS*4
	JGE	ABSET
ABS22	LHI	R3,2222
	JAL	R15,GETNUM
	SIS	R14,1
	JLBS	ABS22
	LR	R6,R14		:SAVE JULIAN DAY
	SHI	R14,$A365
	JGEBS	ABS22
	LI	R7,$A60*60*24
	JAL	R15,SUM
ABS33	LHI	R3,3333
	JAL	R15,GETNUM
	NHI	R14,0FF
	LR	R6,R14		:SAVE HOUR
	SHI	R14,$A24
	JGEBS	ABS33
	LHI	R7,$A60*60
	JAL	R15,SUM
ABS44	LHI	R3,4444
	JAL	R15,GETNUM
	NHI	R14,0FF
	LR	R6,R14		:SAVE MINUTE
	SHI	R14,$A60
	JGEBS	ABS44
	LHI	R7,$A60
	JAL	R15,SUM
ABS55	LHI	R3,5555
	JAL	R15,GETNUM
	NHI	R14,0FF
	LR	R6,R14
	SHI	R14,$A60
	JGEBS	ABS55
	AR	R9,R6		:ADD SECONDS
	LR	R10,R9
	NI	R10,7FFFF000
	LR	R11,R10
	EXHR	R11,R11
	LHR	R11,R11
	LHI	R3,209		:Zero the bottom part for timing control
	LIS	R15,9
	RDR	R3,R12		:***** ERROR CODE 9 *****
	JN	BREAD
	LIS	R3,9
	LHI	R4,409
ABSET1	LIS	R15,5
	RDR	R3,R12		:***** ERROR CODE 5 *****
	JN	BREAD
	CIO	R11,R1		:CONSOLE DISPLAY = |0|read|want|
	CR	R12,R11
	JE	ABSET2
	LIS	R15,6
	RDR	R4,R12		:***** ERROR CODE 6 *****
	JN	BREAD
	J	ABSET1
ABSET2	LHR	R11,R10		:UPPER PART IS NOW CORRECT, GO FOR THE BOTTOM
	NI	R11,0F000
	LHI	R3,109
ABSET3	LIS	R15,7
	RDR	R3,R12		:***** ERROR CODE 7 *****
	JN	BREAD
	NI	R12,0F000
	CIO	R11,R1		:CONSOLE DISPLAY = |0|r000|w000|
	CR	R12,R11
	JE	ABSET4
	LIS	R15,8
	RDR	R4,R12		:***** ERROR CODE 8 *****
	JN	BREAD
	J	ABSET3
ABSET4	LR	R10,R9		:12 more bits to go
	NI	R10,0FFF
	LIS	R15,0A
	RDR	R3,R12		:***** ERROR CODE 0A *****
	JN	BREAD
	NI	R12,07FF
	AR	R10,R12
	CHI	R10,1000
	JGEFS	ABST45
	AHI	R10,1000
ABST45	CHI	R10,1000
	JLFS	ABSET5
	LIS	R15,0C
	RDR	R4,R13		:***** ERROR CODE 0C *****
	JN	BREAD
	SHI	R10,1000
	JBS	ABST45
ABSET5	LHI	R3,109		:Command to read low 16 bits
	LHI	R4,209		:Command to reset low 12 bits
	LIS	R11,0
ABSET6	LIS	R15,0D		:***** ERROR CODE 0D *****
	RDR	R4,R12
	JN	BREAD
	LR	R10,R10
	JE	ABSET8
	CIO	R10,R1
ABSET7	LIS	R15,0E		:***** ERROR CODE 0E *****
	RDR	R3,R12
	JN	BREAD
	LBR	R12,R12
	JEBS	ABSET7
	SIS	R10,1
	J	ABSET6
ABSET8	LCS	R14,1
	LIS	R8,0
	J	MAIN
SUM	SIS	R6,1
	JLR	R15
	AR	R9,R7
	JBS	SUM
DEND	EQ	.
 