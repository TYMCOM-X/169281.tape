100 !
200 !TYMCOM-10 SEND 'MAIL' UTILITY
300 !WRITTEN BY BRADLEY DONG
400 ! 22-JUL-1987
500 !************************************************
600 ! 31-JUL-87 CHANGED SYSTEM NUMBER INPUT PROMPT.
700 !WILL NOW GET APR AND H/W ID FROM FILE (UTIL)CM10.LST.
800 !*************************************************
900 !CHANGED "INFO" PROMPT TO "HELP"  11-AUG-87
1000 !*************************************************
1100 !PATCHED TO ALLOW CM10 TO BE RUN FROM ANY USERNAME 23-OCT-87.
1200 !*********************************************************
1300 !PATCHED TO CHECK FOR/USE EITHER (UTIL)CRALST
1400 !OR (UTIL)CRALST.CMD  12-NOV-87.
1500 !********************************************
1600 !     NOTE:  WILL NEED CM10.LST TO RUN.  CM10.LST IS A 
1700 !            LIST OF ALL PDP10 SYSTEMS YOU WISH TO SEND MAIL FOR.
1800 !     FORMAT:  SYS,APR,HWID
1900 !         IE.  58,1332,EEE
2000 INTEGER BUSS,APR
2100 STRING OPER,DWN,UP,ANS,MON,CRA,CLN,GVN,DESC,MEM,MNUMBER,HWID,SYSIN,SYS
2200 STRING SECTOR,MD,WR,CONT,PORT,TYPE,TOFSC,TOOPS,STOPTYPE,STOPCD,CRALST
2300 STRING DSCHED,UPSCHED,CRTEST,LMON,INTERL,TYPECD,CODE,FULL,SET,DMP,TAPES
2400 !
2500 !
2600 !****************************************************
2700 !**   CHECK FOR (UTIL)CRALST OR (UTIL)CRALST.CMD   **
2800 !****************************************************
2900 !
3000 !
3100 DEF LOGICAL EXTERNAL FD.OPEN(STRING A)
3200 DEF EXTERNAL FD.EXACT(STRING(FILENAME,EXTENTION))
3300 DEF LOGICAL EXTERNAL FD.STEP
3400 DEF EXTERNAL FD.CLOSE
3500 CALL FD.OPEN("UTIL")
3600 CALL FD.EXACT("CRALST","CMD")
3700 IF FD.STEP THEN
3800    CRALST="CRALST.CMD"
3900    ELSE
4000                    CRALST="CRALST"
4100 ENDIF
4200 CALL FD.CLOSE
4300 !
4400 !        **************
4500 !        **   MAIN   **
4600 !        **************
4700 REMOVE "SEND.CMD"
4800 OPEN "SEND.CMD-A" FOR OUTPUT AS FILE 1
4900 OPEN "(UTIL)CM10.LST-A",RANDOM IO,2
5000 PRINT
5100 PRINT 'TYMCOM-10 SEND "MAIL" UTILITY'
5200 PRINT "       ":DATE
5300 PRINT
5400 IIM "HELP, CRASH, ASP, PM, ASP/PM (A/P), OR OTHER:  #":CRTEST
5500 PRINT
5600 FOR I=1 TO 5
5700 IF CRTEST=LEFT("CRASH",I) THEN
5800            CRTEST="CRASH"
5900            GOTO 7900
6000     ORIF CRTEST=LEFT("ASP",I) THEN
6100            CRTEST="ASP"
6200            GOTO 7900
6300     ORIF CRTEST=LEFT("PM",I) THEN
6400             CRTEST="PM"
6500             GOTO 7900
6600     ORIF CRTEST="A/P" THEN
6700            CRTEST="A/P"
6800            GOTO 7900
6900     ORIF CRTEST=LEFT("OTHER",I) THEN
7000            CRTEST="OTHER"
7100            GOTO 7900
7200    ORIF CRTEST=LEFT("HELP",I) THEN
7300            CRTEST="HELP"
7400            GOTO 7900
7500     ELSE
7600            NEXT I
7700             PRINT
7800             GOTO 5400
7900     ENDIF
8000 IF CRTEST="HELP" THEN 9300
8100 IIM "OPER NAME:  #":OPER
8200 IIM "SYSTEM (SYSNO ONLY):  #":SYSIN
8300 LOCATE 1 ON 2
8400 ON ENDFILE (2) GOTO 8800
8500 INPUT FROM 2:SYS,APR,HWID
8600         IF SYSIN#SYS THEN
8700                 GOTO 8400
8800         PRINT
8900                 IIM "SYSTEM NUMBER ONLY PLEASE:  #":SYSIN
9000                 GOTO 8300
9100         ENDIF
9200 PRINT
9300 IF CRTEST="CRASH" THEN
9400            GOSUB 11800
9500    ORIF CRTEST="ASP" THEN
9600            TYPE="ASP DUMP"
9700            GOSUB 17700
9800     ORIF CRTEST="PM" THEN
9900            TYPE="PREVENTATIVE MAINTENANCE"
10000             GOSUB 26100
10100    ORIF CRTEST="A/P" THEN
10200            TYPE="ASP DUMP/PREVENTATIVE MAINTEANCE"
10300            GOSUB 28200
10400    ORIF CRTEST="OTHER" THEN
10500            TYPE="SPECIAL"
10600            GOSUB 26100
10700    ORIF CRTEST="HELP" THEN
10800            GOSUB 45400
10900    ENDIF
11000 IF CRTEST="HELP" THEN 5400
11100 PRINT
11200 CLOSE 1
11300 CLOSE 2
11400 PRINT "TO SEND MAIL ENTER:  RUN COM;SEND.CMD<CR>DEL<CR>"
11500 PRINT
11600 PRINT "**END OF JOB**"
11700 END
11800 !          ********************************
11900 !          *    C R A S H  M O D U L E    *
12000 !          ********************************
12100 IIM "DATE OF CRASH:  #":DATE$
12200 IIM "DOWN:  #":DWN
12300 IIM "UP TIME (TIME LAST DSKCLN STARTED):  #":UP
12400 IIM "ANSWERED:  #":ANS
12500 PRINT
12600 IIM "CRASH FILE:  #":CRA
12700 PRINT
12800 GOSUB 37900
12900 GOSUB 36300
13000 PRINT
13100 PRINT "CRASH TYPE"
13200 IIM "STOPCODE, PARITY ERROR OR OTHER:  #":TYPE
13300 FOR I=1 TO 12
13400 IF TYPE=LEFT("STOPCODE",I) THEN
13500           TYPE="STOPCODE"
13600           GOTO 14700
13700 ORIF TYPE=LEFT("PARITY ERROR",I) THEN
13800           TYPE="PARITY ERROR"
13900           GOTO 14700
14000 ORIF TYPE=LEFT("OTHER",I) THEN
14100           TYPE="OTHER"
14200           GOTO 14700
14300   ELSE
14400           NEXT I
14500           PRINT
14600           GOTO 13200
14700 ENDIF
14800 IF TYPE="STOPCODE" THEN
14900           GOSUB 30600
15000   ORIF TYPE="PARITY ERROR" THEN
15100           GOSUB 31200
15200   ORIF TYPE="OTHER" THEN
15300 ENDIF
15400 GOSUB 32200
15500 GOSUB 33900
15600 PRINT ON 1:"SEND @(UTIL)":CRALST
15700 PRINT ON 1:"F":SYS:"/":HWID:" APR: ":APR:" -- ":TYPE:" - CRASH"
15800 PRINT ON 1:"DATE:  ":DATE$:"   DOWN:  ":DWN:"   ANS:  ":ANS
15900 IF TYPE="STOPCODE" THEN
16000           PRINT ON 1:"STOPCODE TYPE:  ":STOPTYPE:"   NAME:  ":STOPCD:"   M NUMBER:  ":MNUMBER
16100   ORIF TYPE="PARITY ERROR" THEN
16200           PRINT ON 1:"SECTOR:  ":SECTOR:"   MODULE:  ":MD:"   WR:  ":WR:"   CONT:  ":CONT:"   PORT:  ":PORT
16300 ENDIF
16400 GOSUB 41300
16500 PRINT ON 1:"MEMORY ONLINE: ":MEM:"   BUSS MODE: ":BUSS:"   ":INTERL
16600 PRINT ON 1:"CRASH FILE:  ":CRA
16700 PRINT ON 1:
16800 PRINT
16900 PRINT "ENTER CTY OUTPUT AND RECOVERY DESCRIPTION PERTAINING TO CRASH."
17000 PRINT "TERMINATE WITH  <CR>Z<CR>   (30 LINES MAX):"
17100 GOSUB 39800
17200 RETURN
17300 !
17400 !           ***************************
17500 !           *   A S P   M O D U L E   *
17600 !           ***************************
17700 GOSUB 42100
17800 IIM "IS THIS A GOOD OR BAD DUMP:  #":DMP
17900 FOR I=1 TO 4
18000 IF DMP=LEFT("BAD",I) THEN
18100           DMP="B A D  D U M P"
18200           GOSUB 20000
18300           IF CRTEST="A/P" THEN 18400 ELSE GOSUB 32200
18400            GOSUB 21800
18500 PRINT
18600            GOSUB 41200
18700           PRINT ON 1:"     ":DMP
18800           PRINT ON 1:
18900                 PRINT "ENTER REASON(S) DUMP IS BAD."
19000              PRINT:"TERMINATE WITH  <CR>Z<CR>   (30 LINES MAX):"
19100                 GOSUB 39800
19200                 RETURN
19300         ORIF DMP=LEFT("GOOD",I) THEN
19400                 DMP="G O O D  D U M P"
19500         ELSE
19600                 NEXT I 
19700                 PRINT
19800                 GOTO 17800
19900 ENDIF
20000 IIM "WAS A FULL DSKCLN RUN BEFORE DUMP?  #":FULL
20100 FOR I=1 TO 3
20200 IF FULL=LEFT("YES",I) THEN
20300           PRINT ON 1:"A FULL DSKCLN WAS RUN BEFORE DUMP."
20400                 GOTO 21200
20500         ORIF FULL=LEFT("NO",I) THEN
20600           PRINT ON 1:"NO DSKCLN WAS RUN BEFORE DUMP."
20700                 GOTO 21200
20800         ELSE
20900                 NEXT I
21000                 PRINT
21100                 GOTO 20000
21200 ENDIF
21300 IF DMP="B A D  D U M P" THEN RETURN ELSE 21400
21400 PRINT
21500 IIM "SET:  #":SET
21600 IIM "NUMBER OF TAPES USED:  #":TAPES
21700 IF CRTEST="A/P" THEN PRINT ELSE GOSUB 32200
21800 IF CRTEST="A/P" THEN 22200
21900 IIM "DSKCLN AFTER DUMP (FULL, FAST OR NONE):  #":CLN
22000 GOSUB 38000
22100 GOSUB 36300
22200 IF DMP="G O O D  D U M P" THEN 22400
22300 RETURN
22400 PRINT ON 1:"   ":DMP
22500 PRINT ON 1:"SET USED: ":SET:"   NUMBER OF TAPES USED: ":TAPES
22600 PRINT ON 1:
22700 GOSUB 41200
22800 PRINT 
22900 IIM "ANY COMMENTS?  #":FULL
23000 FOR I=1 TO 3
23100 IF FULL=LEFT("YES",I) THEN
23200                 FULL="YES"
23300                 GOTO 24100
23400         ORIF FULL=LEFT("NO",I) THEN
23500                 FULL="NO"
23600                 GOTO 24100
23700         ELSE
23800                 NEXT I
23900                 PRINT
24000                 GOTO 22900
24100 ENDIF
24200 IF CRTEST="A/P" AND FULL="YES" THEN
24300           PRINT "ENTER COMMENTS   (30 LINES MAX)"
24400           PRINT "TERMINATE WITH <CR>Z<CR>:"
24500           GOSUB 39800
24600 ENDIF
24700 IF CRTEST="A/P" THEN 25700
24800 IF FULL="YES" THEN
24900                 PRINT "ENTER COMMENTS   (30 LINES MAX):"
25000           PRINT "TERMINATE WITH  <CR>Z<CR>:"
25100                 GOSUB 39800
25200         ORIF FULL="NO" THEN
25300                 PRINT ON 1:"FREMONT OPERATIONS...":OPER
25400                 PRINT ON 1:CHAR(4)
25500                 PRINT ON 1:"GFD"
25600 ENDIF
25700 RETURN
25800 !          ************************
25900 !          *   P M  M O D U L E   *
26000 !          ************************
26100 GOSUB 42100
26200 GOSUB 32200
26300 GOSUB 33900
26400 PRINT
26500 GOSUB 37900
26600 GOSUB 36300
26700 GOSUB 41300
26800 PRINT ON 1:"MEMORY ONLINE: ":MEM:"   BUSS: ":BUSS:"   ":INTERL
26900 IF CRTEST="OTHER" THEN
27000           PRINT "ENTER REASON(S) FOR OUTAGE."
27100   ELSE
27200 PRINT "ENTER WHAT FSC DID DURING P/M."
27300 ENDIF
27400 PRINT "TERMINATE WITH  <CR>Z<CR>   (30 LINES MAX):"
27500 GOSUB 39800
27600 RETURN
27700 !***************************************************************
27800 !**                A S P / P M  M O D U L E                   **
27900 !**  THIS IS A COMBINATION OF BOTH ASP AND PM MODULES.  THIS  **
28000 !**  MODULE WILL BE "JUMPING" BETWEEN THEM AND THIS ONE.      **
28100 !***************************************************************
28200 GOSUB 17700
28300 GOSUB 27200
28400 GOSUB 32200
28500 GOSUB 33900
28600 PRINT
28700 GOSUB 37900
28800 GOSUB 36300
28900 GOSUB 41300
29000 PRINT ON 1:"FREMONT OPERATIONS...":OPER
29100 PRINT ON 1:CHAR(4)
29200 PRINT ON 1:"GFD"
29300 RETURN
29400 !         *******************************************
29500 !         **   O T H E R  (SPECIAL)  M O D U L E   **
29600 !         *******************************************
29700 GOSUB 42100
29800 GOSUB 32100
29900 GOSUB 33900
30000 GOSUB 37900
30100 GOSUB 36300
30200 GOSUB 41300
30300 PRINT ON 1:"MEMORY ONLINE: ":MEM:"   BUSS MODE: ":BUSS:"   ":INTERL
30400 GOSUB 39800
30500 RETURN
30600 !STOPCODE CRASH
30700 PRINT
30800 IIM "TYPE (IE. HALT, CPU,DISK...INFO):  #":STOPTYPE
30900 IIM "NAME (IE. BADSAT, FILAUS...TTYZNE):  #":STOPCD
31000 IIM '"M" NUMBER (LAST ITEM ON THE SYSTEM ID LINE):  #':MNUMBER
31100 RETURN
31200 !PARITY ERROR CRASH
31300 PRINT
31400 IIM"SECTOR:  #":SECTOR
31500 IIM "MODULE:  #":MD
31600 IIM "WR:  #":WR
31700 IIM "CONT:  #":CONT
31800 IIM "PORT:  #":PORT
31900 RETURN
32000 !
32100 !MONITOR LOAD CHECK
32200 PRINT
32300 IIM "MONITOR:  #":MON
32400 IIM "LOADED FROM DISK OR TAPE:  #":LMON
32500 FOR I=1 TO 4
32600 IF LMON=LEFT("DISK",I) THEN
32700            LMON="DISK"
32800            GOTO 33500
32900    ORIF LMON=LEFT("TAPE",I) THEN
33000            LMON="TAPE"
33100            GOTO 33500
33200    ELSE
33300            NEXT I
33400            GOTO 32400
33500   ENDIF
33600 RETURN
33700 !
33800 !BUS/INTERLEAVE CHECK
33900 PRINT
34000 IIM"MEMORY ONLINE:  #":MEM
34100 IIM "BUSS MODE (1, 2 OR 4):  #":BUSS
34200 IF BUSS=1 OR BUSS=2 OR BUSS=4 THEN
34300            GOTO 34700
34400    ELSE
34500            PRINT
34600            GOTO 34100
34700 ENDIF
34800 IIM "INTERLEAVE OR NONINTERLEAVE:  #":INTERL
34900 FOR I=1 TO 13
35000 IF INTERL=LEFT("INTERLEAVE",I) THEN
35100            INTERL="INTERLEAVE"
35200            GOTO 36000
35300 ORIF INTERL=LEFT("NONINTERLEAVE",I) THEN
35400            INTERL="NONINTERLEAVE"
35500            GOTO 36000
35600    ELSE
35700            NEXT I
35800            PRINT
35900            GOTO 34800
36000    ENDIF
36100 RETURN
36200 !
36300 IIM "WAS DSKCLN 'FORCED' BY OPERATOR OR 'CHOSEN' BY SYSTEM:  #":GVN
36400 FOR I=1 TO 6
36500 IF GVN=LEFT("FORCED",I) THEN
36600            GVN="OPERATOR FORCED"
36700           GOTO 37500
36800 ORIF GVN=LEFT("CHOSEN",I) THEN
36900            GVN="SYSTEM CHOSE"
37000           GOTO 37500
37100    ELSE
37200            NEXT I
37300            PRINT
37400            GOTO 36300
37500    ENDIF
37600 RETURN
37700 !
37800 !TYPE DSKCLN RUN CHECK
37900 IIM "DSKCLN (FULL, FAST OR NONE):  #":CLN
38000 FOR I=2 TO 4
38100 IF CLN=LEFT("FULL",I) THEN
38200            CLN="FULL"
38300            GOTO 39400
38400    ORIF CLN=LEFT("FAST",I) THEN
38500            CLN="FAST"
38600            GOTO 39400
38700    ORIF CLN=LEFT("NONE",I) THEN
38800            CLN="NONE"
38900            GOTO 39400
39000    ELSE
39100            NEXT I
39200            PRINT
39300            GOTO 37900
39400    ENDIF
39500 RETURN
39600 !
39700 !MAIL DESCRIPTION
39800 FOR I=1 TO 30
39900 PRINT "==>":
40000 IIF 'R':DESC
40100 IF DESC="Z" THEN 40400
40200 PRINT ON 1:"   ":DESC
40300 NEXT I
40400 PRINT ON 1:
40500 IF CRTEST="A/P" THEN RETURN
40600 PRINT ON 1:"FREMONT OPERATIONS...":OPER
40700 PRINT ON 1:CHAR(4)
40800 PRINT ON 1:"GFD"
40900 RETURN
41000 !
41100 !OUTPUT MONITOR/DSKCLN TYPE RUN
41200 IF CRTEST="A/P" THEN RETURN
41300 PRINT
41400 IF CLN="NONE" THEN 41500 ELSE 41700
41500 PRINT ON 1:"LOADED ":MON:" FROM ":LMON:".  NO DSKCLN WAS RUN."
41600 GOTO 41800
41700 PRINT ON 1:"LOADED ":MON:" FROM ":LMON:".  ":GVN:" ":CLN:" DSKCLN AT ":UP:"."
41800 RETURN
41900 !
42000 !INPUT SCHEDULED/ACTUAL TIMES
42100 PRINT "TIME F":SYS:"/":HWID:" SCHEDULED DOWN - ":
42200 IIM "(MM/DD/YY HHMM):  #":DSCHED
42300 PRINT "               SCHEDULED UP - ":
42400 IIM "(MM/DD/YY HHMM):  #":UPSCHED
42500 PRINT
42600 PRINT "ACTUAL TIME F":SYS:"/":HWID:"    DOWN - ":
42700 IIM "(MM/DD/YY HHMM):  #":DWN
42800 PRINT "                   ANSWERED - ":
42900 IIM "(MM/DD/YY HHMM):  #":ANS
43000 PRINT
43100 IIM "UP TIME (TIME LAST DSKCLN STARTED):  #":UP
43200 PRINT
43300 IF CRTEST="A/P" THEN 43400 ELSE 44100
43400 PRINT "TIME F":SYS:"/":HWID:" GIVEN TO FSC - ":
43500 IIM "(MM/DD/YY HHMM):  #":TOFSC
43600 PRINT "   RETURNED TO OPERATIONS - ":
43700 IIM "(MM/DD/YY HHMM):  #":TOOPS
43800 PRINT
43900 !
44000 !OUTPUT SCHEDULED/ACTUAL TIMES
44100 PRINT ON 1:"SEND @(UTIL)":CRALST
44200 PRINT ON 1:"F":SYS:"/":HWID:" APR: ":APR:" - ":TYPE
44300 PRINT ON 1:"SCHEDULED DOWN - ":DSCHED
44400 PRINT ON 1:"            UP - ":UPSCHED
44500 PRINT ON 1:
44600 PRINT ON 1:"   ACTUAL DOWN - ":DWN
44700 PRINT ON 1:"      ANSWERED - ":ANS
44800 IF CRTEST="A/P" THEN 44900 ELSE 45200
44900 PRINT ON 1:
45000 PRINT ON 1:"        TO FSC - ":TOFSC
45100 PRINT ON 1:"RETURNED TO OPERATIONS - ":TOOPS
45200 PRINT ON 1:
45300 RETURN
45400 PRINT "         INFORMATION"
45500 PRINT
45600 PRINT
45700 PRINT 'TYMCOM-10 SEND "MAIL" WILL PROMPT FOR ALL REQUIRED'
45800 PRINT "INFORMATION CONCERNING MOST TYPES OF OUTAGES."
45900 PRINT 
46000 PRINT 'ANY PROMPT "GIVING" A REPLY CAN BE ABBREVIATED DOWN TO THE FIRST'
46100 PRINT "ONE OR TWO CHARACTERS."
46200 PRINT
46300 PRINT "ENTER ALL DATES/TIMES AS MM/DD/YY HHMM."
46400 PRINT
46500 PRINT
46600 PRINT "         HOW TO INTERPRET STOPCODES"
46700 PRINT
46800 PRINT"THE FIRST LINE OF OUTPUT HAS THE TYPE OF STOPCODE, ITS NAME, AND A TIMESTAMP."
46900 PRINT "THE SECOND LINE IS USUALLY THE SYSTEM ID.  THE THIRD , IF PRESENT,"
47000 PRINT "IS THE FRAME ID DESCRIBING WHICH JOB OR PROGRAM CAUSED THE STOPCODE."
47100 PRINT "THE REMAINDER OF THE STOPCODE OUPUT DEPENDS ON THE TYPE OF STOPCODE."
47200 PRINT
47300 PRINT "WHEN SENDING CRASHMAIL, THE ONLY IMPORTANT PIECE OF INFORMATION TO "
47400 PRINT 'INCLUDE FROM THE SYSTEM ID IS THE "M" NUMBER.  THE "M" NUMBER IS'
47500 PRINT 'USUALLY THE LAST ITEM ON THE SYSTEM ID LINE.  THE "M" NUMBER IS VERY '
47600 PRINT "IMPORTANT FOR DEBUGGING CRASH DUMPS."
47700 PRINT
47800 PRINT "SOME STOPCODES DO NOT YET HAVE NAMES ASSIGNED TO THEM.  WHEN THEY DO "
47900 PRINT "OCCUR, IT IS VERY IMPORTANT TO MAKE NOTE OF THE SIX DIGIT STOPCODE NUMBER"
48000 PRINT "SO THAT IT CAN BE LOCATED AND GIVEN A NAME.  THE NAME AND THE TWO"
48100 PRINT "NUMBERS IN QUOTES POINT TO THE STOPCODE LOCATION."
48200 PRINT
48300 PRINT '   ?HALT STOPCODE "IME" AT 15-OCT-1986 13:37:45'
48400 PRINT "   F26, HARDWARE FFF, KL-1324, BASE 10350, F26-P035/C, 7-19-87, M01778"
48500 PRINT
48600 PRINT "   STOPCODE   TYPE=HALT    NAME=IME    M NUMBER=M01778"
48700 PRINT
48800 PRINT
48900 RETURN
   