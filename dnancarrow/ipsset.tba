100  ! PROGRAM TO RECOMPUTE THE IPS INFORMATION IN ACCORDANCE WITH ITU
110 ! INDEX OF SUBROUTINE LOCATIONS
120 ! INDEX OF SUBROUTINE LOCATIONS
130 GO TO 210
140 GO TO 1780 ! REMOVE COMMAS
150 GO TO 1580 ! NOW WE HAVE SDR VALUE READ IN THE RATES
160 GO TO 1430 ! NOW FIND THE DIRECT CONNECT CARRIER AND SDR VALUE
170 GO TO 1380! ERROR ROUTINE
180 GO TO 1120! PRINT OUT SERVICES FOR 1 MONTH
190 GO TO 870 ! SERVICE WAS NOT FOUND
200 GO TO  1460 ! FIND THE CARRIER AND READ IN THE RATES
210 ON ERROR GOTO 1400
220 STRING S(99),SVC,A,S1,S2,B,INF,OUTF,S3,S4,CUR,DIR.CARRIER,DIR.ABB,CUR.T(99)
230 STRING TYPE(99),TYPE1
240 VER=910607.1
250 DIR.CARRIER="BRITISH  T"
260 DIR.ABB="IPS"
270 DIM RKSEG(99),RHRS(99),HRS.S(99),KSEG.S(99),SESS.S(99),PC.T(99)
280 S1="%%%%%%%%%%%%%%%%%% %%%%% %%%%%.%%% %%%.%% %%%%%%% %%%.%% % %.%% %%%%%%%"
290 S3="%%%%%%%%%%%%%%%%%%%%%% %%%%%.%% %%%%%.%% %.%%  %%% %%%%%%"
300 S4="%%%%%%%%%%%%%%%%%%%%%% %%%%%%%% %%%%%%%%"
310 S2="%%%%%%%%%%%%%%%%%%                                              %%%%%%%"
320 PRINT "INPUT FILE ":
330 INPUT INF
340 OUTF= DIR.ABB+"OUT"+"."+RIGHT(INF,3)
350 OPEN OUTF+"-A",OUTPUT,5
360 WRITE ON 5:" VERSION: ":VER
370 PRINT " VERSION: ":VER
380 WRITE ON 5:"IN  FILE: ":INF
390 PRINT "IN  FILE: ":INF
400 WRITE ON 5:"OUT FILE: ":OUTF
410 PRINT "OUT FILE: ":OUTF
420 WRITE ON 5:DATE
430 PRINT DATE
440 WRITE ON 5: "                             BT TYMNET "
450 WRITE ON 5: "                        PACKET ACCOUNT WITH"
460 WRITE ON 5: "                           IPSS"
470 WRITE ON 5: "    "
480 WRITE ON 5: "   "
490 OPEN "(INTLPROGS)"+INF ,INPUT,6
500  ON ENDFILE(6) GOTO 990
510 FOR K= 1 TO 30
520 INPUT FROM 6 IN FORM "R":A
530 IF INDEX(A," 199")#0 THEN 570
540 IF INDEX(A," 198")#0 THEN 570
550 NEXT K
560 GOTO 600
570 WRITE ON 5:
580 WRITE ON 5: LEFT(A,30)
590 PRINT  LEFT(A,30)
600 WRITE ON 5:
610 FOR K= 1 TO 3000
620 INPUT FROM 6 IN FORM "R":A
630 NEXT K
640 GO SUB 1460 ! FIND THE CARRIER AND READ IN THE RATES
650 INPUT FROM 6 IN FORM "R":A
660 IF INDEX(A," TOTALS")#0 THEN 990
670 IF INDEX(A,"TOTAL")#0 THEN  650
680 IF SUBSTR(A,63,1)#"." THEN 650
690!
700 SVC=SUBSTR(A,3,15)
710 B=SUBSTR(A,44,53-44)
720 GOSUB 1780
730 SESS=VAL(B)
740 B=SUBSTR(A,53,67-53)
750 GOSUB 1780
760 HRS =VAL(B)
770 B=SUBSTR(A,67,82-67)
780 GOSUB 1780
790 KCH =VAL(B)
800 B=SUBSTR(A,82,93-82)
810 GOSUB 1780
820 KSEG=VAL(B)
830 ! SEARCH FOR THIS DNIC PAIR
840 FOR K=1 TO LAST.RATE
850 IF S(K)=SVC THEN 900
860 NEXT K
870 ! SERVICE WAS NOT FOUND
880 PRINT "SERVICE NOT FOUND: ":SVC
890 GOTO 650
900 HRS.S(K)=HRS.S(K)+HRS
910!PRINT A
920 KSEG.S(K)=KSEG.S(K)+KSEG
930 SESS.S(K)=SESS.S(K)+SESS
940 GOTO 650
950 GOSUB 1140
960 PRINT ON 5:
970 PRINT ON 5:A
980 GOTO 650
990 TYPE1="OUT"
999  WRITE ON 5:
1000 WRITE ON 5:" TRAFFIC OUTBOUND FROM TYMNET"
1010 GO SUB 1140
1020 TYPE1="IN"
1021 WRITE ON 5:
1030 WRITE ON 5:" TRAFFIC  INBOUND TO   TYMNET"
1040 GOSUB 1140
1041 WRITE ON 5:
1050 WRITE ON 5:" TRAFFIC IN OTHER CATAGORIES"
1060 TYPE1="OTHER"
1070 GOSUB 1140
1080 CLOSE(5)
1090 QUIT
1100!
1110!
1120! PRINT OUT SERVICES FOR 1 MONTH
1130 !
1140!
1150 WRITE ON 5: "   "
1160 WRITE ON 5: "     ORIG  DEST   SESSION  CONNECT  RATE    KILO-  RATE CUR  %    TOTAL"
1170 WRITE ON 5: "     DNIC  DNIC   COUNT    HOURS    /HR     SEGS  /KSEG TYP PAY  AMOUNT"
1180 FOR K=1 TO LAST.RATE
1190 IF SESS.S(K)=0 THEN 1320
1200 IF TYPE(K)#TYPE1 THEN 1320
1210 CUR=CUR.T(K)
1220 IF CUR="S" THEN FACT1=SDR ELSE IF CUR="G" THEN FACT1=1/2.54 ELSE FACT1=1
1230 ZZ1=RKSEG(K)*PC.T(K)*FACT1
1240 ZZ2=RHRS( K)*PC.T(K)*FACT1
1250  TOTAL=KSEG.S(K)*ZZ1     +HRS.S(K)*ZZ2
1260 GTOT=TOTAL+GTOT
1270!PRINT                  : S(K),SESS.S(K),HRS.S(K),RHRS(K),KSEG.S(K),RKSEG(K),CUR,TOTAL
1280 PRINT ON 5 IN IMAGE S1 : S(K),SESS.S(K),HRS.S(K),RHRS(K),KSEG.S(K),RKSEG(K),CUR,PC.T(K),TOTAL
1290        KSEG.S(K)=0
1300  HRS.S(K)=0
1310  SESS.S(K)=0
1320 NEXT K
1330 PRINT ON 5 IN IMAGE S2 :"    Monthly Total",GTOT
1340 GTOT=0
1350 RETURN
1360!
1370!
1380! ERROR ROUTINE
1390!
1400 PRINT "ERROR IN LINE :":ERRLN
1410 PAUSE
1420!
1430 ! NOW FIND THE DIRECT CONNECT CARRIER AND SDR VALUE
1440!
1450!
1460 INPUT FROM 6 IN FORM "R":A
1470 K=K+1
1480 IF INDEX(A,DIR.CARRIER )=0 THEN 1460
1490 PRINT A
1500 PRINT "LINE=":K
1510 INPUT FROM 6 IN FORM "R":A
1520 IF INDEX(A,"SDR VAL")=0 THEN 1510
1530 Z1=INDEX(A,".")-1
1540 SDR=VAL(SUBSTR(A,Z1,4))
1550 PRINT "SDR VALUE =":SDR
1560!
1570!
1580 ! NOW WE HAVE SDR VALUE READ IN THE RATES
1590 OPEN "RATES."+DIR.ABB,INPUT,4
1600 K=0
1610 ON ENDFILE(4) GOTO 1750
1620!PRINT IN IMAGE S4:"     ORIG  TERM","RATE/","RATE/"
1630!PRINT IN IMAGE S4:"SVC  DNIC  DNIV","HOUR","KSEG"
1640 INPUT FROM 4:A,X,Y,PC,CUR,B
1650!PRINT IN IMAGE S3:  A,X,Y,PC,CUR,B
1660 K=K+1
1670 TYPE(K)=B
1680 S(K)=A
1690 CUR.T(K)=CUR
1700 PC.T(K )=PC
1720 RHRS(K)=X
1730 RKSEG(K)=Y
1740 GO TO 1640
1750 LAST.RATE=K
1760 CLOSE(4)
1770 RETURN
1780 ! REMOVE COMMAS
1790 X= INDEX(B,",")
1800 IF X=0 THEN 1840
1810 PRINT B
1820 B=LEFT(B,X-1)+SUBSTR(B,X+1)
1830 GOTO 1790
1840 RETURN
