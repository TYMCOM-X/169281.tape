PROCEDURE SITE.WEEKLY
BEGIN

FIELDS

STARTING.DATE           SAME AS DATE.,
ENDING.DATE             SAME AS DATE.,
NUMBER.OF.WEEKS         AS "4N",
WEEK.TOTAL.DAYS         AS "N.3N",
WEEK.SCHEDULED.DAYS     AS "N.3N",
WEEK.PRIME.DAYS         AS "N.3N",
WEEK.TOTAL.SYSTEM.DAYS  AS "3N.3N",
WEEK.SCHEDULED.SYSTEM.DAYS AS "3N.3N",
WEEK.PRIME.SYSTEM.DAYS  AS "3N.3N",
APPLICABLE.FLAG         AS "C",
DAY.OF.WEEK             AS "NN",
TOTAL.SYSTEM.DAYS       AS "4N.3N",
SCHEDULED.SYSTEM.DAYS   AS "4N.3N",
PRIME.SYSTEM.DAYS       AS "4N.3N",
PREVIOUS.SYSTEM.ID      SAME AS SYSTEM.ID,
PREVIOUS.DATE           SAME AS DATE.,
PREVIOUS.SCHEDULED.DATE SAME AS DATE.,
PREVIOUS.PRIME.DATE     SAME AS DATE.,
WEEKS                   AS "4N",
CURRENT.WEEK            AS "4N",
STARTING.REPORT.DATE    SAME AS DATE.,
MULTIPLE.WEEKS          AS "C" DEFAULT "N",
SITE.INPUT              AS "3C",
CLASS.INPUT             AS "3C",
CRASHES.TODAY           AS "6ZB",
TOTAL.CRASHES           AS "7Z",
TOTAL.SCHEDULED.CRASHES AS "7Z",
TOTAL.PRIME.CRASHES     AS "7Z",
TOTAL.ADJUSTED.CRASHES  AS "7Z",
TOTAL.UP.TIME           AS "6ZN.5N",
TOTAL.SCHEDULED.UP.TIME AS "6ZN.5N",
TOTAL.PRIME.UP.TIME     AS "6ZN.5N",
TOTAL.ADJUSTED.UP.TIME  AS "6ZN.5N",
TOTAL.TIME              AS "6ZN.5N",
TOTAL.SCHEDULED.TIME    AS "6ZN.5N",
TOTAL.PRIME.TIME        AS "6ZN.5N",
WEEK.SCHEDULED.TS       AS "6ZN",
SCHEDULED.TS            AS "6ZN",
WEEK.TOTAL              AS "6ZN",
DD                      AS DATE "    WWW",
DW                      AS DATE "  ZM/DD",
PRIME.CRASH.FLAG        AS "N",
SCHEDULED.CRASH.FLAG    AS "N",
CRASH.FLAG              AS "N",
REPORT.DATE             SAME AS DATE.,
CURRENT.DATE            SAME AS DATE.,
ZERO                    AS "N",
ELAPSED.TIME            AS "6ZN",

UN.SCHEDULED.TIME       AS "6ZN",
SCHEDULED.TIME          AS "6ZN",
NON.PRIME.TIME          AS "6ZN",
PRIME.TIME              AS "6ZN",

ALL.CRASHES             AS "2ZN",
SCHEDULED.CRASHES       AS "2ZN",
PRIME.CRASHES           AS "2ZN",
ADJUSTED.CRASHES        AS "2ZN"

CONTROLS FOR RELATIONS CONCURRENT UPDATE

REPORT TO OUTPUT.FILE PAGE.SIZE 1

RELATION TOTALS. IS
KEY     SITE.ID,
        CLASS.ID,
        WEEKS,
        CODE.ID,
        DATE.
DATA    UN.SCHEDULED.TIME,
        SCHEDULED.TIME,
        NON.PRIME.TIME,
        PRIME.TIME

RELATION GROUP.TOTALS. IS
KEY     SITE.ID,
        CLASS.ID,
        WEEKS,
        DATE.,
        GROUP.ID
DATA    UN.SCHEDULED.TIME,
        SCHEDULED.TIME,
        NON.PRIME.TIME,
        PRIME.TIME

RELATION SCHEDULED.TS. IS
KEY     SITE.ID,
        CLASS.ID,
        WEEKS,
        CURRENT.DATE
DATA    SCHEDULED.TS

RELATION CRASH. IS
KEY     SITE.ID,
        CLASS.ID,
        WEEKS,
        DATE.,
        CODE.ID
DATA    ALL.CRASHES,
        SCHEDULED.CRASHES,
        PRIME.CRASHES,
        ADJUSTED.CRASHES

RELATION SYSTEM.DAYS. IS
KEY     SITE.ID,
        CLASS.ID,
        WEEKS,
        DAY.OF.WEEK
DATA    TOTAL.SYSTEM.DAYS,
        SCHEDULED.SYSTEM.DAYS,
        PRIME.SYSTEM.DAYS

GET.DATE.OF.REPORT:
BEGIN
TYPE "DATE OF REPORT: " ACCEPT REPORT.DATE
IF REPORT.DATE>TODAY THEN REPEAT GET.DATE.OF.REPORT
END %GET.DATE.OF.REPORT%

LET STARTING.REPORT.DATE=REPORT.DATE

TYPE "WEEKS: " ACCEPT WEEKS
LET NUMBER.OF.WEEKS=WEEKS

   GET.SITE:
   BEGIN
   TYPE "SITE: " ACCEPT SITE.INPUT
   IF SITE.INPUT="ALL" THEN FINISH GET.SITE
   FOR EACH SITE. IF ABBREV=SITE.INPUT THEN FINISH GET.SITE
   TYPE "NO SUCH SITE",@CR
   REPEAT GET.SITE
 END %GET.SITE%

 GET.CLASS:
 BEGIN
 TYPE "CLASS: " ACCEPT CLASS.INPUT
 IF CLASS.INPUT="ALL" THEN FINISH GET.CLASS
 FOR EACH CLASS. IF ABBREV=CLASS.INPUT THEN FINISH GET.CLASS
 TYPE "NO SUCH CLASS",@CR
 REPEAT GET.CLASS
 END %GET.CLASS%

GET.OUTPUT.FILE.NAME:
BEGIN
TYPE "OUTPUT TO: " ACCEPT OUTPUT.FILE

IF OUTPUT.FILE="TER" OR "TERM" OR "TERMI" OR "TERMIN" OR "TERMINA"
   OR "TERMINAL" THEN FINISH GET.OUTPUT.FILE.NAME
IF USER.NAME NE "MURMAINT" THEN LET REPORT.DIRECTORY = "("+USER.NAME+")"
IF SUBSTR(OUTPUT.FILE,1,1)="(" AS "C" THEN
   BEGIN
   TYPE @CR,"REPORTS ARE FORCED INTO USER NAME ",REPORT.DIRECTORY
   TYPE @CR,"PLEASE TYPE IN A FILE NAME ONLY",@CR,@CR
   REPEAT GET.OUTPUT.FILE.NAME
   END
  ELSE
   BEGIN
   LET OUTPUT.FILE=REPORT.DIRECTORY+OUTPUT.FILE
   TYPE "REPORT WILL BE ON FILE ",OUTPUT.FILE,@CR
   END
END %GET.OUTPUT.FILE.NAME%


LET STARTING.DATE=REPORT.DATE
LET ENDING.DATE=STARTING.DATE+(WEEKS*7)-1

WRITE.REPORT:
WRITE REPORT BEGIN %OPEN UP OUTPUT FILE%

LET DD,DW=REPORT.DATE
 FOR EACH TIME.
     THIS.TIME.RECORD:
     BEGIN

     IF (DATE.<STARTING.DATE OR >ENDING.DATE) THEN FINISH THIS.TIME.RECORD
     IF SITE.INPUT="ALL" THEN NOTHING ELSE
        IF SITE.INPUT#ABBREV(SITE. VIA ID=SITE.ID) THEN
           FINISH THIS.TIME.RECORD

     IF CLASS.INPUT="ALL" THEN NOTHING ELSE
        IF CLASS.INPUT#ABBREV(CLASS. VIA ID=CLASS.ID) THEN
           FINISH THIS.TIME.RECORD

     LET WEEKS=(ENDING.DATE-DATE.)/7 %GETS TRUNCATED TO INTEGER PART%

     IF SYSTEM.ID#PREVIOUS.SYSTEM.ID THEN
        BEGIN
        LET PREVIOUS.SYSTEM.ID=SYSTEM.ID
        IF APPLICABLE(SYSTEM. VIA ID=SYSTEM.ID)="N" THEN
           BEGIN
           LET APPLICABLE.FLAG="N"
           FINISH THIS.TIME.RECORD
           END
          ELSE
              LET APPLICABLE.FLAG="Y"
        LET PREVIOUS.DATE,PREVIOUS.SCHEDULED.DATE,PREVIOUS.PRIME.DATE=
            REPORT.DATE-1
        LET CRASH.FLAG,SCHEDULED.CRASH.FLAG,PRIME.CRASH.FLAG=0
        END

     IF APPLICABLE.FLAG="N" THEN FINISH THIS.TIME.RECORD
     IF PREVIOUS.DATE#DATE. OR
     (SCHED.FLAG=1 AND PREVIOUS.SCHEDULED.DATE#DATE.) OR
     (PRIME.FLAG=1 AND PREVIOUS.PRIME.DATE#DATE.) THEN
        BEGIN
        SELECT SYSTEM.DAYS. VIA (SITE.ID,CLASS.ID,WEEKS,
               DAY.OF.WEEK=((DATE. AS DATE "DD") AS "CC") AS "NN")
           THEN
               BEGIN
              IF PREVIOUS.DATE#DATE. THEN
                 BEGIN
                 LET PREVIOUS.DATE=DATE.
                 ADD 1 TO TOTAL.SYSTEM.DAYS
                 END
             IF SCHED.FLAG=1 AND PREVIOUS.SCHEDULED.DATE#DATE. THEN
                BEGIN
                ADD 1 TO SCHEDULED.SYSTEM.DAYS
                LET PREVIOUS.SCHEDULED.DATE=DATE.
                END
             IF PRIME.FLAG=1 AND PREVIOUS.PRIME.DATE#DATE. THEN
                BEGIN
                ADD 1 TO PRIME.SYSTEM.DAYS
                LET PREVIOUS.PRIME.DATE=DATE.
                END
             ALTER SYSTEM.DAYS.
             END
           ELSE
               BEGIN
               LET DAY.OF.WEEK=((DATE. AS DATE "DD") AS "CC") AS "NN"
               LET TOTAL.SYSTEM.DAYS=1
               LET SCHEDULED.SYSTEM.DAYS,PRIME.SYSTEM.DAYS=0
               LET PREVIOUS.DATE=DATE.
               IF SCHED.FLAG=1 THEN
                  BEGIN
                  LET SCHEDULED.SYSTEM.DAYS=1
                  LET PREVIOUS.SCHEDULED.DATE=DATE.
                  END
               IF PRIME.FLAG=1 THEN
                  BEGIN
                  LET PRIME.SYSTEM.DAYS=1
                  LET PREVIOUS.PRIME.DATE=DATE.
                  END
               INSERT INTO SYSTEM.DAYS.
               END %IF NO SELECTION%
             END %IF SELECTION NECESSARY%

     LET ELAPSED.TIME=STOP.TIME-START.TIME
     SELECT.TOTALS:
        BEGIN
         SELECT TOTALS. VIA KEY
                THEN
                    BEGIN
                    CONDITIONAL ON SCHED.FLAG
                                BEGIN
                                1: ADD ELAPSED.TIME TO SCHEDULED.TIME
                                2: ADD ELAPSED.TIME TO UN.SCHEDULED.TIME
                                END
                    CONDITIONAL ON PRIME.FLAG
                                BEGIN
                                1: ADD ELAPSED.TIME TO PRIME.TIME
                                2: ADD ELAPSED.TIME TO NON.PRIME.TIME
                                END
                    ALTER TOTALS.
                    END
               ELSE
                   BEGIN
                   INSERT INTO TOTALS.
                          USING (ZERO FOR UN.SCHEDULED.TIME,
                                 ZERO FOR SCHEDULED.TIME,
                                 ZERO FOR NON.PRIME.TIME,
                                 ZERO FOR PRIME.TIME)
                   REPEAT SELECT.TOTALS
                   END
        END %SELECT.TOTALS%
     SELECT.CRASH:
        BEGIN
        SELECT CRASH. VIA KEY
               THEN
                   BEGIN
                   IF CRASH.FLAG=1 AND TYPE.ABBREV(CODE. VIA ID=CODE.ID)="LST"
                      THEN ADD 1 TO ALL.CRASHES
                   IF TYPE.ABBREV(CODE. VIA ID=CODE.ID)="UP"
                      THEN LET CRASH.FLAG=1
                      ELSE LET CRASH.FLAG=0
                   CONDITIONAL ON SCHED.FLAG
                       BEGIN
                       1: CONDITIONAL ON TYPE.ABBREV(CODE. VIA ID=CODE.ID)
                              BEGIN
                               "UP": LET SCHEDULED.CRASH.FLAG=1
                              "SDN": LET SCHEDULED.CRASH.FLAG=0
                              "LST": BEGIN
                                     IF SCHEDULED.CRASH.FLAG=1 THEN
                                        BEGIN
                                        ADD 1 TO SCHEDULED.CRASHES
                                        IF EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="N"
                                           THEN ADD 1 TO ADJUSTED.CRASHES
                                           END
                                     LET SCHEDULED.CRASH.FLAG=0
                                     END
                              END
                       2: LET SCHEDULED.CRASH.FLAG=0
                       END %OF CONDITIONAL ON SCHED.FLAG%
                   CONDITIONAL ON PRIME.FLAG
                       BEGIN
                       1: BEGIN
                          CONDITIONAL ON TYPE.ABBREV(CODE. VIA ID=CODE.ID)
                              BEGIN
                               "UP": LET PRIME.CRASH.FLAG=1
                              "SDN": LET PRIME.CRASH.FLAG=0
                              "LST": BEGIN
                                     IF PRIME.CRASH.FLAG=1 THEN ADD 1 TO PRIME.CRASHES
                                     LET PRIME.CRASH.FLAG=0
                                     END
                              END
                          END
                       2: LET PRIME.CRASH.FLAG=0
                       END
                    ALTER CRASH.
                    END
               ELSE
                   BEGIN
                   INSERT INTO CRASH.
                          USING (ZERO FOR ALL.CRASHES,
                                 ZERO FOR SCHEDULED.CRASHES,
                                 ZERO FOR PRIME.CRASHES,
                                 ZERO FOR ADJUSTED.CRASHES)
                   REPEAT SELECT.CRASH
                   END
        END %SELECT.CRASH%
     END %THIS.TIME.RECORD%

%PUT CODE TOTALS INTO GROUP TOTALS%

FOR EACH TOTALS.
 SELECT GROUP.TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS,DATE.,GROUP.ID=GROUP.ID(CODE. VIA ID=CODE.ID)) THEN
        BEGIN
        ADD UN.SCHEDULED.TIME OF TOTALS. TO UN.SCHEDULED.TIME OF GROUP.TOTALS.
        ADD SCHEDULED.TIME OF TOTALS. TO SCHEDULED.TIME OF GROUP.TOTALS.
        ADD NON.PRIME.TIME OF TOTALS. TO NON.PRIME.TIME OF GROUP.TOTALS.
        ADD PRIME.TIME OF TOTALS. TO PRIME.TIME OF GROUP.TOTALS.
        ALTER GROUP.TOTALS.
        END
     ELSE INSERT INTO GROUP.TOTALS. USING (GROUP.ID(CODE. VIA ID=CODE.ID) FOR GROUP.ID)

%START REPORT OUTPUT WITH WHILE WEEKS,SITE,CLASS LOOP%

LET WEEKS=NUMBER.OF.WEEKS
LET CURRENT.WEEK=0
WHILE WEEKS>0 DO
      BEGIN
      SUBTRACT 1 FROM WEEKS
      LET REPORT.DATE=STARTING.REPORT.DATE+(CURRENT.WEEK*7)
      IF REPORT.DATE>TODAY THEN FINISH WRITE.REPORT
      ADD 1 TO CURRENT.WEEK

FOR EACH SITE.
    SITE.OUTPUT:
    BEGIN
       IF SITE.INPUT="ALL" THEN NOTHING ELSE
       IF SITE.INPUT=ABBREV OF SITE. THEN NOTHING ELSE
       FINISH SITE.OUTPUT

      LET SITE.ID=ID OF SITE.

FOR EACH CLASS.
    CLASS.OUTPUT:
    BEGIN
      IF CLASS.INPUT="ALL" THEN NOTHING ELSE
      IF CLASS.INPUT=ABBREV OF CLASS. THEN NOTHING ELSE
      FINISH CLASS.OUTPUT

%IF THIS SITE/CLASS HAS NO TIME THEN SKIP IT%

    CHECK.FOR.TIME.AGAIN:
    BEGIN
     FOR EACH TIME. WHERE (DATE.>=REPORT.DATE AND <=REPORT.DATE+6) AND
         SITE.ID=ID OF SITE. AND CLASS.ID=ID OF CLASS. FINISH CHECK.FOR.TIME.AGAIN
    FINISH CLASS.OUTPUT
    END %CHECK.FOR.TIME.AGAIN%
      LET CLASS.ID=ID OF CLASS.

%OUTPUT REPORT HEADING%


IF MULTIPLE.WEEKS="Y" THEN PRINT @FF
LET MULTIPLE.WEEKS="Y"
PRINT @CR,@TAB TO 22,
      TRIM(TITLE OF SITE.)," ",
      TRIM(TITLE OF CLASS.),
      " FOR WEEK OF ",REPORT.DATE AS DATE "MMM ZD, YYYY",@CR
PRINT @TAB TO 19,DD,DD+1,DD+2,DD+3,DD+4,DD+5,DD+6,"  WEEKLY",@CR
PRINT @TAB TO 19,DW,DW+1,DW+2,DW+3,DW+4,DW+5,DW+6,"  TOTALS",@CR

%OUTPUT INDIVIDUAL CODE TOTALS%

 FOR EACH GROUP. WHERE TYPE.ABBREV="UP" OR "SDN" OR "LST" SORTED ON POS
 FOR EACH CODE. WHERE CLASS.ID=ID OF CLASS.
  AND GROUP.ID=ID OF GROUP. AND NOT (END.DATE<REPORT.DATE OR START.DATE>REPORT.DATE+6)
  SORTED ON CODE
  BEGIN
  PRINT CODE AS "ZN"
  IF EXCEPTION.FLAG="N" THEN PRINT " " ELSE PRINT "*"
  PRINT TITLE
  LET WEEK.TOTAL=0
LET CURRENT.DATE=REPORT.DATE-1
WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
 BEGIN
 ADD 1 TO CURRENT.DATE
  SELECT TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS,CODE.ID=ID OF CODE.,DATE.=CURRENT.DATE) THEN
   BEGIN
   LET ELAPSED.TIME=UN.SCHEDULED.TIME+SCHEDULED.TIME
   ADD ELAPSED.TIME TO WEEK.TOTAL
   PRINT (ELAPSED.TIME/60) AS "3ZB",":",
         ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
   END
   ELSE PRINT @TAB 7
  END
 IF WEEK.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (WEEK.TOTAL/60) AS "4ZB",":",
          ((WEEK.TOTAL-(WEEK.TOTAL/60)*60)) AS "NN",@CR
  END %SINGLE CODE OUTPUT%

%OUTPUT DAILY TOTALS%

PRINT @TAB TO 4,"DAILY TOTALS" AS "15C"
LET WEEK.TOTAL=0
LET TOTAL.TIME,TOTAL.SCHEDULED.TIME,TOTAL.PRIME.TIME=0
LET CURRENT.DATE=REPORT.DATE-1

WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
 BEGIN
 ADD 1 TO CURRENT.DATE
 LET ELAPSED.TIME=0
 FOR EACH GROUP.TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS,DATE.=CURRENT.DATE)
            BEGIN
            ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO ELAPSED.TIME
            ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO TOTAL.TIME
            ADD SCHEDULED.TIME TO TOTAL.SCHEDULED.TIME
            ADD PRIME.TIME TO TOTAL.PRIME.TIME
            END
 IF ELAPSED.TIME=0 THEN PRINT @TAB 7 ELSE
    PRINT (ELAPSED.TIME/60) AS "3ZB",":",
          ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
 ADD ELAPSED.TIME TO WEEK.TOTAL
 END %WHILE STATMENT FOR DAILY TOTALS OUTPUT%

 IF WEEK.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (WEEK.TOTAL/60) AS "4ZB",":",
          ((WEEK.TOTAL-(WEEK.TOTAL/60)*60)) AS "NN",@CR

%END DAILY TOTALS OUTPUT%

%OUTPUT SCHEDULED TIME GROUP TOTALS%

PRINT @CR,@TAB TO 28,"SCHEDULED TIME GROUP TOTALS",@CR
FOR EACH GROUP. WHERE TYPE.ABBREV="UP" OR "SDN" OR "LST" SORTED ON POS
 LIST.GROUP:
 BEGIN
 LIST.GROUP.CHECK:
      BEGIN
      FOR EACH CODE. WHERE GROUP.ID=ID OF GROUP. AND
          CLASS.ID=ID OF CLASS. AND
          NOT (START.DATE>REPORT.DATE+6 OR END.DATE<REPORT.DATE)
          FINISH LIST.GROUP.CHECK
      FINISH LIST.GROUP %DON'T LIST THIS GROUP%
 END %LIST.GROUP.CHECK%

 PRINT @TAB TO 4,TITLE
 LET WEEK.TOTAL=0
 LET CURRENT.DATE=REPORT.DATE-1
 WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
  BEGIN
  ADD 1 TO CURRENT.DATE
  LET ELAPSED.TIME=0
   SELECT GROUP.TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS,DATE.=CURRENT.DATE,GROUP.ID=ID OF GROUP.) THEN
             ADD SCHEDULED.TIME TO ELAPSED.TIME
 IF ELAPSED.TIME=0 THEN PRINT @TAB 7 ELSE
    BEGIN
    PRINT (ELAPSED.TIME/60) AS "3ZB",":",
        ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
    SELECT SCHEDULED.TS. VIA KEY THEN
           BEGIN
           ADD ELAPSED.TIME TO SCHEDULED.TS
           ALTER SCHEDULED.TS.
           END
         ELSE INSERT INTO SCHEDULED.TS. USING (ELAPSED.TIME FOR SCHEDULED.TS)
   END %IF ELAPSED.TIME#0%
  ADD ELAPSED.TIME TO WEEK.TOTAL
  END %WHILE%
 IF WEEK.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (WEEK.TOTAL/60) AS "4ZB",":",
          ((WEEK.TOTAL-(WEEK.TOTAL/60)*60)) AS "NN",@CR
 END %LIST GROUP%

%OUTPUT SCHEDULED TS TOTALS%

PRINT @TAB TO 4,"SCHEDULED TS" AS "15C"
LET CURRENT.DATE=REPORT.DATE-1
LET WEEK.TOTAL=0
WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
 BEGIN
 ADD 1 TO CURRENT.DATE
 SELECT SCHEDULED.TS. VIA KEY THEN
  BEGIN
  PRINT (SCHEDULED.TS/60) AS "3ZB",":",
        ((SCHEDULED.TS-(SCHEDULED.TS/60)*60)) AS "NN"
  ADD SCHEDULED.TS TO WEEK.TOTAL
  END %OF SELECTION%
  ELSE PRINT @TAB 7
END %WHILE%
IF WEEK.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (WEEK.TOTAL/60) AS "4ZB",":",
         ((WEEK.TOTAL-(WEEK.TOTAL/60)*60)) AS "NN",@CR

%OUTPUT AVAILABILITY FIGURES%

LET TOTAL.UP.TIME=0
LET TOTAL.SCHEDULED.UP.TIME=0
LET TOTAL.PRIME.UP.TIME=0
FOR EACH GROUP.TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS)
    IF TYPE.ABBREV(GROUP. VIA ID=GROUP.ID)="UP" THEN
    BEGIN
    ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO TOTAL.UP.TIME
    ADD SCHEDULED.TIME TO TOTAL.SCHEDULED.UP.TIME
    ADD PRIME.TIME TO TOTAL.PRIME.UP.TIME
    END

LET ELAPSED.TIME=0
FOR EACH TOTALS. VIA (SITE.ID,CLASS.ID,WEEKS) IF EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="Y" THEN
    ADD SCHEDULED.TIME TO ELAPSED.TIME

LET TOTAL.ADJUSTED.UP.TIME=TOTAL.SCHEDULED.UP.TIME+ELAPSED.TIME

PRINT @TAB TO 4,"AVAILABILITY" AS "16C"
PRINT " ","24 HR"
IF TOTAL.TIME=0 THEN PRINT @TAB 8
   ELSE PRINT (TOTAL.UP.TIME/TOTAL.TIME*100) AS "4Z.NN","%"
PRINT " ","PRIME"
IF TOTAL.PRIME.TIME=0 THEN PRINT @TAB 8
   ELSE PRINT (TOTAL.PRIME.UP.TIME/TOTAL.PRIME.TIME*100) AS "4Z.NN","%"
PRINT " ","SCHED"
IF TOTAL.SCHEDULED.TIME=0 THEN PRINT @TAB 8
   ELSE PRINT (TOTAL.SCHEDULED.UP.TIME/TOTAL.SCHEDULED.TIME*100) AS "4Z.NN","%"
PRINT " ","*ADJ'D"
IF TOTAL.SCHEDULED.TIME=0 THEN PRINT @CR
   ELSE PRINT (TOTAL.ADJUSTED.UP/TOTAL.SCHEDULED.TIME*100) AS "4Z.NN","%",@CR

%OUTPUT SCHEDULED TIME CRASH SUMMARY%

PRINT @CR,@TAB TO 28,"SCHEDULED TIME CRASH SUMMARY",@CR

FOR EACH GROUP. WHERE TYPE.ABBREV="LST" SORTED ON POS
 FOR EACH CODE. WHERE (NOT (START.DATE>REPORT.DATE+6 OR END.DATE<REPORT.DATE))
     AND CLASS.ID=ID OF CLASS.
     AND GROUP.ID=ID OF GROUP. SORTED ON CODE
  BEGIN
  PRINT @TAB TO 3
  IF EXCEPTION.FLAG="Y" THEN PRINT "*" ELSE PRINT " "
  PRINT TITLE OF CODE.
  LET WEEK.TOTAL=0
  LET CURRENT.DATE=REPORT.DATE-1
  WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
   BEGIN
   ADD 1 TO CURRENT.DATE
   SELECT CRASH. VIA (SITE.ID,CLASS.ID,WEEKS,DATE.=CURRENT.DATE,CODE.ID=ID OF CODE.) THEN
    BEGIN
    ADD SCHEDULED.CRASHES TO WEEK.TOTAL
    PRINT SCHEDULED.CRASHES AS "6ZB"
    END
   ELSE PRINT @TAB 7
  END %WHILE%
 
IF WEEK.TOTAL=0 THEN PRINT @CR ELSE
   PRINT WEEK.TOTAL AS "8Z",@CR

END %EACH GROUP OF CRASH CODES%

%OUTPUT DAILY TOTALS FOR SCHEDULED TIME CRASHES%

LET TOTAL.CRASHES=0
LET WEEK.TOTAL=0
LET TOTAL.SCHEDULED.CRASHES=0
LET TOTAL.PRIME.CRASHES=0
LET TOTAL.ADJUSTED.CRASHES=0

PRINT @TAB TO 4,"DAILY TOTALS" AS "15C"
LET CURRENT.DATE=REPORT.DATE-1
WHILE CURRENT.DATE>=REPORT.DATE-1 AND <REPORT.DATE+6 DO
 BEGIN
 ADD 1 TO CURRENT.DATE
 LET CRASHES.TODAY=0
 FOR EACH CRASH. VIA (SITE.ID,CLASS.ID,WEEKS,DATE.=CURRENT.DATE)
     BEGIN
     ADD ALL.CRASHES TO TOTAL.CRASHES
     ADD SCHEDULED.CRASHES TO TOTAL.SCHEDULED.CRASHES
     ADD PRIME.CRASHES TO TOTAL.PRIME.CRASHES
     ADD ADJUSTED.CRASHES TO TOTAL.ADJUSTED.CRASHES
     ADD SCHEDULED.CRASHES TO CRASHES.TODAY
     END
  ADD CRASHES.TODAY TO WEEK.TOTAL
  PRINT CRASHES.TODAY

 END %WHILE%
 PRINT WEEK.TOTAL AS "7ZB",@CR

%COMPUTE AVERAGE NUMBER OF SYSTEMS PER DAY%

LET WEEK.TOTAL.SYSTEM.DAYS,WEEK.SCHEDULED.SYSTEM.DAYS,WEEK.PRIME.SYSTEM.DAYS=0
LET WEEK.TOTAL.DAYS,WEEK.SCHEDULED.DAYS,WEEK.PRIME.DAYS=0
FOR EACH SYSTEM.DAYS. VIA (SITE.ID,CLASS.ID)
    BEGIN
    IF TOTAL.SYSTEM.DAYS>0 THEN ADD 1 TO WEEK.TOTAL.DAYS
    ADD TOTAL.SYSTEM.DAYS TO WEEK.TOTAL.SYSTEM.DAYS
    IF SCHEDULED.SYSTEM.DAYS>0 THEN ADD 1 TO WEEK.SCHEDULED.DAYS
    ADD SCHEDULED.SYSTEM.DAYS TO WEEK.SCHEDULED.SYSTEM.DAYS
    IF PRIME.SYSTEM.DAYS>0 THEN ADD 1 TO WEEK.PRIME.DAYS
    ADD PRIME.SYSTEM.DAYS TO WEEK.PRIME.SYSTEM.DAYS
    END
%OUTPUT CRASHES PER MACHINE AND TOTAL CRASHES%

PRINT @TAB TO 4,"CPS & TOTALS" AS "15C"

PRINT " 24H"
IF WEEK.TOTAL.SYSTEM.DAYS=0 THEN PRINT @TAB 10 ELSE
   PRINT (TOTAL.CRASHES*WEEK.TOTAL.DAYS/WEEK.TOTAL.SYSTEM.DAYS) AS "2ZB.BB",
        TOTAL.CRASHES AS "3ZB"

PRINT " PRI"
IF WEEK.PRIME.SYSTEM.DAYS=0 THEN PRINT @TAB 10 ELSE
   PRINT (TOTAL.PRIME.CRASHES*WEEK.PRIME.DAYS/WEEK.PRIME.SYSTEM.DAYS) AS "2ZB.BB",
         TOTAL.PRIME.CRASHES AS "3ZB"

PRINT " SCH"
IF WEEK.SCHEDULED.SYSTEM.DAYS=0 THEN PRINT @TAB 10 ELSE
PRINT (TOTAL.SCHEDULED.CRASHES*WEEK.SCHEDULED.DAYS/WEEK.SCHEDULED.SYSTEM.DAYS) AS "2ZB.BB",
      TOTAL.SCHEDULED.CRASHES AS "3ZB"

PRINT " *ADJ"
IF WEEK.SCHEDULED.SYSTEM.DAYS=0 THEN PRINT @CR ELSE
PRINT (TOTAL.ADJUSTED.CRASHES*WEEK.SCHEDULED.DAYS/WEEK.SCHEDULED.SYSTEM.DAYS) AS "2ZB.BB",
      TOTAL.ADJUSTED.CRASHES AS "3ZB",@CR

%OUTPUT NUMBER OF SYSTEMS%

PRINT @TAB TO 4,"AVG. NO.SYS/DAY" AS "15C"
PRINT " 24 HR: "
IF WEEK.TOTAL.DAYS=0 THEN PRINT @TAB 6 ELSE
   PRINT (WEEK.TOTAL.SYSTEM.DAYS/WEEK.TOTAL.DAYS) AS "2ZB.BB"

PRINT " PRIME: "
IF WEEK.PRIME.DAYS=0 THEN PRINT @TAB 6 ELSE
   PRINT (WEEK.PRIME.SYSTEM.DAYS/WEEK.PRIME.DAYS) AS "2ZB.BB"

PRINT " SCHED: "
IF WEEK.SCHEDULED.DAYS=0 THEN PRINT @TAB 6 ELSE
   PRINT (WEEK.SCHEDULED.SYSTEM.DAYS/WEEK.SCHEDULED.DAYS) AS "2ZB.BB"

PRINT " *ADJ'D: "
IF WEEK.SCHEDULED.DAYS=0 THEN PRINT @CR ELSE
   PRINT (WEEK.SCHEDULED.SYSTEM.DAYS/WEEK.SCHEDULED.DAYS) AS "2ZB.BB",@CR

END %CLASS.OUTPUT%
END %SITE.OUTPUT%

END %WHILE WEEK>0%

END %REPORT%
END
    JT­