PROCEDURE INCENTIVE
BEGIN

FIELDS

ACTUAL.SYSTEM.DAYS      AS "7ZN.5N",
FACTORED.SYSTEM.DAYS    AS "7ZN.5N",
ACTUAL.UP.TIME          AS "7ZN.5N",
ACTUAL.ADJUSTED.UP.TIME AS "7ZN.5N",
ACTUAL.TOTAL.TIME       AS "7ZN.5N",
ACTUAL.CRASHES          AS "7ZN.5N",
ACTUAL.ADJUSTED.CRASHES AS "7ZN.5N",
FACTORED.UP.TIME        AS "7ZN.5N",
FACTORED.ADJUSTED.UP.TIME AS "7ZN.5N",
FACTORED.TOTAL.TIME     AS "7ZN.5N",
FACTORED.CRASHES        AS "7ZN.5N",
FACTORED.ADJUSTED.CRASHES AS "7ZN.5N",
CURRENT.DATE            SAME AS DATE.,
CLASS.ADJUSTED.UP.TIME  AS "7ZN.5N",
CLASS.CRASHES           AS "7ZN.5N",
CLASS.ADJUSTED.CRASHES  AS "7ZN.5N",
CLASS.UP.TIME           AS "7ZN.5N",
CLASS.ADJUSTED.TIME     AS "7ZN.5N",
CLASS.TOTAL.TIME        AS "7ZN.5N",
SITE.CRASHES            AS "7ZN.5N",
SITE.ADJUSTED.CRASHES   AS "7ZN.5N",
SITE.UP.TIME            AS "7ZN.5N",
SITE.ADJUSTED.UP.TIME   AS "7ZN.5N",
SITE.TOTAL.TIME         AS "7ZN.5N",
MONTHS                  AS "ZN",
NUMBER.OF.DAYS          AS "4N.3N",
SYSTEM.DAYS             AS "8N.3N",
SITE.TITLE.SWITCH       AS "C" DEFAULT "Y",
CRASH.FLAG              AS "N" DEFAULT 0,
%OUTPUT.FILE             AS "23C", ed gauci 12/17%
CLASS.INPUT             SAME AS ABBREV,
SITE.INPUT              SAME AS ABBREV,
ELAPSED.TIME            SAME AS START.TIME,
PREVIOUS.SYSTEM.ID      SAME AS SYSTEM.ID,
PREVIOUS.DATE           SAME AS DATE.,
ZERO                    AS "N" DEFAULT 0,
UP.TIME                 AS "7ZN.5N",
ADJUSTED.UP.TIME        AS "7ZN.5N",
TOTAL.TIME              AS "7ZN.5N",
CRASHES                 AS "7ZN.5N",
ADJUSTED.CRASHES        AS "7ZN.5N",
SYSTEMS                 AS "7ZN.5N",
STARTING.DATE           SAME AS DATE.,
ENDING.DATE             SAME AS DATE.,
FOO.CLASS.ADJUSTED.UP.TIME             AS '7ZN.5N',
FOO.CLASS.ADJUSTED.CRASHES             AS '7ZN.5N',
FOO.CLASS.CRASHES                      AS '7ZN.5N',
FOO.CLASS.UP.TIME                      AS '7ZN.5N',
FOO.CLASS.TOTAL.TIME                   AS '7ZN.5N',
FOO.FLAG                               AS 'N',
FOO.NUMBER.OF.DAYS                     AS '4N.3N',
FOO.SYSTEM.DAYS                        AS '8N.3N',
FNA.NUMBER.OF.DAYS                     AS '4N.3N',
FNC.NUMBER.OF.DAYS                     AS '4N.3N'

CONTROLS FOR RELATIONS CONCURRENT UPDATE

RELATION AVAILABILITY. IS
KEY     SITE.ID,
        CLASS.ID
DATA    UP.TIME,
        ADJUSTED.UP.TIME,
        TOTAL.TIME

RELATION CRASH. IS
KEY     SITE.ID,
        CLASS.ID
DATA    CRASHES,
        ADJUSTED.CRASHES

RELATION SYSTEM.DAYS. IS
KEY     SITE.ID,
        CLASS.ID,
        DATE.
DATA    SYSTEMS


REPORT TO OUTPUT.FILE PAGE.SIZE 66

TYPE "START,END DATES: " ACCEPT STARTING.DATE,ENDING.DATE

LET CURRENT.DATE=STARTING.DATE
LET MONTHS=1
CALCULATE.MONTHS:
BEGIN
IF CURRENT.DATE=ENDING.DATE THEN FINISH CALCULATE.MONTHS
ADD 1 TO CURRENT.DATE
IF (CURRENT.DATE AS DATE "MM") AS "CC" #
   ((CURRENT.DATE-1) AS DATE "MM") AS "CC"
   THEN ADD 1 TO MONTHS
REPEAT CALCULATE.MONTHS
END

   GET.SITE:
   BEGIN
   TYPE "SITE: " ACCEPT SITE.INPUT
   IF SITE.INPUT="ALL" THEN FINISH GET.SITE
   FOR EACH SITE. IF ABBREV=SITE.INPUT THEN FINISH GET.SITE
   TYPE "NO SUCH SITE",@CR
   REPEAT GET.SITE
 END %GET.SITE%

 GET.CLASS:
 BEGIN
 TYPE "CLASS: " ACCEPT CLASS.INPUT
 IF CLASS.INPUT="ALL" THEN FINISH GET.CLASS
 FOR EACH CLASS. IF ABBREV=CLASS.INPUT THEN FINISH GET.CLASS
 TYPE "NO SUCH CLASS",@CR
 REPEAT GET.CLASS
 END %GET.CLASS%

GET.OUTPUT.FILE.NAME:
BEGIN
TYPE "OUTPUT FILE: " ACCEPT OUTPUT.FILE
IF OUTPUT.FILE="TER" OR "TERM" OR "TERMI" OR "TERMIN" OR "TERMINA"
  OR "TERMINAL" THEN FINISH GET.OUTPUT.FILE.NAME
IF USER.NAME NE "MURMAINT" THEN LET REPORT.DIRECTORY = "("+USER.NAME+")"
IF SUBSTR(OUTPUT.FILE,1,1) = "(" AS "C" THEN
    BEGIN
    TYPE @CR,"OUTPUT IS FORCED INTO USERNAME ",REPORT.DIRECTORY
    TYPE @CR,"PLEASE TYPE IN A FILE NAME ONLY",@CR,@CR
    REPEAT GET.OUTPUT.FILE.NAME
    END
 ELSE
    BEGIN
    LET OUTPUT.FILE = REPORT.DIRECTORY+OUTPUT.FILE
    TYPE "REPORT WILL BE ON FILE ",OUTPUT.FILE,@CR
    END
END %GET.OUTPUT.FILE.NAME%

WRITE REPORT
BEGIN

FOR EACH SYSTEM. WHERE APPLICABLE="Y"
   AND NOT (STARTING.DATE>END.DATE OR ENDING.DATE<START.DATE)
 THIS.SYSTEM:
 BEGIN

 IF SITE.INPUT="ALL" THEN NOTHING ELSE
 IF SITE.INPUT=ABBREV(SITE. VIA ID=SITE.ID) THEN NOTHING ELSE
 FINISH THIS.SYSTEM

 IF CLASS.INPUT="ALL" THEN NOTHING ELSE
 IF CLASS.INPUT=ABBREV(CLASS. VIA ID=CLASS.ID) THEN NOTHING ELSE
 FINISH THIS.SYSTEM

LET CURRENT.DATE=STARTING.DATE-1
WHILE CURRENT.DATE<ENDING.DATE DO
  BEGIN
  ADD 1 TO CURRENT.DATE

 FOR EACH TIME. VIA (SYSTEM.ID=ID OF SYSTEM.,DATE.=CURRENT.DATE)
     THIS.TIME.RECORD:
     BEGIN

     IF SYSTEM.ID#PREVIOUS.SYSTEM.ID THEN
        BEGIN
        LET PREVIOUS.SYSTEM.ID=SYSTEM.ID
        LET PREVIOUS.DATE=STARTING.DATE-1
        LET CRASH.FLAG=0
        END


     IF SCHED.FLAG=1 AND PREVIOUS.DATE#DATE. THEN
        BEGIN
        SELECT SYSTEM.DAYS. VIA KEY
           THEN
             BEGIN
                ADD 1 TO SYSTEMS
                LET PREVIOUS.DATE=DATE.
             ALTER SYSTEM.DAYS.
             END
           ELSE
               BEGIN
                  LET SYSTEMS=1
                  LET PREVIOUS.DATE=DATE.
               INSERT INTO SYSTEM.DAYS.
               END
               END %IF NO SELECTION%

     LET ELAPSED.TIME=STOP.TIME-START.TIME

     SELECT.AVAILABILITY:
        BEGIN
        IF SCHED.FLAG=1 THEN
         SELECT AVAILABILITY. VIA KEY
                THEN
                    BEGIN
                    ADD ELAPSED.TIME TO TOTAL.TIME
                    IF EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="Y" THEN
                       ADD ELAPSED.TIME TO ADJUSTED.UP.TIME
                    IF TYPE.ABBREV(CODE. VIA ID=CODE.ID)="UP" THEN
                       ADD ELAPSED.TIME TO UP.TIME,ADJUSTED.UP.TIME
                    ALTER AVAILABILITY.
                    END
               ELSE
                   BEGIN
                   INSERT INTO AVAILABILITY. USING (ZERO FOR TOTAL.TIME)
                   REPEAT SELECT.AVAILABILITY
                   END
        END %SELECT.AVAILABILITY%

     SELECT.CRASH:
        BEGIN
        IF SCHED.FLAG=1 THEN
        SELECT CRASH. VIA KEY
               THEN
                   BEGIN
                   CONDITIONAL ON TYPE.ABBREV(CODE. VIA ID=CODE.ID)
                            BEGIN
                            "UP": LET CRASH.FLAG=1
                           "SDN": LET CRASH.FLAG=0
                           "LST": BEGIN
                                   IF CRASH.FLAG=1 THEN
                                    BEGIN
                                     ADD 1 TO CRASHES
                                     IF EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="N"
                                        THEN ADD 1 TO ADJUSTED.CRASHES
                                     END
                                    LET CRASH.FLAG=0
                                   END
                           END
                    ALTER CRASH.
                    END
               ELSE
                   BEGIN
                   INSERT INTO CRASH. USING
                          (ZERO FOR CRASHES,
                          ZERO FOR ADJUSTED.CRASHES)
                   REPEAT SELECT.CRASH
                   END
            ELSE %IF SCHED.FLAG=0% LET CRASH.FLAG=0
        END %SELECT.CRASH%

     END %FOR EACH TIME%
 END %WHILE%
 END %THIS.SYSTEM%

%REPORT OUTPUT%

%HEADING%

PRINT @CR,@TAB TO 26,"COMPUTER SYSTEM RELIABILITY",@CR,@TAB TO 24,
      "FROM ",STARTING.DATE AS DATE "MMM ZD,YYYY"," TO ",
      ENDING.DATE AS DATE "MMM ZD,YYYY",@CR,@CR
PRINT @TAB TO 17,"SYS   NUM   AVAIL   AVAIL   AVAIL  NUM   BY  W/O CR/SYS  CR/SYS",@CR
PRINT "COMPUTER SITE   TYP   SYS  W/FTPF BY/FTPF WO/FTPF CRSH FTPF FTPF W/FTPF WO/FTPF",@CR,@CR

%SITE-CLASS INFORMATION OUTPUT%

FOR EACH SITE.
    THIS.SITE:
    BEGIN

    CHECK.SITE:
    BEGIN
    FOR EACH SYSTEM.DAYS. VIA SITE.ID=ID OF SITE.
        FINISH CHECK.SITE
   FINISH THIS.SITE
   END %CHECK.SITE%

   LET SITE.TITLE.SWITCH="Y"

   FOR EACH CLASS.
       THIS.CLASS:
       BEGIN

       CHECK.CLASS:
       BEGIN
       FOR EACH SYSTEM.DAYS. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
           FINISH CHECK.CLASS
       FINISH THIS.CLASS
       END %CHECK.CLASS%

IF SITE.TITLE.SWITCH="Y" THEN PRINT TITLE OF SITE.
LET SITE.TITLE.SWITCH="N"

PRINT @TAB TO 17,ABBREV OF CLASS.

%OUTPUT AVERAGE NUMBER OF SYSTEMS PER DAY%

LET DATE.=STARTING.DATE
LET SYSTEM.DAYS=0
LET NUMBER.OF.DAYS=0
WHILE DATE.<=ENDING.DATE DO
      BEGIN
      SELECT SYSTEM.DAYS. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.,DATE.)
         THEN
            BEGIN
             ADD SYSTEMS TO SYSTEM.DAYS
             ADD 1 TO NUMBER.OF.DAYS
             END
      ADD 1 TO DATE.
      END
PRINT (SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"

%OUTPUT AVAILABILITY%

SELECT AVAILABILITY. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
       THEN BEGIN
            PRINT (UP.TIME/TOTAL.TIME*100) AS "4Z.NN","%"
            PRINT ((ADJUSTED.UP.TIME-UP.TIME)/TOTAL.TIME*100) AS "4Z.NN","%"
            PRINT (ADJUSTED.UP.TIME/TOTAL.TIME*100) AS "4Z.NN","%"
            END
       ELSE ABORT "*****PROGRAM ERROR*****"

%OUTPUT NUMBER OF CRASHES%

SELECT CRASH. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
       THEN
           BEGIN
           PRINT CRASHES AS "5Z"
           PRINT (CRASHES-ADJUSTED.CRASHES) AS "5Z"
           PRINT ADJUSTED.CRASHES AS "5Z"
           PRINT (CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "4Z.NN"
           PRINT (ADJUSTED.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "5Z.NN"
           END

PRINT @CR

END %THIS.CLASS%

%OUTPUT TOTALS FOR THIS SITE%

PRINT " " AS "15C",@TAB TO 17,"ALL"

%COMPUTE AVERAGE NUMBER OF SYSTEMS PER DAY FOR THIS SITE%

LET SYSTEM.DAYS=0
FOR EACH SYSTEM.DAYS. VIA SITE.ID=ID OF SITE. ADD SYSTEMS TO SYSTEM.DAYS

LET DATE.=STARTING.DATE
LET NUMBER.OF.DAYS=0
WHILE DATE.<=ENDING.DATE DO
      BEGIN
      THIS.DAY:
      FOR EACH SYSTEM.DAYS. VIA SITE.ID=ID OF SITE.,
          WHERE DATE.=DATE. OF SYSTEM.DAYS.
          BEGIN
          ADD 1 TO NUMBER.OF.DAYS
          FINISH THIS.DAY
          END
      ADD 1 TO DATE.
      END

PRINT (SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"

%OUTPUT AVAILABILITY FOR SITE%

LET SITE.UP.TIME=0
LET SITE.TOTAL.TIME=0
LET SITE.ADJUSTED.UP.TIME=0
FOR EACH AVAILABILITY. VIA SITE.ID=ID OF SITE.
    BEGIN
    ADD UP.TIME TO SITE.UP.TIME
    ADD TOTAL.TIME TO SITE.TOTAL.TIME
    ADD ADJUSTED.UP.TIME TO SITE.ADJUSTED.UP.TIME
    END

PRINT (SITE.UP.TIME/SITE.TOTAL.TIME*100) AS "4Z.NN","%"
PRINT ((SITE.ADJUSTED.UP.TIME-SITE.UP.TIME)/SITE.TOTAL.TIME*100) AS "4Z.NN","%"
PRINT (SITE.ADJUSTED.UP.TIME/SITE.TOTAL.TIME*100) AS "4Z.NN","%"

%OUTPUT NUMBER OF CRASHES%

LET SITE.CRASHES=0
LET SITE.ADJUSTED.CRASHES=0
FOR EACH CRASH. VIA SITE.ID=ID OF SITE.
    BEGIN
    ADD CRASHES TO SITE.CRASHES
    ADD ADJUSTED.CRASHES TO SITE.ADJUSTED.CRASHES
    END
PRINT SITE.CRASHES AS "5Z"
PRINT (SITE.CRASHES-SITE.ADJUSTED.CRASHES) AS "5Z"
PRINT SITE.ADJUSTED.CRASHES AS "5Z"
PRINT (SITE.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "4Z.NN"
PRINT (SITE.ADJUSTED.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "5Z.NN"
PRINT @CR

%OUTPUT FACTORED TOTALS FOR THIS SITE%

%
FOR EACH CLASS. WHERE ABBREV="PDP" OR "SRI" OR "370"
    BEGIN
    FOR EACH SYSTEM.DAYS. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
        BEGIN
        ADD SYSTEMS TO SYSTEM.DAYS
        IF ABBREV="370" THEN ADD SYSTEMS TO SYSTEM.DAYS
        END
    FOR EACH AVAILABILITY. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
        BEGIN
        ADD UP.TIME TO SITE.UP.TIME
        ADD ADJUSTED.UP.TIME TO SITE.ADJUSTED.UP.TIME
        ADD TOTAL.TIME TO SITE.TOTAL.TIME
        IF ABBREV="370" THEN
           BEGIN
           ADD UP.TIME TO SITE.UP.TIME
           ADD ADJUSTED.UP.TIME TO SITE.ADJUSTED.UP.TIME
           ADD TOTAL.TIME TO SITE.TOTAL.TIME
           END
        END
    FOR EACH CRASH. VIA (SITE.ID=ID OF SITE.,CLASS.ID=ID OF CLASS.)
        BEGIN
        ADD CRASHES TO SITE.CRASHES
        ADD ADJUSTED.CRASHES TO SITE.ADJUSTED.CRASHES
        IF ABBREV="370" THEN
           BEGIN
           ADD CRASHES TO SITE.CRASHES
           ADD ADJUSTED.CRASHES TO SITE.ADJUSTED.CRASHES
           END
        END
    END

PRINT "FACTORED TOTALS" AS "15C",@TAB TO 17,"ALL"
PRINT (SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"
PRINT (SITE.UP.TIME/SITE.TOTAL.TIME*100) AS "4Z.NN"," "
PRINT ((SITE.ADJUSTED.UP.TIME-SITE.UP.TIME)/SITE.TOTAL.TIME*100) AS "4Z.NN"," "
PRINT (SITE.ADJUSTED.UP.TIME/SITE.TOTAL.TIME*100) AS "4Z.NN"," "

PRINT SITE.CRASHES AS "5Z"
PRINT (SITE.CRASHES-SITE.ADJUSTED.CRASHES) AS "5Z"
PRINT SITE.ADJUSTED.CRASHES AS "5Z"
PRINT (SITE.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "4Z.NN"
PRINT (SITE.ADJUSTED.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "5Z.NN"
PRINT @CR
%
PRINT @CR

END %THIS.SITE%

%OUTPUT TOTALS FOR EACH CLASS%

PRINT " " AS "15C"

LET FOO.CLASS.ADJUSTED.CRASHES = 0
LET FOO.CLASS.ADJUSTED.UP.TIME = 0
LET FOO.CLASS.CRASHES = 0
LET FOO.CLASS.TOTAL.TIME = 0
LET FOO.CLASS.UP.TIME = 0
LET FOO.FLAG = 0
LET FOO.NUMBER.OF.DAYS = 0
LET FOO.SYSTEM.DAYS = 0

FOR EACH CLASS.
    THIS.CLASS.:
    BEGIN

    CHECK.CLASS.:
    BEGIN
    FOR EACH SYSTEM.DAYS. WHERE CLASS.ID=ID OF CLASS. FINISH CHECK.CLASS.
    FINISH THIS.CLASS.
    END %CHECK.CLASS.%

IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC')) AND FOO.FLAG = 0
  THEN PRINT @TAB TO 17, 'FNY'
  ELSE IF (ABBREV NE 'FNA') AND (ABBREV NE 'FNC')
         THEN PRINT @TAB TO 17, ABBREV OF CLASS.

LET CLASS.CRASHES=0
LET CLASS.ADJUSTED.CRASHES=0
LET CLASS.UP.TIME=0
LET CLASS.ADJUSTED.UP.TIME=0
LET CLASS.TOTAL.TIME=0
LET NUMBER.OF.DAYS=0
LET SYSTEM.DAYS=0

LET CURRENT.DATE=STARTING.DATE
WHILE CURRENT.DATE<=ENDING.DATE DO
      BEGIN
      THIS.DATE:
      FOR EACH SYSTEM.DAYS. WHERE CLASS.ID=ID OF CLASS.
          AND DATE.=CURRENT.DATE
          BEGIN
          ADD 1 TO NUMBER.OF.DAYS
          FINISH THIS.DATE
          END
      ADD 1 TO CURRENT.DATE
      END

IF ABBREV = 'FNA'
  THEN LET FNA.NUMBER.OF.DAYS = NUMBER.OF.DAYS
IF ABBREV = 'FNC'
  THEN LET FNC.NUMBER.OF.DAYS = NUMBER.OF.DAYS
IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC'))
  THEN IF FOO.FLAG = 1
         THEN BEGIN
                IF FNA.NUMBER.OF.DAYS GE FNC.NUMBER.OF.DAYS
                  THEN LET FOO.NUMBER.OF.DAYS = FNA.NUMBER.OF.DAYS
                  ELSE LET FOO.NUMBER.OF.DAYS = FNC.NUMBER.OF.DAYS
              END

         ELSE NOTHING
FOR EACH SYSTEM.DAYS. WHERE CLASS.ID=ID OF CLASS.
    BEGIN
    ADD SYSTEMS TO SYSTEM.DAYS,ACTUAL.SYSTEM.DAYS,FACTORED.SYSTEM.DAYS
    IF ABBREV OF CLASS.="PDP" OR "SRI" OR "370" THEN
       ADD SYSTEMS TO FACTORED.SYSTEM.DAYS
    IF ABBREV OF CLASS.="370" THEN ADD SYSTEMS TO FACTORED.SYSTEM.DAYS
    IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC'))
      THEN ADD SYSTEMS TO FOO.SYSTEM.DAYS

    END

FOR EACH AVAILABILITY. WHERE CLASS.ID=ID OF CLASS.
    BEGIN
    ADD UP.TIME TO CLASS.UP.TIME,ACTUAL.UP.TIME,FACTORED.UP.TIME
    ADD ADJUSTED.UP.TIME TO CLASS.ADJUSTED.UP.TIME,ACTUAL.ADJUSTED.UP.TIME,FACTORED.ADJUSTED.UP.TIME
    ADD TOTAL.TIME TO CLASS.TOTAL.TIME,ACTUAL.TOTAL.TIME,FACTORED.TOTAL.TIME
    IF ABBREV OF CLASS.="PDP" OR "SRI" OR "370" THEN
       BEGIN
       ADD UP.TIME TO FACTORED.UP.TIME
       ADD ADJUSTED.UP.TIME TO FACTORED.ADJUSTED.UP.TIME
       ADD TOTAL.TIME TO FACTORED.TOTAL.TIME
       END
    IF ABBREV OF CLASS.="370" THEN
       BEGIN
       ADD UP.TIME TO FACTORED.UP.TIME
       ADD ADJUSTED.UP.TIME TO FACTORED.ADJUSTED.UP.TIME
       ADD TOTAL.TIME TO FACTORED.TOTAL.TIME
       END
    IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC'))
      THEN BEGIN
             ADD UP.TIME TO FOO.CLASS.UP.TIME
             ADD ADJUSTED.UP.TIME TO FOO.CLASS.ADJUSTED.UP.TIME
             ADD TOTAL.TIME TO FOO.CLASS.TOTAL.TIME
           END

    END

FOR EACH CRASH. WHERE CLASS.ID=ID OF CLASS.
    BEGIN
    ADD CRASHES TO CLASS.CRASHES,ACTUAL.CRASHES,FACTORED.CRASHES
    ADD ADJUSTED.CRASHES TO CLASS.ADJUSTED.CRASHES,ACTUAL.ADJUSTED.CRASHES,FACTORED.ADJUSTED.CRASHES
    IF ABBREV OF CLASS.="PDP" OR "SRI" OR "370" THEN
       BEGIN
       ADD CRASHES TO FACTORED.CRASHES
       ADD ADJUSTED.CRASHES TO FACTORED.ADJUSTED.CRASHES
       END
    IF ABBREV OF CLASS.="370" THEN
       BEGIN
       ADD CRASHES TO FACTORED.CRASHES
       ADD ADJUSTED.CRASHES TO FACTORED.ADJUSTED.CRASHES
       END
    IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC'))
      THEN BEGIN
             ADD CRASHES TO FOO.CLASS.CRASHES
             ADD ADJUSTED.CRASHES TO FOO.CLASS.ADJUSTED.CRASHES
             ADD 1 TO FOO.FLAG
           END
    END

IF ((ABBREV = 'FNA') OR (ABBREV = 'FNC'))
  THEN IF FOO.FLAG = 2
         THEN BEGIN
                PRINT (FOO.SYSTEM.DAYS /FOO.NUMBER.OF.DAYS) AS '3Z.NN'
                PRINT (FOO.CLASS.UP.TIME / FOO.CLASS.TOTAL.TIME * 100) AS '4Z.NN', '%'
                PRINT ((FOO.CLASS.ADJUSTED.UP.TIME - FOO.CLASS.UP.TIME) / FOO.CLASS.TOTAL.TIME * 100)
                      AS '4Z.NN', '%'
                PRINT (FOO.CLASS.ADJUSTED.UP.TIME / FOO.CLASS.TOTAL.TIME * 100) AS '4Z.NN', '%'
                PRINT FOO.CLASS.CRASHES AS '5Z'
                PRINT (FOO.CLASS.CRASHES - FOO.CLASS.ADJUSTED.CRASHES) AS '5Z'
                PRINT FOO.CLASS.ADJUSTED.CRASHES AS '5Z'
                PRINT (FOO.CLASS.CRASHES * FOO.NUMBER.OF.DAYS / FOO.SYSTEM.DAYS / MONTHS)
                      AS '4Z.NN'
                PRINT (FOO.CLASS.ADJUSTED.CRASHES * FOO.NUMBER.OF.DAYS / FOO.SYSTEM.DAYS /
                      MONTHS) AS '5Z.NN'
                PRINT @CR
              END
         ELSE NOTHING
  ELSE BEGIN
         PRINT (SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"

         PRINT (CLASS.UP.TIME/CLASS.TOTAL.TIME*100) AS "4Z.NN","%"
         PRINT ((CLASS.ADJUSTED.UP.TIME-CLASS.UP.TIME)/CLASS.TOTAL.TIME*100) AS "4Z.NN","%"
         PRINT (CLASS.ADJUSTED.UE/CLASS.TOTAL.TIME*100) AS "4Z.NN","%"

         PRINT CLASS.CRASHES AS "5Z"
         PRINT (CLASS.CRASHES-CLASS.ADJUSTED.CRASHES) AS "5Z"
         PRINT CLASS.ADJUSTED.CRASHES AS "5Z"

         PRINT (CLASS.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "4Z.NN"
         PRINT (CLASS.ADJUSTED.CRASHES*NUMBER.OF.DAYS/SYSTEM.DAYS/MONTHS) AS "5Z.NN"

         PRINT @CR
       END

END %FOR EACH CLASS TOTAL%

%OUTPUT TOTALS FOR ALL SYSTEMS IN REPORT%

LET NUMBER.OF.DAYS=0
LET CURRENT.DATE=STARTING.DATE
WHILE CURRENT.DATE<=ENDING.DATE DO
      BEGIN
      THIS.DATE.:
      FOR EACH SYSTEM.DAYS. WHERE DATE.=CURRENT.DATE
          BEGIN
          ADD 1 TO NUMBER.OF.DAYS
          FINISH THIS.DATE.
          END
      ADD 1 TO CURRENT.DATE
      END %WHILE LOOP%

PRINT @CR," " AS "15C",@TAB TO 17,"ALL"

PRINT (ACTUAL.SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"

PRINT (ACTUAL.UP.TIME/ACTUAL.TOTAL.TIME*100) AS "4Z.NN","%"
PRINT ((ACTUAL.ADJUSTED.UP.TIME-ACTUAL.UP.TIME)/ACTUAL.TOTAL.TIME*100) AS "4Z.NN","%"
PRINT (ACTUAL.ADJUSTED.UP.TIME/ACTUAL.TOTAL.TIME*100) AS "4Z.NN","%"

PRINT ACTUAL.CRASHES AS "5Z"
PRINT (ACTUAL.CRASHES-ACTUAL.ADJUSTED.CRASHES) AS "5Z"
PRINT ACTUAL.ADJUSTED.CRASHES AS "5Z"

PRINT (ACTUAL.CRASHES*NUMBER.OF.DAYS/ACTUAL.SYSTEM.DAYS/MONTHS) AS "4Z.NN"
PRINT (ACTUAL.ADJUSTED.CRASHES*NUMBER.OF.DAYS/ACTUAL.SYSTEM.DAYS/MONTHS) AS "5Z.NN"

PRINT @CR

%
PRINT "FACTORED TOTALS" AS "15C",@TAB TO 17,"ALL"

PRINT (FACTORED.SYSTEM.DAYS/NUMBER.OF.DAYS) AS "3Z.NN"

PRINT (FACTORED.UP.TIME/FACTORED.TOTAL.TIME*100) AS "4Z.NN"," "
PRINT ((FACTORED.ADJUSTED.UP.TIME-FACTORED.UP.TIME)/FACTORED.TOTAL.TIME*100) AS "4Z.NN"," "
PRINT (FACTORED.ADJUSTED.UP.TIME/FACTORED.TOTAL.TIME*100) AS "4Z.NN"," "

PRINT FACTORED.CRASHES AS "5Z"
PRINT (FACTORED.CRASHES-FACTORED.ADJUSTED.CRASHES) AS "5Z"
PRINT FACTORED.ADJUSTED.CRASHES AS "5Z"

PRINT (FACTORED.CRASHES*NUMBER.OF.DAYS/FACTORED.SYSTEM.DAYS/MONTHS) AS "4Z.NN"
PRINT (FACTORED.ADJUSTED.CRASHES*NUMBER.OF.DAYS/FACTORED.SYSTEM.DAYS/MONTHS) AS "4Z.NN"

PRINT @CR
%
END %WRITE REPORT%

END
+$y¡