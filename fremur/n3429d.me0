PROCEDURE MONTHLY
BEGIN

FIELDS

ENDING.DATE.INPUT.MMM   AS DATE "MMM YY",
MONTH.COUNT             AS "N",
DATE.STRING             AS "8C",
STARTING.DATE           SAME AS DATE.,
ENDING.DATE.INPUT       AS DATE "MM-YY",
ENDING.DATE             SAME AS DATE.,
MON.TH                   AS "NN",
YEA.R                    AS "NN",
TIME.SITE.ID            SAME AS SITE.ID,
TIME.CLASS.ID           SAME AS CLASS.ID,
PERIODS                 AS "4N",
MULTIPLE.SYSTEMS        AS "C" DEFAULT "N",
SITE.INPUT              AS "3C",
CLASS.INPUT             AS "3C",
TOTAL.UP.TIME           AS "4ZN.5N",
TOTAL.SCHEDULED.UP.TIME AS "4ZN.5N",
TOTAL.PRIME.UP.TIME     AS "4ZN.5N",
TOTAL.ADJUSTED.UP.TIME  AS "4ZN.5N",
TOTAL.TIME              AS "4ZN.5N",
TOTAL.SCHEDULED.TIME    AS "4ZN.5N",
TOTAL.PRIME.TIME        AS "4ZN.5N",
SIX.MONTH.TOTAL         AS "4ZN",
SIX.MONTH.UP.TOTAL      AS "4ZN.5N",
DD                      AS DATE "  MMM YY",
SYSTEM.NUMBER           AS "3ZN",
PRIME.CRASH.FLAG        AS "N",
SCHEDULED.CRASH.FLAG    AS "N",
CRASH.FLAG              AS "N",
CURRENT.DATE            SAME AS DATE.,
ZERO                    AS "N",
ELAPSED.TIME            AS "4ZN",

UN.SCHEDULED.TIME       AS "4ZN",
SCHEDULED.TIME          AS "4ZN",
NON.PRIME.TIME          AS "4ZN",
PRIME.TIME              AS "4ZN",

ALL.CRASHES             AS "ZN",
SCHEDULED.CRASHES       AS "ZN",
PRIME.CRASHES           AS "ZN",
ADJUSTED.CRASHES        AS "ZN"

CONTROLS FOR RELATIONS CONCURRENT UPDATE

REPORT TO OUTPUT.FILE PAGE.SIZE 1

RELATION TOTALS. IS
KEY     MON.TH,
        CODE.ID
DATA    UN.SCHEDULED.TIME,
        SCHEDULED.TIME,
        NON.PRIME.TIME,
        PRIME.TIME

RELATION GROUP.TOTALS. IS
KEY     MON.TH,
        GROUP.ID
DATA    UN.SCHEDULED.TIME,
        SCHEDULED.TIME,
        NON.PRIME.TIME,
        PRIME.TIME

RELATION AVAILABILITY. IS
KEY     MON.TH
DATA    TOTAL.UP.TIME,
        TOTAL.TIME,
        TOTAL.PRIME.TIME,
        TOTAL.PRIME.UP.TIME,
        TOTAL.SCHEDULED.TIME,
        TOTAL.SCHEDULED.UP.TIME

RELATION CRASH. IS
KEY     MON.TH,
        CODE.ID
DATA    ALL.CRASHES,
        SCHEDULED.CRASHES,
        PRIME.CRASHES,
        ADJUSTED.CRASHES

RELATION CRASH.TOTALS. IS
KEY     MON.TH
DATA    ALL.CRASHES,
        SCHEDULED.CRASHES,
        PRIME.CRASHES,
        ADJUSTED.CRASHES

RELATION PERIOD. IS
KEY     STARTING.DATE
DATA    ENDING.DATE

TYPE "ENDING DATE OF REPORT AS MMM YY: " ACCEPT ENDING.DATE.INPUT.MMM
LET ENDING.DATE.INPUT=ENDING.DATE.INPUT.MMM

SET.END.DATE:
BEGIN
LET DATE.STRING=SUBSTR(ENDING.DATE.INPUT AS "5C",1,2)+"-31-" AS "4C"+
    SUBSTR(ENDING.DATE.INPUT AS "5C",-2)
IF DATE.STRING IS ENDING.DATE THEN
   BEGIN
   LET ENDING.DATE=DATE.STRING AS DATE "MM-DD-YY"
   FINISH SET.END.DATE
   END

LET DATE.STRING=SUBSTR(ENDING.DATE.INPUT AS "5C",1,2)+"-30-" AS "4C"+
    SUBSTR(ENDING.DATE.INPUT AS "5C",-2)
IF DATE.STRING IS ENDING.DATE THEN
   BEGIN
   LET ENDING.DATE=DATE.STRING AS DATE "MM-DD-YY"
   FINISH SET.END.DATE
   END

LET DATE.STRING=SUBSTR(ENDING.DATE.INPUT AS "5C",1,2)+"-29-" AS "4C"+
    SUBSTR(ENDING.DATE.INPUT AS "5C",-2)
IF DATE.STRING IS ENDING.DATE THEN
   BEGIN
   LET ENDING.DATE=DATE.STRING AS DATE "MM-DD-YY"
   FINISH SET.END.DATE
   END

LET DATE.STRING=SUBSTR(ENDING.DATE.INPUT AS "5C",1,2)+"-28-" AS "4C"+
    SUBSTR(ENDING.DATE.INPUT AS "5C",-2)
IF DATE.STRING IS ENDING.DATE THEN
   LET ENDING.DATE=DATE.STRING AS DATE "MM-DD-YY"

END %SET.END.DATE%

LET MON.TH=SUBSTR(ENDING.DATE AS "8C",1,2) AS "NN"
LET YEA.R =SUBSTR(ENDING.DATE AS "8C",7,2) AS "NN"

IF MON.TH>=6 THEN SUBTRACT 5 FROM MON.TH ELSE
   BEGIN
   SUBTRACT 1 FROM YEA.R
   ADD 7 TO MON.TH
   END

LET DATE.STRING=MON.TH AS "CC"+"-01-" AS "4C"
LET DATE.STRING=SUBSTR(DATE.STRING,1,6)+YEA.R AS "CC"
LET STARTING.DATE=DATE.STRING AS DATE "MM-DD-YY"

GET.PERIODS:
BEGIN
TYPE "PERIODS: " ACCEPT PERIODS
IF PERIODS<=0 THEN REPEAT GET.PERIODS
END %GET.PERIODS%

INSERT INTO PERIOD.

WHILE PERIODS>1 DO
      BEGIN
      SUBTRACT 1 FROM PERIODS
      LET ENDING.DATE=STARTING.DATE-1
      LET MON.TH=SUBSTR(ENDING.DATE AS "8C",1,2) AS "NN"
      LET YEA.R =SUBSTR(ENDING.DATE AS "8C",7,2) AS "NN"
      IF MON.TH>=6 THEN SUBTRACT 5 FROM MON.TH ELSE
         BEGIN
         SUBTRACT 1 FROM YEA.R
         ADD 7 TO MON.TH
         END
      LET DATE.STRING=MON.TH AS "CC"+"-01-" AS "4C"
      LET DATE.STRING=SUBSTR(DATE.STRING,1,6)+YEA.R AS "CC"
      LET STARTING.DATE=DATE.STRING AS DATE "MM-DD-YY"
      INSERT INTO PERIOD.
      END %WHILE%

GET.SYSTEM.NUMBER:
BEGIN
TYPE "SYSTEM NUMBER: " ACCEPT SYSTEM.NUMBER

IF SYSTEM.NUMBER=0 THEN
   BEGIN

   GET.SITE:
   BEGIN
   TYPE "SITE: " ACCEPT SITE.INPUT
   IF SITE.INPUT="ALL" THEN FINISH GET.SITE
   FOR EACH SITE. IF ABBREV=SITE.INPUT THEN FINISH GET.SITE
   TYPE "NO SUCH SITE",@CR
   REPEAT GET.SITE
 END %GET.SITE%

 GET.CLASS:
 BEGIN
 TYPE "CLASS: " ACCEPT CLASS.INPUT
 IF CLASS.INPUT="ALL" THEN FINISH GET.CLASS
 FOR EACH CLASS. IF ABBREV=CLASS.INPUT THEN FINISH GET.CLASS
 TYPE "NO SUCH CLASS",@CR
 REPEAT GET.CLASS
 END %GET.CLASS%

END %IF SYSTEM.NUMBER=0%

IF SYSTEM.NUMBER=0 THEN FINISH GET.SYSTEM.NUMBER

FIND.SYSNO:
     BEGIN
     FOR EACH SYSTEM. WHERE SYSNO=SYSTEM.NUMBER FINISH FIND.SYSNO
    TYPE "NO SUCH SYSTEM",@CR
    REPEAT GET.SYSTEM.NUMBER
     END %FIND.SYSNO%
END %GET.SYSTEM.NUMBER%

GET.OUTPUT.FILE.NAME:
BEGIN
TYPE "OUTPUT TO: " ACCEPT OUTPUT.FILE

IF OUTPUT.FILE="TER" OR "TERM" OR "TERMI" OR "TERMIN" OR "TERMINA"
   OR "TERMINAL" THEN FINISH GET.OUTPUT.FILE.NAME
IF USER.NAME NE "MURMAINT" THEN LET REPORT.DIRECTORY = "("+USER.NAME+")"
IF SUBSTR(OUTPUT.FILE,1,1)="(" AS "C" THEN
   BEGIN
   TYPE @CR,"REPORTS ARE FORCED INTO USER NAME ",REPORT.DIRECTORY
   TYPE @CR,"PLEASE TYPE IN A FILE NAME ONLY",@CR,@CR
   REPEAT GET.OUTPUT.FILE.NAME
   END
  ELSE
   BEGIN
   LET OUTPUT.FILE=REPORT.DIRECTORY+OUTPUT.FILE
   TYPE "REPORT WILL BE ON FILE ",OUTPUT.FILE,@CR
   END
END %GET.OUTPUT.FILE.NAME%


WRITE.REPORT:
WRITE REPORT BEGIN %OPEN UP OUTPUT FILE%

FOR EACH PERIOD.
      BEGIN

FOR EACH SYSTEM. SORTED ON SITE.ID,CLASS.ID,SYSNO
    SYSTEM.REPORT:
    BEGIN

    IF SYSTEM.NUMBER#0 AND SYSNO#SYSTEM.NUMBER THEN FINISH SYSTEM.REPORT

    IF SYSTEM.NUMBER=0 THEN 
       IF SITE.INPUT="ALL" THEN NOTHING ELSE
       IF SITE.INPUT=ABBREV(SITE. VIA ID=SITE.ID) THEN NOTHING ELSE
       FINISH SYSTEM.REPORT

   IF SYSTEM.NUMBER=0 THEN
      IF CLASS.INPUT="ALL" THEN NOTHING ELSE
      IF CLASS.INPUT=ABBREV(CLASS. VIA ID=CLASS.ID) THEN NOTHING ELSE
      FINISH SYSTEM.REPORT

      LET SYSTEM.ID=ID OF SYSTEM.

%IF THIS SYSNO HAS NO TIME FOR THIS MONTH THEN SKIP IT%

    CHECK.FOR.TIME:
    BEGIN
    LET CURRENT.DATE=STARTING.DATE-1
    WHILE CURRENT.DATE<ENDING.DATE DO
     BEGIN
     ADD 1 TO CURRENT.DATE
     FOR EACH TIME. VIA (SYSTEM.ID,DATE.=CURRENT.DATE) FINISH CHECK.FOR.TIME
     END
    FINISH SYSTEM.REPORT
    END %CHECK.FOR.TIME%

%INITIALIZE RELATIONS%

DELETE FROM TOTALS. ALL
DELETE FROM GROUP.TOTALS. ALL
DELETE FROM AVAILABILITY. ALL
DELETE FROM CRASH. ALL
DELETE FROM CRASH.TOTALS. ALL

LET DD=STARTING.DATE
LET CURRENT.DATE=STARTING.DATE-1

 FOR EACH TIME. VIA SYSTEM.ID, WHERE (DATE.>=STARTING.DATE AND <=ENDING.DATE)
     BEGIN
     LET CURRENT.DATE=DATE.
     LET TIME.SITE.ID=SITE.ID
     LET TIME.CLASS.ID=CLASS.ID
     LET ELAPSED.TIME=STOP.TIME-START.TIME
     SELECT.TOTALS:
        BEGIN
         SELECT TOTALS. VIA (MON.TH=((DATE. AS DATE "MM") AS "CC") AS "NN",CODE.ID)
                THEN
                    BEGIN
                    CONDITIONAL ON SCHED.FLAG
                                BEGIN
                                1: ADD ELAPSED.TIME TO SCHEDULED.TIME
                                2: ADD ELAPSED.TIME TO UN.SCHEDULED.TIME
                                END
                    CONDITIONAL ON PRIME.FLAG
                                BEGIN
                                1: ADD ELAPSED.TIME TO PRIME.TIME
                                2: ADD ELAPSED.TIME TO NON.PRIME.TIME
                                END
                    ALTER TOTALS.
                    END
               ELSE
                   BEGIN
                   LET MON.TH=((DATE. AS DATE "MM") AS "CC") AS "NN"
                   INSERT INTO TOTALS. USING
                           (ZERO FOR UN.SCHEDULED.TIME,
                           ZERO FOR SCHEDULED.TIME,
                           ZERO FOR NON.PRIME.TIME,
                           ZERO FOR PRIME.TIME)
                   REPEAT SELECT.TOTALS
                   END
        END %SELECT.TOTALS%
     SELECT.CRASH:
        BEGIN
        SELECT CRASH. VIA (MON.TH=((DATE. AS DATE "MM") AS "CC") AS "NN",CODE.ID)
               THEN
                   BEGIN
                   IF CRASH.FLAG=1 AND TYPE.ABBREV(CODE. VIA ID=CODE.ID)="LST"
                      THEN ADD 1 TO ALL.CRASHES
                   IF TYPE.ABBREV(CODE. VIA ID=CODE.ID)="UP"
                      THEN LET CRASH.FLAG=1
                      ELSE LET CRASH.FLAG=0
                   CONDITIONAL ON SCHED.FLAG
                       BEGIN
                       1: CONDITIONAL ON TYPE.ABBREV(CODE. VIA ID=CODE.ID)
                              BEGIN
                               "UP": LET SCHEDULED.CRASH.FLAG=1
                              "SDN": LET SCHEDULED.CRASH.FLAG=0
                              "LST": BEGIN
                                     IF SCHEDULED.CRASH.FLAG=1 THEN
                                        BEGIN
                                        ADD 1 TO SCHEDULED.CRASHES
                                        IF EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="N"
                                           THEN ADD 1 TO ADJUSTED.CRASHES
                                           END
                                     LET SCHEDULED.CRASH.FLAG=0
                                     END
                              END
                       2: LET SCHEDULED.CRASH.FLAG=0
                       END %OF CONDITIONAL ON SCHED.FLAG%
                   CONDITIONAL ON PRIME.FLAG
                       BEGIN
                       1: BEGIN
                          CONDITIONAL ON TYPE.ABBREV(CODE. VIA ID=CODE.ID)
                              BEGIN
                               "UP": LET PRIME.CRASH.FLAG=1
                              "SDN": LET PRIME.CRASH.FLAG=0
                              "LST": BEGIN
                                     IF PRIME.CRASH.FLAG=1 THEN ADD 1 TO PRIME.CRASHES
                                     LET PRIME.CRASH.FLAG=0
                                     END
                              END
                          END
                       2: LET PRIME.CRASH.FLAG=0
                       END
                    ALTER CRASH.
                    END
               ELSE
                   BEGIN
                   LET MON.TH=((DATE. AS DATE "MM") AS "CC") AS "NN"
                   INSERT INTO CRASH. USING
                          (ZERO FOR ALL.CRASHES,
                          ZERO FOR SCHEDULED.CRASHES,
                           ZERO FOR PRIME.CRASHES,
                          ZERO FOR ADJUSTED.CRASHES)
                   REPEAT SELECT.CRASH
                   END
        END %SELECT.CRASH%
     END %FOR EACH TIME%

%PUT CODE TOTALS INTO GROUP TOTALS%

FOR EACH TOTALS.
 SELECT GROUP.TOTALS. VIA (MON.TH,GROUP.ID=GROUP.ID(CODE. VIA ID=CODE.ID)) THEN
        BEGIN
        ADD UN.SCHEDULED.TIME OF TOTALS. TO UN.SCHEDULED.TIME OF GROUP.TOTALS.
        ADD SCHEDULED.TIME OF TOTALS. TO SCHEDULED.TIME OF GROUP.TOTALS.
        ADD NON.PRIME.TIME OF TOTALS. TO NON.PRIME.TIME OF GROUP.TOTALS.
        ADD PRIME.TIME OF TOTALS. TO PRIME.TIME OF GROUP.TOTALS.
        ALTER GROUP.TOTALS.
        END
     ELSE INSERT INTO GROUP.TOTALS. USING (GROUP.ID(CODE. VIA ID=CODE.ID) FOR GROUP.ID)

%OUTPUT REPORT HEADING%

IF MULTIPLE.SYSTEMS="Y" THEN PRINT @FF
LET MULTIPLE.SYSTEMS="Y"
PRINT @CR,@TAB TO 25,
      TRIM(TITLE(SITE. VIA ID=TIME.SITE.ID))," ",
      TRIM(TITLE(CLASS. VIA ID=TIME.CLASS.ID)),
      " SYSTEM ",SYSNO
      IF APPLICABLE="Y" THEN PRINT @CR ELSE PRINT " (N/A)",@CR
PRINT @TAB TO 19,DD,DD+31,DD+31*2,DD+31*3,DD+31*4,DD+31*5,"   TOTALS",@CR

%OUTPUT INDIVIDUAL CODE TOTALS%

 FOR EACH GROUP. WHERE TYPE.ABBREV="UP" OR "SDN" OR "LST" SORTED ON POS
 FOR EACH CODE. WHERE CLASS.ID=CLASS.ID OF SYSTEM.
  AND GROUP.ID=ID OF GROUP.
  AND NOT (START.DATE>ENDING.DATE OR END.DATE<STARTING.DATE)
  SORTED ON CODE
  BEGIN
  PRINT CODE AS "ZN"
  IF EXCEPTION.FLAG="N" THEN PRINT " " ELSE PRINT "*"
  PRINT TITLE
  LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
  SELECT TOTALS. VIA (MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN",
                      CODE.ID=ID OF CODE.) THEN
   BEGIN
   LET ELAPSED.TIME=UN.SCHEDULED.TIME+SCHEDULED.TIME
   ADD ELAPSED.TIME TO SIX.MONTH.TOTAL
   PRINT (ELAPSED.TIME/60) AS "4ZB",":",
         ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
   END
   ELSE PRINT @TAB 8
  END
 IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (SIX.MONTH.TOTAL/60) AS "5ZB",":",
          ((SIX.MONTH.TOTAL-(SIX.MONTH.TOTAL/60)*60)) AS "NN",@CR
  END %SINGLE CODE OUTPUT%

%OUTPUT MONTHLY TOTALS%

PRINT @TAB TO 4,"MONTHLY TOTALS" AS "15C"
LET SIX.MONTH.TOTAL=0

LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 LET ELAPSED.TIME=0
 FOR EACH GROUP.TOTALS. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
            ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO ELAPSED.TIME
 IF ELAPSED.TIME=0 THEN PRINT @TAB 8 ELSE
    PRINT (ELAPSED.TIME/60) AS "4ZB",":",
          ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
 ADD ELAPSED.TIME TO SIX.MONTH.TOTAL
 END %WHILE STATMENT FOR MONTHLY TOTALS OUTPUT%

 IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (SIX.MONTH.TOTAL/60) AS "5ZB",":",
          ((SIX.MONTH.TOTAL-(SIX.MONTH.TOTAL/60)*60)) AS "NN",@CR

%END MONTHLY TOTALS OUTPUT%

%OUTPUT SCHEDULED TIME GROUP TOTALS%

PRINT @CR,@TAB TO 28,"SCHEDULED TIME GROUP TOTALS",@CR
FOR EACH GROUP. WHERE TYPE.ABBREV="UP" OR "SDN" OR "LST" SORTED ON POS
 LIST.GROUP:
 BEGIN
 LIST.GROUP.CHECK:
      BEGIN
      FOR EACH CODE. WHERE GROUP.ID=ID OF GROUP. AND
          CLASS.ID=CLASS.ID OF SYSTEM. AND
          NOT (START.DATE>ENDING.DATE OR END.DATE<STARTING.DATE)
          FINISH LIST.GROUP.CHECK
      FINISH LIST.GROUP %DON'T LIST THIS GROUP%
 END %LIST.GROUP.CHECK%

 PRINT @TAB TO 4,TITLE
 LET SIX.MONTH.TOTAL=0
 LET MONTH.COUNT=0
 WHILE MONTH.COUNT<6 DO
  BEGIN
  LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
  ADD 1 TO MONTH.COUNT
  LET ELAPSED.TIME=0
   SELECT GROUP.TOTALS. VIA (MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN",
          GROUP.ID=ID OF GROUP.) THEN
 BEGIN
 IF SCHEDULED.TIME=0 THEN PRINT @TAB 8 ELSE
    BEGIN
    PRINT (SCHEDULED.TIME/60) AS "4ZB",":",
        ((SCHEDULED.TIME-(SCHEDULED.TIME/60)*60)) AS "NN"
    ADD SCHEDULED.TIME TO SIX.MONTH.TOTAL
    END
  SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
           BEGIN
           ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO TOTAL.TIME
           ADD SCHEDULED.TIME TO TOTAL.SCHEDULED.TIME
           ADD PRIME.TIME TO TOTAL.PRIME.TIME
           IF TYPE.ABBREV="UP" THEN
              BEGIN
              ADD UN.SCHEDULED.TIME+SCHEDULED.TIME TO TOTAL.UP.TIME
              ADD SCHEDULED.TIME TO TOTAL.SCHEDULED.UP.TIME
              ADD PRIME.TIME TO TOTAL.PRIME.UP.TIME
              END
           ALTER AVAILABILITY.
           END
         ELSE
              BEGIN
              LET MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
              LET TOTAL.TIME=UN.SCHEDULED.TIME+SCHEDULED.TIME
              LET TOTAL.SCHEDULED.TIME=SCHEDULED.TIME
              LET TOTAL.PRIME.TIME=PRIME.TIME
              IF TYPE.ABBREV="UP" THEN
                 BEGIN
                 LET TOTAL.UP.TIME=UN.SCHEDULED.TIME+SCHEDULED.TIME
                 LET TOTAL.SCHEDULED.UP.TIME=SCHEDULED.TIME
                 LET TOTAL.PRIME.UP.TIME=PRIME.TIME
                 END
              INSERT INTO AVAILABILITY.
              END
  END %GROUP SELECTION%
  ELSE %IF NO SELECTION% PRINT @TAB 8

  END %WHILE%
 IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
    PRINT (SIX.MONTH.TOTAL/60) AS "5ZB",":",
          ((SIX.MONTH.TOTAL-(SIX.MONTH.TOTAL/60)*60)) AS "NN",@CR
 END %LIST GROUP%

%OUTPUT SCHEDULED TS TOTALS%

PRINT @TAB TO 4,"SCHEDULED TS" AS "15C"
LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
  BEGIN
  LET ELAPSED.TIME=TOTAL.SCHEDULED.TIME
  PRINT (ELAPSED.TIME/60) AS "4ZB",":",
        ((ELAPSED.TIME-(ELAPSED.TIME/60)*60)) AS "NN"
  ADD TOTAL.SCHEDULED.TIME TO SIX.MONTH.TOTAL
  END %OF SELECTION%
  ELSE PRINT @TAB 8
END %WHILE%
IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (SIX.MONTH.TOTAL/60) AS "5ZB",":",
         ((SIX.MONTH.TOTAL-(SIX.MONTH.TOTAL/60)*60)) AS "NN",@CR

%OUTPUT AVAILABILITY FIGURES%

PRINT @TAB TO 4,"AVAIL: 24 HOUR" AS "16C"
LET SIX.MONTH.TOTAL=0
LET SIX.MONTH.UP.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
      BEGIN
      LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
      ADD 1 TO MONTH.COUNT
      SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
             THEN
                 BEGIN
                 IF TOTAL.TIME=0 THEN PRINT @TAB 8 ELSE
                    PRINT (TOTAL.UP.TIME/TOTAL.TIME*100) AS "3ZB.NN","%"
                 ADD TOTAL.UP.TIME TO SIX.MONTH.UP.TOTAL
                 ADD TOTAL.TIME TO SIX.MONTH.TOTAL
                 END
             ELSE PRINT @TAB 8
     END %WHILE%

IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (SIX.MONTH.UP.TOTAL/SIX.MONTH.TOTAL*100) AS "4","%",@CR

PRINT @TAB TO 4,"AVAIL: PRIME" AS "16C"
LET SIX.MONTH.TOTAL=0
LET SIX.MONTH.UP.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
      BEGIN
      LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
      ADD 1 TO MONTH.COUNT
      SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
             THEN
                 BEGIN
                 IF TOTAL.PRIME.TIME=0 THEN PRINT @TAB 8 ELSE
                    PRINT (TOTAL.PRIME.UP.TIME/TOTAL.PRIME.TIME*100) AS "3ZB.NN","%"
                 ADD TOTAL.PRIME.UP.TIME TO SIX.MONTH.UP.TOTAL
                 ADD TOTAL.PRIME.TIME TO SIX.MONTH.TOTAL
                 END
             ELSE PRINT @TAB 8
     END %WHILE%

IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (SIX.MONTH.UP.TOTAL/SIX.MONTH.TOTAL*100) AS "4ZB.NN","%",@CR

PRINT @TAB TO 4,"AVAIL: SCHED" AS "16C"
LET SIX.MONTH.TOTAL=0
LET SIX.MONTH.UP.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
      BEGIN
      LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
      ADD 1 TO MONTH.COUNT
      SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
             THEN
                 BEGIN
                 IF TOTAL.SCHEDULED.TIME=0 THEN PRINT @TAB 8 ELSE
                    PRINT (TOTAL.SCHEDULED.UP.TIME/TOTAL.SCHEDULED.TIME*100) AS "3ZB.NN","%"
                 ADD TOTAL.SCHEDULED.UP.TIME TO SIX.MONTH.UP.TOTAL
                 ADD TOTAL.SCHEDULED.TIME TO SIX.MONTH.TOTAL
                 END
             ELSE PRINT @TAB 8
     END %WHILE%

IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (SIX.MONTH.UP.TOTAL/SIX.MONTH.TOTAL*100) AS "4ZB.NN","%",@CR

PRINT @TAB TO 3,"*","AVAIL: ADJ'D" AS "16C"
LET SIX.MONTH.TOTAL=0
LET SIX.MONTH.UP.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
      BEGIN
      LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
      ADD 1 TO MONTH.COUNT
      SELECT AVAILABILITY. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
             THEN
                 BEGIN
                 FOR EACH TOTALS.
                     VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN",
                     WHERE EXCEPTION.FLAG(CODE. VIA ID=CODE.ID)="Y"
                     ADD SCHEDULED.TIME TO TOTAL.SCHEDULED.UP.TIME
                 IF TOTAL.SCHEDULED.TIME=0 THEN PRINT @TAB 8 ELSE
                    PRINT (TOTAL.SCHEDULED.UP.TIME/TOTAL.SCHEDULED.TIME*100) AS "3ZB.NN","%"
                 ADD TOTAL.SCHEDULED.UP.TIME TO SIX.MONTH.UP.TOTAL
                 ADD TOTAL.SCHEDULED.TIME TO SIX.MONTH.TOTAL
                 END
             ELSE PRINT @TAB 8
     END %WHILE%

IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT (SIX.MONTH.UP.TOTAL/SIX.MONTH.TOTAL*100) AS "4ZB.NN","%",@CR

%OUTPUT SCHEDULED TIME CRASH SUMMARY%

PRINT @CR,@TAB TO 28,"SCHEDULED TIME CRASH SUMMARY",@CR

FOR EACH GROUP. WHERE TYPE.ABBREV="LST" SORTED ON POS
 FOR EACH CODE. WHERE (NOT (START.DATE>ENDING.DATE OR END.DATE<STARTING.DATE))
     AND CLASS.ID=CLASS.ID OF SYSTEM.
     AND GROUP.ID=ID OF GROUP. SORTED ON CODE
  BEGIN
  PRINT @TAB TO 3
  IF EXCEPTION.FLAG="Y" THEN PRINT "*" ELSE PRINT " "
  PRINT TITLE OF CODE.
  LET SIX.MONTH.TOTAL=0
  LET MONTH.COUNT=0
  WHILE MONTH.COUNT<6 DO
   BEGIN
   LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
   ADD 1 TO MONTH.COUNT
   SELECT CRASH. VIA (MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN",
          CODE.ID=ID OF CODE.) THEN
    BEGIN
    ADD SCHEDULED.CRASHES TO SIX.MONTH.TOTAL
    PRINT SCHEDULED.CRASHES AS "7ZB"
    SELECT CRASH.TOTALS. VIA MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
           THEN
               BEGIN
               ADD ALL.CRASHES OF CRASH. TO ALL.CRASHES OF CRASH.TOTALS.
               ADD SCHEDULED.CRASHES OF CRASH. TO SCHEDULED.CRASHES OF CRASH.TOTALS.
               ADD PRIME.CRASHES OF CRASH. TO PRIME.CRASHES OF CRASH.TOTALS.
               ADD ADJUSTED.CRASHES OF CRASH. TO ADJUSTED.CRASHES OF CRASH.TOTALS.
               ALTER CRASH.TOTALS.
               END
           ELSE
               BEGIN
               LET MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN"
               INSERT INTO CRASH.TOTALS.
               END
    END
   ELSE PRINT @TAB 8
  END %WHILE%
 
IF SIX.MONTH.TOTAL=0 THEN PRINT @CR ELSE
   PRINT SIX.MONTH.TOTAL AS "9Z",@CR

END %EACH GROUP OF CRASH CODES%

%OUTPUT MONTHLY CRASH TOTALS%

PRINT @TAB TO 4,"TOTALS: 24 HOUR" AS "15C"
LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 SELECT CRASH.TOTALS. VIA
        MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
        BEGIN
        ADD ALL.CRASHES TO SIX.MONTH.TOTAL
        PRINT ALL.CRASHES AS "7ZB"
        END
   ELSE PRINT @TAB 8
 END %WHILE%
PRINT SIX.MONTH.TOTAL AS "8ZB",@CR

PRINT @TAB TO 4,"TOTALS: PRIME" AS "15C"
LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 SELECT CRASH.TOTALS. VIA
        MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
        BEGIN
        ADD PRIME.CRASHES TO SIX.MONTH.TOTAL
        PRINT PRIME.CRASHES AS "7ZB"
        END
   ELSE PRINT @TAB 8
 END %WHILE%
PRINT SIX.MONTH.TOTAL AS "8ZB",@CR

PRINT @TAB TO 4,"TOTALS: SCHED" AS "15C"
LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 SELECT CRASH.TOTALS. VIA
        MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
        BEGIN
        ADD SCHEDULED.CRASHES TO SIX.MONTH.TOTAL
        PRINT SCHEDULED.CRASHES AS "7ZB"
        END
   ELSE PRINT @TAB 8
 END %WHILE%
PRINT SIX.MONTH.TOTAL AS "8ZB",@CR

PRINT @TAB TO 3,"*","TOTALS: ADJ'D" AS "15C"
LET SIX.MONTH.TOTAL=0
LET MONTH.COUNT=0
WHILE MONTH.COUNT<6 DO
 BEGIN
 LET CURRENT.DATE=STARTING.DATE+(MONTH.COUNT*31)
 ADD 1 TO MONTH.COUNT
 SELECT CRASH.TOTALS. VIA
        MON.TH=((CURRENT.DATE AS DATE "MM") AS "CC") AS "NN" THEN
        BEGIN
        ADD ADJUSTED.CRASHES TO SIX.MONTH.TOTAL
        PRINT ADJUSTED.CRASHES AS "7ZB"
        END
   ELSE PRINT @TAB 8
 END %WHILE%
PRINT SIX.MONTH.TOTAL AS "8ZB",@CR

END %SYSTEM.REPORT%

END %SIX MONTH PERIOD REPORT%

END %REPORT%
END
    Rj3